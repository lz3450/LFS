# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=6.12.28
pkgrel=3
epoch=
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
options=(!strip)
source=(
  git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#branch=linux-${pkgver%.*}.y
  config
  update-kernel.sh
)
sha256sums=('SKIP'
            'ab55ab11da014bb89962fa65191783438d811e8c461fc1e7a36fbac46d45df2f'
            '5d9b124f6106ffc1a44957b1185cf8b078b51ac1151b8f9a8cdc27e8e9f8c5f8')

export LOCALVERSION="-KZL"
export KBUILD_BUILD_HOST="Ubuntu"
export KBUILD_BUILD_USER="kzl"
export KBUILD_BUILD_TIMESTAMP="$(date '+%Y/%m/%d %T %z') ($pkgrel)"

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/^v//'
}

prepare() {
  cd $pkgbase

  mkdir -p build

  cp "$srcdir"/config build/.config
}

build() {
  cd $pkgbase

  make O=build all
}

package_linux() {
  install=$pkgname.install

  cd $pkgbase

  local _kernelrelease=$(make O=build -s kernelrelease)
  local _image_name=$(make O=build -s image_name)

  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 O=build modules_install

  install -dm755 "$pkgdir"/boot/efi/
  install -Dm755 "build/$_image_name" "$pkgdir/boot/vmlinuz-$pkgver-KZL"
  install -Dm755 "build/$_image_name" "$pkgdir/boot/efi/vmlinuz-KZL"
  install -Dm644 "$srcdir/config" "$pkgdir/boot/config-$pkgver-KZL"

  ln -vsf --no-dereference "/usr/src/linux-headers-$_kernelrelease" "$pkgdir/usr/lib/modules/$_kernelrelease/source"
  ln -vsf --no-dereference "/usr/src/linux-headers-$_kernelrelease/build" "$pkgdir/usr/lib/modules/$_kernelrelease/build"

  sed "s/@pkgver@/$pkgver/g" "$srcdir"/update-kernel.sh | install -Dm755 /dev/stdin "$pkgdir"/boot/update-kernel.sh
}

package_linux-headers() {
  cd $pkgbase

  local _kernelrelease=$(make O=build -s kernelrelease)

  install -dm755 "$pkgdir"/usr/src/
  rsync -a \
    --exclude='*.o' \
    --exclude='*.ko*' \
    --exclude='*.a' \
    --exclude='*.cmd' \
    --exclude='*.o.d' \
    --exclude='*.so.dbg' \
    --exclude='.git*' \
    --exclude='.checked*' \
    --exclude='.clang-format' \
    --exclude='.cocciconfig' \
    --exclude='.get_maintainer.ignore' \
    --exclude='.mailmap' \
    --exclude='.missing-syscalls.d' \
    --exclude='.rustfmt.toml' \
    --exclude='.tmp_vmlinux*' \
    --exclude='.vmlinux' \
    --exclude='vmlinux.bin' \
    --exclude='build/arch/x86/boot/setup.elf' \
    --exclude='COPYING' \
    --exclude='CREDITS' \
    --exclude='Documentation' \
    --exclude='LICENSES' \
    --exclude='MAINTAINERS' \
    --exclude='README' \
    --include='arch/Kconfig' \
    --include='arch/x86' \
    --exclude='arch/*' \
    --exclude='tools/build/**.c' \
    --include='build/**.c' \
    --include='scripts/**.c' \
    --exclude='*.c' \
    "$srcdir/$pkgbase/" "$pkgdir/usr/src/linux-headers-$_kernelrelease"
}
