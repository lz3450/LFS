# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rust
pkgver=1.85.0
pkgrel=7
epoch=
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('KZL')
depends=(
  llvm-project
)
source=(
  # git+https://github.com/rust-lang/rust.git#branch=stable
  git+https://github.com/rust-lang/rust.git#tag=$pkgver
)
sha256sums=('04a229830fca2f721d0ca2579b2eb2e05f79c54dd9cd944763712a6f4620bc35')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-g.*//;s/-/.g/'
}

build() {
  cd $pkgname

  local _target="$(clang -print-target-triple)"
  local _change_id=$(grep -oP 'change_id: \K\d+' src/bootstrap/src/utils/change_tracker.rs | sort -n | tail -n1)
  local -a _options=(
    ################
    # stage2
    ################
    # --enable-local-rust
    # --local-rust-root=/"$PREFIX"
    ################
    --enable-llvm-link-shared
    --enable-rpath
    --enable-sanitizers
    --enable-profiler
    "--prefix=/$PREFIX"
    "--sysconfdir=etc"
    "--llvm-root=/$PREFIX"
    "--llvm-config=/$PREFIX/bin/llvm-config"
    "--llvm-filecheck=/$PREFIX/bin/FileCheck"
    "--python=/$PREFIX/bin/python3"
    "--release-channel=stable"
    "--release-description=KZL $(date '+%Y/%m/%d %T %z')"
    "--tools=cargo,clippy,rustfmt,rust-analyzer,rust-analyzer-proc-macro-srv,analysis,src,wasm-component-ld"
    "--build=$_target"
    "--host=$_target"
    "--target=$_target"
    --enable-option-checking
    --enable-verbose-configure
    "--set=change-id=$_change_id"
    "--set=rust.frame-pointers=true"
    "--set=rust.backtrace-on-ice=true"
    "--set=rust.remap-debuginfo=true"
    "--set=rust.jemalloc=true"
    "--set=target.$_target.cc=/$PREFIX/bin/clang"
    "--set=target.$_target.cxx=/$PREFIX/bin/clang++"
    "--set=target.$_target.ar=/$PREFIX/bin/llvm-ar"
    "--set=target.$_target.ranlib=/$PREFIX/bin/llvm-ranlib"
    "--set=target.$_target.linker=/$PREFIX/bin/clang"
    "--set=build.lldb=/$PREFIX/bin/lldb"
    "--set=rust.lld=true"
    "--set=rust.use-lld=true"
  )

  msg2 "Configuring..."
  ./configure \
    "${_options[@]}"
  cat config.toml

  msg2 "Compiling..."
  RUST_BACKTRACE=full ./x.py -j "$(nproc)" build
}

check() {
  cd $pkgname

  ./x.py test
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ./x.py -j "$(nproc)" install

  rm -f "$pkgdir"/$PREFIX/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}
  rm -rf "$pkgdir"/$PREFIX/share/doc
}
