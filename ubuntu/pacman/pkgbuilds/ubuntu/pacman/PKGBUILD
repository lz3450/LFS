# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman
pkgver=7.0.0.g40
pkgrel=35
epoch=
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
license=('KZL')
backup=(
  "$PREFIX/etc/makepkg.conf"
  "$PREFIX/etc/makepkg.conf.d/rust.conf"
  "$PREFIX/etc/pacman.conf"
)
# _ref="v$pkgver"
# source=(git+https://gitlab.archlinux.org/pacman/pacman.git#tag=$_ref)
# source=(git+https://gitlab.archlinux.org/pacman/pacman.git#branch=$_ref)
# source=(git+https://gitlab.archlinux.org/pacman/pacman.git#commit=$_ref)
source=(
  # git+https://gitlab.archlinux.org/pacman/pacman.git#tag=$_ref
  # git+https://gitlab.archlinux.org/pacman/pacman.git#branch=$_ref
  # git+https://gitlab.archlinux.org/pacman/pacman.git#commit=$_ref
  git+https://gitlab.archlinux.org/pacman/pacman.git
)
sha256sums=('SKIP')

# Compare the output of `gcc -march=native -Q --help=target` in the host system and the build system
# Order of options matters
# MAKEPKG_CFLAGS="-march=haswell -O2 -pipe -fno-plt -fexceptions -fcf-protection -fstack-clash-protection -fno-omit-frame-pointer -mno-omit-leaf-frame-pointer -mabm -maes -mrtm"
MAKEPKG_CFLAGS="-march=native -O2 -pipe -fno-plt -fexceptions -fcf-protection -fstack-clash-protection -fno-omit-frame-pointer -mno-omit-leaf-frame-pointer"
# MAKEPKG_LDFLAGS="-Wl,-O1 -Wl,--sort-common -Wl,--as-needed -Wl,-z,relro -Wl,-z,now -Wl,-z,pack-relative-relocs"
MAKEPKG_LDFLAGS="-Wl,-O1 -Wl,--sort-common -Wl,--as-needed -Wl,-z,relro -Wl,-z,now"

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  sed -i \
      -e '/^CHOST=/a\\n\PREFIX="opt"' \
      -e '/^#NPROC=/c\NPROC="$(nproc)"' \
      -e '/^#CPPFLAGS=/c\CPPFLAGS=""' \
      -e "/^#CFLAGS=/c\CFLAGS=\"$MAKEPKG_CFLAGS\"" \
      -e '/^#CXXFLAGS=/c\CXXFLAGS="$CFLAGS"' \
      -e "/^#LDFLAGS=/c\LDFLAGS=\"$MAKEPKG_LDFLAGS -Wl,-rpath,/$PREFIX/lib\"" \
      -e '/^#LTOFLAGS=/c\LTOFLAGS="-flto -ffat-lto-objects"' \
      -e '/^#MAKEFLAGS=/c\MAKEFLAGS="-j $(nproc) -O"' \
      -e '/^#DEBUG_CFLAGS=/c\DEBUG_CFLAGS="-g"' \
      -e '/^#DEBUG_CXXFLAGS=/c\DEBUG_CXXFLAGS="$DEBUG_CFLAGS"' \
      -e '/^#BUILDDIR=/c\BUILDDIR="$HOME/makepkg/build"' \
      -e '/^INTEGRITY_CHECK=/s/(.*)/(sha256)/' \
      -e '/^#PKGDEST=/c\PKGDEST="$HOME/makepkg/packages"' \
      -e '/^#SRCDEST=/c\SRCDEST="$HOME/makepkg/sources"' \
      -e '/^#SRCPKGDEST=/c\SRCPKGDEST="$HOME/makepkg/srcpackages"' \
      -e '/^#LOGDEST=/c\LOGDEST="$HOME/makepkg/makepkglogs"' \
      -e '/^#PACKAGER=/c\PACKAGER="Zelun Kong <zelun.kong@outlook.com>"' \
      -e '/^OPTIONS=/s/libtool/!libtool/' \
      -e '/^OPTIONS=/s/!lto/lto/' \
      etc/makepkg.conf.in

  sed -i \
      -e '/^#RUSTFLAGS=/s/#//' \
      -e '/^#DEBUG_RUSTFLAGS=/s/#//' \
      etc/makepkg.conf.d/rust.conf.in

  cat >> etc/pacman.conf.in << EOF

[ubuntu]
SigLevel = Optional TrustAll
Server = file:///home/.repository/\$repo
EOF
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  meson setup \
    --prefix "/$PREFIX" \
    --libdir lib \
    --sysconfdir etc \
    --auto-features disabled \
    --buildtype plain \
    -D buildstatic=false \
    -D crypto=openssl \
    -D curl=enabled \
    -D doc=disabled \
    -D doxygen=disabled \
    -D file-seccomp=enabled \
    -D gpgme=enabled \
    -D ldconfig=/usr/bin/ldconfig \
    -D pkg-ext='.pkg.tar.zst' \
    -D scriptlet-shell=/usr/bin/bash \
    -D src-ext='.src.tar.zst' \
    -D use-git-version=false \
    build

  msg2 "Compiling..."
  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build
}

package() {
  cd $pkgname

  meson install -C build --destdir "$pkgdir"

  rm -vr "$pkgdir"/usr
}
