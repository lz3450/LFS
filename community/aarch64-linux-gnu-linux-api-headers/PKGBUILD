# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# from scratch
# linux-api-header -> binutils -> gcc (stage 1) -> glibc -> gcc (stage2)

# update
# linux-api-header -> binutils -> glibc -> gcc (stage2)

_target_arch=arm64
_target=aarch64-linux-gnu
pkgname=$_target-linux-api-headers
pkgver=5.4.42
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace ($_target)"
arch=('any')
url='https://www.kernel.org'
makedepends=(rsync)
source=(https://www.kernel.org/pub/linux/kernel/v${pkgver:0:1}.x/linux-$pkgver.tar.{xz,sign})
sha256sums=('4e431b7c0266a07b76fdb77f8917ad681f0fa34ffba0eb8a172b67f7ef57bc31'
            'SKIP')
validpgpkeys=('647F28654894E3BD457199BE38DBBDC86092693E')


build() {
  cd linux-$pkgver

  make ARCH=$_target_arch mrproper
  make ARCH=$_target_arch headers_check
}

package() {
  cd linux-$pkgver

  make INSTALL_HDR_PATH="$pkgdir/usr/$_target/" ARCH=$_target_arch headers_install

  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -or -name ..install.cmd \) -delete
}
