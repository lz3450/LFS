# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rust
pkgver=1.50.0
pkgrel=1
epoch=2
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
depends=(curl llvm)
makedepends=(libffi)
optdepends=("lldb: rust-lldb script"
            "gdb: rust-gdb script")
source=(https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz{,.asc})
sha256sums=('95978f8d02bb6175ae3238930baf03563c240aedf9a70bebdc3eaa2a8c3c5a5e'
            'SKIP')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE')

prepare() {
  cd rustc-$pkgver-src

  cp config.toml.example config.toml

  sed -e '/vendor = /s/^#//' \
      -e '/vendor = /s/false/true/' \
      -e '/extended = false$/s/^#//' \
      -e '/extended = /s/false/true/' \
      -e '/sanitizers = /s/^#//' \
      -e '/sanitizers = /s/false/true/' \
      -e '/profiler = /s/^#//' \
      -e '/profiler = /s/false/true/' \
      -e '/tools = /s/^#//' \
      -e '/prefix = /s/^#//' \
      -e '/prefix = /s|/local||' \
      -e '/channel = \"stable\"$/s/^#//' \
      -e '/channel = /s/dev/stable/' \
      -e '/rpath = /s/^#//' \
      -e '/rpath = /s/true/false/' \
      -e '/llvm-config = /s/^#//' \
      -e '/llvm-config = /s|../path/to/llvm/root/bin/llvm-config|/usr/bin/llvm-config|' \
      -i config.toml

  sed -e 's/libexec/lib/' \
      -i src/bootstrap/dist.rs \
      -i src/tools/cargo/src/cargo/ops/registry/auth.rs
}

build() {
  cd rustc-$pkgver-src

  ./x.py -j "$(nproc)" build
}

check() {
  cd rustc-$pkgver-src

  ./x.py test || warning "Check failed."
}

package() {
  cd rustc-$pkgver-src

  DESTDIR="$pkgdir/" ./x.py -j "$(nproc)" install

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft "$pkgdir"/usr/lib "$pkgdir"/usr/lib/rustlib/x86_64-unknown-linux-gnu/lib/*.so

  install -dm755 "$pkgdir"/usr/share/bash-completion/
  mv "$pkgdir"/etc/bash_completion.d/* "$pkgdir"/usr/share/bash-completion/completions
  rm -rf "$pkgdir"/etc

  rm "$pkgdir"/usr/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}
}
