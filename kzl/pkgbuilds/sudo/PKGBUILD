# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=sudo
pkgver=1.9.5p2
pkgrel=1
epoch=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
url="https://www.sudo.ws/sudo"
depends=(libgcrypt libldap pam)
backup=(etc/sudoers
        etc/pam.d/sudo)
source=($url/dist/$pkgname-$pkgver.tar.gz{,.sig}
        sudo.pam)
sha256sums=('539e2ef43c8a55026697fb0474ab6a925a11206b5aa58710cb42a0e1c81f0978'
            'SKIP'
            'd1738818070684a5d2c9b26224906aad69a4fea77aabd960fc2675aee2df1fa2')
validpgpkeys=('59D1E9CCBA2B376704FDD35BA9F4C021CEA470FB')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-rundir=/run/sudo \
    --with-vardir=/var/db/sudo \
    --with-logfac=auth \
    --enable-gcrypt \
    --enable-tmpfiles.d \
    --with-pam \
    --with-sssd \
    --with-ldap \
    --with-ldap-conf-file=/etc/openldap/ldap.conf \
    --with-env-editor \
    --with-passprompt="[sudo] password for %p: " \
    --with-all-insults

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  # remove sudoers.dist; not needed since pacman manages updates to sudoers
  rm "$pkgdir"/etc/sudoers.dist

  # remove /run/sudo directory; create it using systemd-tmpfiles
  rmdir "$pkgdir"/run/sudo
  rmdir "$pkgdir"/run

  install -Dm644 "$srcdir"/sudo.pam "$pkgdir"/etc/pam.d/sudo
}
