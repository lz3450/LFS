# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxml2
pkgver=2.9.10+155+g7c06d99e
pkgrel=1
epoch=1
pkgdesc="XML parsing library, version 2"
arch=('x86_64')
url="http://xmlsoft.org"
depends=(icu)
source=(git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=7c06d99e1f4f853e3c5b307c0dc79c8a32a09855)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g' | sed 's/^v//'
}

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --with-history \
    --with-icu \
    --with-python=/usr/bin/python3 \
    --with-threads

  PYTHONHASHSEED=0 make
  find doc -type f -exec chmod 0644 {} +
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
