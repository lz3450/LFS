# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=llvm
pkgver=11.0.0
pkgrel=1
epoch=1
pkgdesc="Collection of modular and reusable compiler and toolchain technologies"
arch=('x86_64')
url="https://llvm.org/"
depends=(libffi)
provides=(llvm-libs)
conflicts=(llvm-libs)
replaces=(llvm-libs)
options=(staticlibs)
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-11.0.0/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('913f68c898dfb4a03b397c5e11c6a2f39d0f22ed7665c9cefa87a34423a72469'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

build() {
  cd $pkgname-$pkgver.src

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS" \
    -DLLVM_BUILD_TESTS=ON \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_ENABLE_FFI=ON \
    -DLLVM_BUILD_LLVM_DYLIB=ON \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DLLVM_INSTALL_UTILS=ON \
    -S . \
    -B build

  cmake --build build
}

check() {
  cd $pkgname-$pkgver.src

  cmake --build build --target tests || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver.src

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
