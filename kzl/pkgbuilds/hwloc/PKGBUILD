# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=hwloc
pkgver=2.2.0
pkgrel=1
epoch=1
pkgdesc="(Portable Hardware Locality) a portable abstraction of hierarchical architectures"
arch=('x86_64')
url="https://www.open-mpi.org/projects/hwloc/"
makedepends=(cairo libxml2 pciutils)
optdepends=(cairo libxml2 pciutils)
source=(https://www.open-mpi.org/software/hwloc/v${pkgver%.*}/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('ae70b893df272b84afd7068d351aae5c8c4fd79d40ca783b3e67554b873a2252')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-plugins

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
