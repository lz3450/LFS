# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> binutils -> glibc

pkgname=binutils
pkgver=2.36.1
pkgrel=1
epoch=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="https://www.gnu.org/software/binutils/"
options=(staticlibs)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e81d9edf373f193af428a0f256674aea62a9d74dfe93f65192d4eae030b0f3b0'
            'SKIP')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

prepare() {
  cd $pkgname-$pkgver

  mkdir -p build

  sed -e '/^development=/s/true/false/' -i bfd/development.sh
  sed -e "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" -i libiberty/configure
}

build() {
  cd $pkgname-$pkgver/build

  ../configure \
    --prefix=/usr \
    --enable-gold \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-targets=x86_64-pep \
    --enable-threads \
    --enable-deterministic-archives \
    --enable-cet \
    --enable-ld=default \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-system-zlib \
    --with-pic \
    --with-debuginfod \

  make configure-host
  make tooldir="/usr/"
}

check() {
  cd $pkgname-$pkgver/build

  make -k LDFLAGS="" check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver/build

  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir"/usr/lib/libbfd.so
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir"/usr/lib/libopcodes.so
}
