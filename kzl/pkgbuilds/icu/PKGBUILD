# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=icu
pkgver=67.1
pkgrel=1
epoch=1
pkgdesc="International Components for Unicode library"
arch=('x86_64')
url="http://site.icu-project.org/home"
source=(https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/${pkgname}4c-${pkgver//./_}-src.tgz{,.asc})
sha256sums=('94a80cd6f251a53bd2a997f6f1b5ac6653fe791dfab66e1eb0227740fb86d5dc'
            'SKIP')
validpgpkeys=('0E51E7F06EF719FBD072782A5F56E5AFA63CCD33')

build() {
  cd $pkgname/source

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --mandir=/usr/share/man

  make
}

check() {
  cd $pkgname/source

  make -k check
}

package() {
  cd $pkgname/source

  make DESTDIR="$pkgdir/" install
}
