# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=audit
pkgname=(audit python-audit)
pkgver=3.0
pkgrel=1
epoch=1
pkgdesc="Userspace components of the audit framework"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/audit"
makedepends=(krb5 libcap-ng swig)
source=($url/$pkgbase-$pkgver.tar.gz)
sha256sums=('bd31826823b912b6fe271d2d979ed879e9fc393cab1e2f7c4e1af258231765b8')

prepare() {
  cd $pkgbase-$pkgver

  sed 's|/var/run/auditd.pid|/run/auditd.pid|' -i init.d/auditd.service
}

build() {
  cd $pkgbase-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/audit \
    --sysconfdir=/etc \
    --enable-gssapi-krb5 \
    --enable-systemd \
    --with-libcap-ng

  make
}

package_audit() {
  depends=(krb5 libcap-ng)
  backup=(etc/libaudit.conf
          etc/audit/audit-stop.rules
          etc/audit/auditd.conf
          etc/audisp/audispd.conf
          etc/audisp/audisp-remote.conf
          etc/audisp/zos-remote.conf
          etc/audisp/plugins.d/af_unix.conf
          etc/audisp/plugins.d/audispd-zos-remote.conf
          etc/audisp/plugins.d/au-remote.conf
          etc/audisp/plugins.d/syslog.conf)

  cd $pkgbase-$pkgver

  make DESTDIR="$pkgdir/" INSTALL="install -p" install

  install -dm0700 "$pkgdir"/var/log/audit

  rm -rf \
    "$pkgdir"/etc/rc.d \
    "$pkgdir"/etc/sysconfig \
    "$pkgdir"/usr/lib/audit \
    "$pkgdir"/usr/lib/python*

  sed -ri 's|/sbin|/usr/bin|' \
    "$pkgdir"/etc/audit/*.conf \
    "$pkgdir"/etc/audit/plugins.d/*.conf \
    "$pkgdir"/usr/lib/systemd/system/auditd.service

  chmod 644 "$pkgdir"/usr/lib/systemd/system/auditd.service
}

package_python-audit() {
  depends=(audit)
  pkgdesc+=" (python bindings)"

  cd $pkgbase-$pkgver/bindings

  make DESTDIR="$pkgdir/" INSTALL="install -p" install
}
