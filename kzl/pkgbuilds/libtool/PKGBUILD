# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libtool
pkgver=2.4.6+42+gb88cebd5
pkgrel=1
epoch=1
pkgdesc="A generic library support script"
arch=('x86_64')
url="https://www.gnu.org/software/libtool"
provides=(libltdl libtool-multilib)
makedepends=(help2man)
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=b88cebd510add4420dd8f5367e3cc6e6e1f267cd
        git+https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git)
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g;s/^v//'
  # git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.gnulib.url "$srcdir/gnulib"
  git config --local submodule.gl-mod/bootstrap.url "$srcdir/gnulib-bootstrap"
  git submodule update

  ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check || warning "Check failed."
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
