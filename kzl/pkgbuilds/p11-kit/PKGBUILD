# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p11-kit
pkgver=0.23.22
pkgrel=1
epoch=1
pkgdesc="Loads and enumerates PKCS#11 modules"
arch=('x86_64')
url="https://p11-glue.freedesktop.org"
depends=(libffi libtasn1)
makedepends=(bash-completion)
source=(git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver)
sha256sums=('SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs || warning "Check failed."
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
