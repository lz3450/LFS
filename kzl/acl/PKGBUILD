# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=acl
pkgver=2.2.53
pkgrel=1
epoch=1
pkgdesc="Access control list utilities, libraries and headers"
arch=('x86_64')
url="https://savannah.nongnu.org/projects/acl/"
depends=(attr)
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('06be9865c6f418d851ff4494e12406568353b891ffe1f596b34693c387af26c7'
            'SKIP')
validpgpkeys=('B902B5271325F892AC251AD441633B9FE837F581')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
