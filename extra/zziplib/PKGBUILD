# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zziplib
pkgver=0.13.71
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('x86_64')
url="http://zziplib.sourceforge.net"
makedepends=(zip)
checkdepends=(unzip)
source=(git+https://github.com/gdraheim/zziplib.git?signed#commit=c3de4fe291ab6756593a0ef6bad007ddd3953e00)
sha256sums=('SKIP')
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23')

build() {
  cd $pkgname

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DZZIPDOCS=OFF \
    -GNinja \
    -S . \
    -B build

  ninja -C build
}

check() {
  cd $pkgname

  ninja check -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" ninja -C build install
}
