# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakechroot
pkgver=2.20.1
pkgrel=1
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="https://github.com/dex4er/fakechroot/wiki"
source=($pkgname-$pkgver.tar.gz::https://github.com/dex4er/fakechroot/archive/$pkgver.tar.gz)
sha256sums=('7f9d60d0d48611969e195fadf84d05f6c74f71bbf8f41950ad8f5bf061773e18')
  
build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libdir=/usr/lib/libfakeroot \
    --sysconfdir=/etc

  make
}

check() {
  cd $pkgname-$pkgver

  # t/fts.t fails when building on BTRFS
  make -k check || :
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > "$pkgdir"/etc/ld.so.conf.d/fakechroot.conf
}
