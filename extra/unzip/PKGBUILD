# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=unzip
pkgver=6.0
pkgrel=1
pkgdesc="For extracting and viewing files in .zip archives"
arch=(x86_64)
url="https://www.info-zip.org/UnZip.html"
source=(https://downloads.sourceforge.net/infozip/$pkgname${pkgver/./}.tar.gz
        overflow-fsize.patch
        cve20149636.patch
        test_compr_eb.patch
        getZip64Data.patch
        crc32.patch
        empty-input.patch
        csiz-underflow.patch
        nextbyte-overflow.patch)
sha256sums=('036d96991646d0449ed0aa952e4fbe21b476ce994abc276e49d30e686708bd37'
            'e2a10fa494c39fb3f311d0f2d7db775bdecbc8d5b9d298c7bd035ace1f1713d5'
            'c9a863e570bdaf2637c43bf1bba3d97808a1b0504d85418f6a8550ac286788f2'
            '1a1390390402e674ef7b143705ade0e9aa082131bb9686e95fb7985310def885'
            'f598f2c1c840c845f9e9d08fc14d358b87c002cfaff021c4b4a7086ecbe337c8'
            '5598c4124ec5865745bad5bf80c48f92cafd55363d5e140e5abca801815b0ce6'
            '9bdea454a3677e00fc81b8ffe534040b2a250ae0d76437d38e41f849997668ae'
            '95dd15d5d9cdf5cea18c357b152930d6d52660599e0fd4907d6405870fdd9fe1'
            '788c29727ff0689c3b1828466127758426f6d2c769048aa985950373747c76f3')

prepare() {
  cd $pkgname${pkgver/./}

	sed -i '/MANDIR =/s#)/#)/share/#' unix/Makefile
	patch -p1 -i ../overflow-fsize.patch #FS#44171
	patch -p1 -i ../cve20149636.patch #FS#44171
	patch -i ../test_compr_eb.patch # FS#43391
	patch -i ../getZip64Data.patch # FS#43300
	patch -i ../crc32.patch # FS#43300
	patch -p1 -i ../empty-input.patch # FS#46955
	patch -p1 -i ../csiz-underflow.patch # FS#46955
	patch -p1 -i ../nextbyte-overflow.patch # FS#46955
}

build() {
  cd $pkgname${pkgver/./}

	# DEFINES, make, and install args from Debian
	DEFINES='-DACORN_FTYPE_NFS \
    -DWILD_STOP_AT_DIR \
    -DLARGE_FILE_SUPPORT \
		-DUNICODE_SUPPORT \
    -DUNICODE_WCHAR \
    -DUTF8_MAYBE_NATIVE \
    -DNO_LCHMOD \
		-DDATE_FORMAT=DF_YMD \
    -DUSE_BZIP2 \
    -DNOMEMCPY \
    -DNO_WORKING_ISPRINT'

	make -f unix/Makefile \
    prefix=/usr \
		D_USE_BZ2=-DUSE_BZIP2 \
    L_BZ2=-lbz2 \
		LF2="$LDFLAGS" \
    CF="$CFLAGS $CPPFLAGS -I. $DEFINES" \
		unzips
}

package() {
  cd $pkgname${pkgver/./}

	make -f unix/Makefile prefix="$pkgdir/usr" install
}
