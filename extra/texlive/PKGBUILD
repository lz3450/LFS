pkgname=texlive
pkgver=54456
pkgrel=1
pkgdesc="TeX Live"
arch=(x86_64)
url='http://tug.org/texlive/'
depends=(libsigsegv ffcall cairo pixman graphite gd poppler libpng libjpeg
         freetype2 icu harfbuzz harfbuzz-icu gmp mpfr libpaper zziplib)
install=texlive.install
source=(git+https://github.com/Tex-Live/texlive-source.git#tag=svn$pkgver
        ftp://tug.org/$pkgname/historic/2020/$pkgname-20200406-texmf.tar.xz
        09-texlive-fonts.conf
        texlive.install
        texmf.cnf
        texmfcnf.lua)
noextract=($pkgname-20190410-texmf.tar.xz)
sha256sums=('SKIP'
            '0aa97e583ecfd488e1dc60ff049fec073c1e22dfe7de30a3e4e8c851bb875a95'
            '5e79c40cf3ab93348fc89e97890198601767ea2c8fea89ea76088c17a2b35962'
            '27fba25012f840a11f3021c4c71c2ccc80f45e4d16d46dd26fc570c80f3c5db4'
            '585678708ae9f1e883668fe2d1686e11d35aea9854fae60eb20bb6f02f0490ef'
            '0b6c3ee516608ce04d7133db52cadfa1be5d885b3f82bb39dc5897b213847e0d')

prepare() {
  cd $pkgname-source

  mkdir build
}

build() {
  cd $pkgname-source/build

  ../configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --datarootdir=/usr/share \
    --datadir=/usr/share \
    --mandir=/usr/share/man \
    --enable-shared \
    --enable-luatex \
    --disable-static \
    --disable-multiplatform \
    --disable-native-texlive-build \
    --disable-dump-share \
    --disable-psutils \
    --with-banner-add="/KZL" \
    --with-system-cairo \
    --with-system-freetype2 \
    --with-freetype2-libdir=/usr/lib \
    --with-freetype2-include=/usr/include/freetype2 \
    --with-system-gd \
    --with-system-gmp \
    --with-system-graphite2 \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-libpaper \
    --with-system-libpng \
    --with-system-mpfr \
    --with-system-pixman \
    --with-system-poppler \
    --with-system-zlib \
    --with-system-zziplib

  make
}

check() {
  cd $pkgname-source/build

  make check || true
}

package() {
  cd $pkgname-source/build

  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" texlinks

  tar -xvf "$srcdir"/$pkgname-20190410-texmf.tar.xz -C "$pkgdir"/usr/share/ --skip-old-files --strip-components=1

  # echo "--> installing the /etc/texmf tree"
  install -dm755 "$pkgdir"/etc/texmf/chktex/
  install -dm755 "$pkgdir"/etc/texmf/dvipdfmx/
  install -dm755 "$pkgdir"/etc/texmf/dvips/config/
  install -dm755 "$pkgdir"/etc/texmf/tex/generic/config/
  install -dm755 "$pkgdir"/etc/texmf/tex/generic/tex-ini-files/
  install -dm755 "$pkgdir"/etc/texmf/ttf2pk/
  install -dm755 "$pkgdir"/etc/texmf/web2c/
  install -dm755 "$pkgdir"/etc/texmf/xdvi/

  # texlive fonts
  install -dm755 "$pkgdir"/etc/fonts/conf.avail/
  install -Dm644 "$srcdir"/09-texlive-fonts.conf "$pkgdir"/etc/fonts/conf.avail/

  # copy config files to $TEXMFSYSCONFIG tree
  cp -a "$pkgdir"/usr/share/texmf-dist/chktex/chktexrc "$pkgdir"/etc/texmf/chktex/
  cp -a "$pkgdir"/usr/share/texmf-dist/dvipdfmx/dvipdfmx.cfg "$pkgdir"/etc/texmf/dvipdfmx/
  cp -a "$pkgdir"/usr/share/texmf-dist/dvips/config/config.ps "$pkgdir"/etc/texmf/dvips/config/
  cp -a "$pkgdir"/usr/share/texmf-dist/tex/generic/config/language.def "$pkgdir"/etc/texmf/tex/generic/config/
  cp -a "$pkgdir"/usr/share/texmf-dist/tex/generic/config/language.dat "$pkgdir"/etc/texmf/tex/generic/config/
  cp -a "$pkgdir"/usr/share/texmf-dist/tex/generic/tex-ini-files/pdftexconfig.tex "$pkgdir"/etc/texmf/tex/generic/tex-ini-files/
  cp -a "$pkgdir"/usr/share/texmf-dist/ttf2pk/ttf2pk.cfg "$pkgdir"/etc/texmf/ttf2pk/
  cp -a "$pkgdir"/usr/share/texmf-dist/web2c/fmtutil-hdr.cnf "$pkgdir"/etc/texmf/web2c/fmtutil.cnf
  cp -a "$pkgdir"/usr/share/texmf-dist/web2c/mktex.cnf "$pkgdir"/etc/texmf/web2c/
  cp -a "$pkgdir"/usr/share/texmf-dist/web2c/updmap-hdr.cfg "$pkgdir"/etc/texmf/web2c/
  cp -a "$pkgdir"/usr/share/texmf-dist/xdvi/XDvi "$pkgdir"/etc/texmf/xdvi/

  # replace upstream texmf.cnf
  install -Dm644 "$srcdir"/texmf.cnf "$pkgdir"/etc/texmf/web2c/texmf.cnf
  install -Dm644 "$srcdir"/texmf.cnf "$pkgdir"/usr/share/texmf-dist/web2c/texmf.cnf
  # replace upstream texmfcnf.lua
  install -Dm644 "$srcdir"/texmfcnf.lua "$pkgdir"/usr/share/texmf-dist/web2c/texmfcnf.lua

  install -dm755 "$pkgdir"/usr/share/tlpkg/TeXLive/
  install -Dm644 ../texk/tests/TeXLive/* "$pkgdir"/usr/share/tlpkg/TeXLive/

  # use python2 instead of python
  sed -i "1s/python/python2/" "$pkgdir"/usr/share/texmf-dist/scripts/de-macro/de-macro

  # more cleanup
  rm -rf "$pkgdir"/usr/share/texmf-dist/scripts/context/stubs/mswin/
}
