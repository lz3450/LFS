# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=sqlite
pkgver=3310100
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="https://www.sqlite.org"
makedepends=(tcl)
source=($url/2020/$pkgname-src-$pkgver.zip)
sha256sums=('f2dc2382855d99a960c363c1e5ae72b49da4c55d49154aa6d100e5970a1fee58')

build() {
  cd $pkgname-src-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-fts5 \
    --enable-rtree \
    --enable-json1 \
    --disable-static \
    --disable-amalgamation \
    CFLAGS="$CFLAGS \
      -DSQLITE_ENABLE_COLUMN_METADATA=1 \
      -DSQLITE_ENABLE_UNLOCK_NOTIFY \
      -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
      -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
      -DSQLITE_SECURE_DELETE \
      -DSQLITE_MAX_VARIABLE_NUMBER=250000 \
      -DSQLITE_MAX_EXPR_DEPTH=10000"

  make
  make showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

package() {
  cd $pkgname-src-$pkgver

  make DESTDIR="$pkgdir/" install
  install -Dm755 showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer "$pkgdir"/usr/bin/
}
