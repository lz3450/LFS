# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=mpfr
pkgver=4.0.2
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('x86_64')
url="https://www.mpfr.org/"
depends=(gmp)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
            'SKIP')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-thread-safe \
    --enable-shared

  make
}

check() {
  cd $pkgname-$pkgver
  
  make check
  make check-exported-symbols
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
