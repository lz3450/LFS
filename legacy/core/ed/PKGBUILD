# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ed
pkgver=1.16
pkgrel=1
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('x86_64')
url="https://www.gnu.org/software/ed/ed.html"
options=(!emptydirs)
source=(https://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.lz{,.sig})
sha256sums=('cfc07a14ab048a758473ce222e784fbf031485bcd54a76f74acfee1f390d8b2c'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    "CPPFLAGS=$CPPFLAGS" \
    "CFLAGS=$CFLAGS" \
    "LDFLAGS=$LDFLAGS"

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
