# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libgcrypt
pkgver=1.8.5
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=('x86_64')
url="https://www.gnupg.org"
depends=(libgpg-error)
source=(https://gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('3b4a2a94cb637eff5bdebbcaf46f4d95c4f25206f459809339cdada0eb577ac3'
            'SKIP')
validpgpkeys=('031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6')

prepare() {
  cd $pkgname-$pkgver

  sed -e "s:t-secmem::" -i tests/Makefile.am
  sed -e "s:t-sexp::" -i tests/Makefile.am

  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-padlock-support

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
