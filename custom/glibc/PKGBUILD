# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> binutils -> glibc

pkgname=glibc
pkgver=2.32
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.gnu.org/software/libc"
depends=(tzdata)
options=(!strip)
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig}
        ld.so.conf
        resolv.conf
        locale-gen::https://git.archlinux.org/svntogit/packages.git/plain/trunk/locale-gen?h=packages/glibc
        locale.gen.txt)
backup=(etc/gai.conf
        etc/ld.so.conf
        etc/locale.gen
        etc/nscd.conf
        etc/resolv.conf)
install=glibc.install
sha256sums=('1627ea54f5a1a8467032563393e0901077626dc66f37f10ee6363bb722222836'
            'SKIP'
            'dad04a370e488aa85fb0a813a5c83cf6fd981ce01883fc59685447b092de84b5'
            '5557d8e601b17a80d1ea7de78a9869be69637cb6a02fbfe334e22fdf64e61d4c'
            '83f108f915863c7ed0338e2d3e8f2e071a531a090ef8f8b2eb3a956a3c4f04d7'
            '0540122fb9340d546702a891bcce344fc3e8f21fe482c2c321b71c2e6b36964e')
validpgpkeys=('BC7C7372637EC10C57D7AA6579C43DFBF1CF2187')

prepare() {
  cd $pkgname-$pkgver
  
  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}
  LDFLAGS=${LDFLAGS/,-z,now/}

  ../configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --with-headers=/usr/include \
    --enable-kernel=5.4.42 \
    --enable-static-pie \
    --enable-cet \
    --disable-profile \
    --disable-timezone-tools \
    --enable-stack-protector=strong \
    --enable-bind-now \
    --with-pkgversion="KZL $pkgver-$pkgrel"
  
  make
}

check() {
  cd $pkgname-$pkgver/build

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver/build

  make install_root="$pkgdir/" install

  rm -f "$pkgdir"/etc/ld.so.{cache,conf}
  install -dm755 "$pkgdir"/etc/ld.so.conf.d/
  install -dm755 "$pkgdir"/usr/share/factory/etc/
  install -Dm644 "$srcdir"/ld.so.conf "$pkgdir"/etc/
  install -Dm644 "$srcdir"/ld.so.conf "$pkgdir"/usr/share/factory/etc/
  install -Dm644 "$srcdir"/resolv.conf "$pkgdir"/etc/
  install -Dm644 "$srcdir"/resolv.conf "$pkgdir"/usr/share/factory/etc/

  install -dm755 "$pkgdir"/usr/lib/{locale,systemd/system,tmpfiles.d}/
  install -dm755 "$pkgdir"/var/db/nscd/
  install -Dm644 ../nscd/nscd.conf "$pkgdir"/etc/
  install -Dm644 ../nscd/nscd.service "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 ../nscd/nscd.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/

  install -Dm644 ../posix/gai.conf "$pkgdir"/etc/gai.conf

  install -Dm755 "$srcdir"/locale-gen "$pkgdir"/usr/bin/

  # Create /etc/locale.gen
  install -Dm644 "$srcdir"/locale.gen.txt "$pkgdir"/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' ../localedata/SUPPORTED >> "$pkgdir"/etc/locale.gen

  # Provided by libxcrypt
  rm -f "$pkgdir"/usr/include/crypt.h "$pkgdir"/usr/lib/libcrypt.{a,so}
}
