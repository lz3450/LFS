# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nvidia
pkgver=455.28
pkgrel=1
epoch=1
arch=('x86_64')
url="http://www.nvidia.com/"
depends=(linux)
optdepends=("opencl-headers: headers necessary for OpenCL development")
provides=(opencl-driver)
options=(!strip)
source=(nvidia.sysusers
        https://us.download.nvidia.com/XFree86/Linux-x86_64/$pkgver/NVIDIA-Linux-x86_64-$pkgver.run)
sha256sums=('b9c775b87829698cee92cd8b0004bd3d34b8b5cb36cef4d96199539fa72f8d15'
            'e4e1499b15aedc7c0b5c0d30c170858ed1625ab2701d3bb11747ac8c8371da0f')

prepare() {
  sh NVIDIA-Linux-x86_64-$pkgver.run --extract-only
  cd NVIDIA-Linux-x86_64-$pkgver
  bsdtar -xf nvidia-persistenced-init.tar.bz2
}

build() {
  cd NVIDIA-Linux-x86_64-$pkgver/kernel

  make
}

package() {
  cd NVIDIA-Linux-x86_64-$pkgver

  # driver
  install -Dm644 kernel/nvidia.ko "$pkgdir"/usr/lib/modules/$(uname -r)/kernel/drivers/video/nvidia.ko
  install -Dm644 kernel/nvidia-drm.ko "$pkgdir"/usr/lib/modules/$(uname -r)/kernel/drivers/video/nvidia-drm.ko
  install -Dm644 kernel/nvidia-modeset.ko "$pkgdir"/usr/lib/modules/$(uname -r)/kernel/drivers/video/nvidia-modeset.ko
  install -Dm644 kernel/nvidia-uvm.ko "$pkgdir"/usr/lib/modules/$(uname -r)/kernel/drivers/video/nvidia-uvm.ko

  echo -e "blacklist nouveau\noptions nouveau modeset=0" | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/modprobe.d/$pkgbase.conf

  # OpenCL
  install -Dm644 nvidia.icd "$pkgdir"/etc/OpenCL/vendors/nvidia.icd
  install -Dm755 libnvidia-compiler.so.$pkgver "$pkgdir"/usr/lib/libnvidia-compiler.so.$pkgver
  install -Dm755 libnvidia-opencl.so.$pkgver "$pkgdir"/usr/lib/libnvidia-opencl.so.$pkgver

  # X driver
  install -Dm755 nvidia_drv.so "$pkgdir"/usr/lib/xorg/modules/drivers/nvidia_drv.so

  # GLX extension module for X
  install -Dm755 libglxserver_nvidia.so.$pkgver "$pkgdir"/usr/lib/nvidia/xorg/libglxserver_nvidia.so.$pkgver
  ln -s libglxserver_nvidia.so.$pkgver "$pkgdir"/usr/lib/nvidia/xorg/libglxserver_nvidia.so

  install -Dm755 libGLX_nvidia.so.$pkgver "$pkgdir"/usr/lib/libGLX_nvidia.so.$pkgver

  # OpenGL
  install -Dm755 libEGL_nvidia.so.$pkgver "$pkgdir"/usr/lib/libEGL_nvidia.so.$pkgver
  install -Dm755 libGLESv1_CM_nvidia.so.$pkgver "$pkgdir"/usr/lib/libGLESv1_CM_nvidia.so.$pkgver
  install -Dm755 libGLESv2_nvidia.so.$pkgver "$pkgdir"/usr/lib/libGLESv2_nvidia.so.$pkgver
  install -Dm644 10_nvidia.json "$pkgdir"/usr/share/glvnd/egl_vendor.d/10_nvidia.json

  install -Dm755 libnvidia-glcore.so.$pkgver "$pkgdir"/usr/lib/libnvidia-glcore.so.$pkgver
  install -Dm755 libnvidia-eglcore.so.$pkgver "$pkgdir"/usr/lib/libnvidia-eglcore.so.$pkgver
  install -Dm755 libnvidia-glsi.so.$pkgver "$pkgdir"/usr/lib/libnvidia-glsi.so.$pkgver

  # Vulkan ICD
  install -Dm644 nvidia_icd.json "$pkgdir/usr/share/vulkan/icd.d/nvidia_icd.json"
  install -Dm644 nvidia_layers.json "$pkgdir/usr/share/vulkan/implicit_layer.d/nvidia_layers.json"

  # VDPAU
  install -Dm755 libvdpau_nvidia.so.$pkgver "$pkgdir"/usr/lib/vdpau/libvdpau_nvidia.so.$pkgver

  # nvidia-tls library
  install -Dm755 libnvidia-tls.so.$pkgver $pkgdir/usr/lib/libnvidia-tls.so.$pkgver

  # CUDA
  install -Dm755 libcuda.so.$pkgver $pkgdir/usr/lib/libcuda.so.$pkgver
  install -Dm755 libnvcuvid.so.$pkgver $pkgdir/usr/lib/libnvcuvid.so.$pkgver

  # PTX JIT Compiler (Parallel Thread Execution (PTX), a pseudo-assembly language for CUDA)
  install -Dm755 libnvidia-ptxjitcompiler.so.$pkgver "$pkgdir"/usr/lib/libnvidia-ptxjitcompiler.so.$pkgver

  # Raytracing
  install -Dm755 libnvoptix.so.$pkgver "$pkgdir"/usr/lib/libnvoptix.so.$pkgver
  install -Dm755 libnvidia-rtcore.so.$pkgver "$pkgdir"/usr/lib/libnvidia-rtcore.so.$pkgver
  install -Dm755 libnvidia-cbl.so.$pkgver "$pkgdir"/usr/lib/libnvidia-cbl.so.$pkgver

  # Optical flow
  install -Dm755 libnvidia-opticalflow.so.$pkgver "$pkgdir"/usr/lib/libnvidia-opticalflow.so.$pkgver

  # misc
  install -Dm755 libnvidia-cfg.so.$pkgver "$pkgdir"/usr/lib/libnvidia-cfg.so.$pkgver
  install -Dm755 libnvidia-encode.so.$pkgver "$pkgdir"/usr/lib/libnvidia-encode.so.$pkgver
  install -Dm755 libnvidia-ifr.so.$pkgver "$pkgdir"/usr/lib/libnvidia-ifr.so.$pkgver
  install -Dm755 libnvidia-fbc.so.$pkgver "$pkgdir"/usr/lib/libnvidia-fbc.so.$pkgver
  install -Dm755 libnvidia-glvkspirv.so.$pkgver "$pkgdir"/usr/lib/libnvidia-glvkspirv.so.$pkgver
  install -Dm755 libnvidia-ml.so.$pkgver "$pkgdir"/usr/lib/libnvidia-ml.so.$pkgver
  install -Dm755 libnvidia-ngx.so.$pkgver "$pkgdir"/usr/lib/libnvidia-ngx.so.$pkgver

  # nvidia-smi
  install -Dm755 nvidia-smi "$pkgdir"/usr/bin/nvidia-smi
  install -Dm644 nvidia-smi.1.gz "$pkgdir"/usr/share/man/man1/nvidia-smi.1.gz

  # DEBUG
  install -Dm755 nvidia-debugdump "$pkgdir"/usr/bin/nvidia-debugdump

  # nvidia-xconfig
  install -Dm755 nvidia-xconfig "$pkgdir"/usr/bin/nvidia-xconfig
  install -Dm644 nvidia-xconfig.1.gz "$pkgdir"/usr/share/man/man1/nvidia-xconfig.1.gz

  # nvidia-cuda-mps
  install -Dm755 nvidia-cuda-mps-server "$pkgdir"/usr/bin/nvidia-cuda-mps-server
  install -Dm755 nvidia-cuda-mps-control "$pkgdir"/usr/bin/nvidia-cuda-mps-control
  install -Dm644 nvidia-cuda-mps-control.1.gz "$pkgdir"/usr/share/man/man1/nvidia-cuda-mps-control.1.gz

  # nvidia-modprobe
  # This should be removed if nvidia fixed their uvm module!
  install -D -m4755 nvidia-modprobe "$pkgdir"/usr/bin/nvidia-modprobe
  install -Dm644 nvidia-modprobe.1.gz "$pkgdir"/usr/share/man/man1/nvidia-modprobe.1.gz

  # nvidia-persistenced
  install -Dm755 nvidia-persistenced "$pkgdir"/usr/bin/nvidia-persistenced
  install -Dm644 nvidia-persistenced.1.gz "$pkgdir"/usr/share/man/man1/nvidia-persistenced.1.gz
  install -Dm644 nvidia-persistenced-init/systemd/nvidia-persistenced.service.template "$pkgdir"/usr/lib/systemd/system/nvidia-persistenced.service
  sed -i 's/__USER__/nvidia-persistenced/' "$pkgdir"/usr/lib/systemd/system/nvidia-persistenced.service
  install -Dm644 "$srcdir"/nvidia.sysusers "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf

  # application profiles
  install -Dm644 nvidia-application-profiles-$pkgver-rc "$pkgdir"/usr/share/nvidia/nvidia-application-profiles-$pkgver-rc
  install -Dm644 nvidia-application-profiles-$pkgver-key-documentation "$pkgdir"/usr/share/nvidia/nvidia-application-profiles-$pkgver-key-documentation

  # new power management support
  install -Dm644 nvidia-suspend.service "$pkgdir"/usr/lib/systemd/system/nvidia-suspend.service
  install -Dm644 nvidia-hibernate.service "$pkgdir"/usr/lib/systemd/system/nvidia-hibernate.service
  install -Dm644 nvidia-resume.service "$pkgdir"/usr/lib/systemd/system/nvidia-resume.service
  install -Dm755 nvidia "$pkgdir"/usr/lib/systemd/system-sleep/nvidia
  install -Dm755 nvidia-sleep.sh "$pkgdir"/usr/bin/nvidia-sleep.sh

  # nvidia-bug-report
  install -Dm755 nvidia-bug-report.sh "$pkgdir"/usr/bin/nvidia-bug-report.sh
}
