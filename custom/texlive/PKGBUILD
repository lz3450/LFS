pkgname=texlive
pkgver=20200406
pkgrel=1
epoch=1
pkgdesc="TeX Live"
arch=('x86_64')
url='http://tug.org/texlive/'
depends=(cairo ghostscript freetype2 gmp graphite harfbuzz icu gd libjpeg libpaper libpng libsigsegv mpfr pixman poppler zziplib)
provides=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
conflicts=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
replaces=(libsynctex texlive-bibtexextra texlive-bin texlive-core texlive-fontsextra texlive-formatsextra texlive-games texlive-humanities texlive-langchinese texlive-langcyrillic texlive-langextra texlive-langgreek texlive-langjapanese texlive-langkorean texlive-latexextra texlive-music texlive-pictures texlive-pstricks texlive-publishers texlive-science)
options=(!strip)
install=texlive.install
source=(ftp://tug.org/historic/systems/$pkgname/${pkgver:0:4}/$pkgname-$pkgver-source.tar.xz
        ftp://tug.org/historic/systems/$pkgname/${pkgver:0:4}/$pkgname-$pkgver-texmf.tar.xz
        texmf.cnf
        texmfcnf.lua
        09-texlive-fonts.conf
        texlive.install)
noextract=($pkgname-$pkgver-texmf.tar.xz)
sha256sums=('e32f3d08cbbbcf21d8d3f96f2143b64a1f5e4cb01b06b761d6249c8785249078'
            '0aa97e583ecfd488e1dc60ff049fec073c1e22dfe7de30a3e4e8c851bb875a95'
            '67895500f94ff7f9c26ced5ed38ea9bb039a406bb3ec50d658c12d0ec3505bc4'
            'e019b04588775d9cb7fb8fc8881e400d5756e9f1b331f555ae1f856b196bf896'
            'dd4bd23f57bfcf60abb2e0c4902840df3fed6eed82674bbe5d98ff0f9d7b4ee5'
            '4b1cbc2a653e327d0cd293f61a66b86575709a1b58c6b36fe25a18411692fd27')

prepare() {
  cd $pkgname-$pkgver-source

  test -d build || mkdir build
}

build() {
  cd $pkgname-$pkgver-source/build

  ../configure \
    --prefix=/opt/TeXLive \
    --datarootdir=/opt/TeXLive \
    --disable-native-texlive-build \
    --disable-multiplatform \
    --disable-t1utils \
    --disable-dump-share \
    --disable-aleph \
    --enable-luatex \
    --disable-bibtexu \
    --disable-psutils \
    --enable-shared \
    --disable-static \
    --with-clisp-runtime=default \
    --with-banner-add="/KZL" \
    --with-system-harfbuzz \
    --with-system-icu \
    --with-system-graphite2 \
    --with-system-zziplib \
    --with-system-poppler \
    --with-system-mpfr \
    --with-system-gmp \
    --with-system-cairo \
    --with-system-pixman \
    --with-system-gd \
    --with-system-freetype2 \
    --with-freetype2-libdir=/usr/lib \
    --with-freetype2-include=/usr/include/freetype2 \
    --with-system-libpng \
    --with-system-libpaper \
    --with-system-zlib \
    --without-x
    
    # --prefix=/opt/TeXLive/${pkgver:0:4} \
    # --bindir=/opt/TeXLive/${pkgver:0:4}/bin/$(uname -m | sed -e 's/$/-linux/') \
    # --libdir=/opt/TeXLive/${pkgver:0:4}/lib \
    # --includedir=/opt/TeXLive/${pkgver:0:4}/include \
    # --datarootdir=/opt/TeXLive/${pkgver:0:4}/ \
    # --infodir=/opt/TeXLive/${pkgver:0:4}/texmf-dist/doc/info \
    # --mandir=/opt/TeXLive/${pkgver:0:4}/texmf-dist/doc/man \

  make
}

check() {
  cd $pkgname-$pkgver-source/build

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver-source/build

  make DESTDIR="$pkgdir/" install-strip
  make DESTDIR="$pkgdir/" texlinks

  install -Dm644 "$srcdir"/$pkgname-$pkgver-source/texk/tests/TeXLive/* -t "$pkgdir"/opt/TeXLive/texmf-dist/scripts/texlive/TeXLive/

  tar -xf "$srcdir"/$pkgname-$pkgver-texmf.tar.xz -C "$pkgdir"/opt/TeXLive --skip-old-files --strip-components=1

  # replace upstream texmf.cnf and texmfcnf.lua
  install -Dm644 "$srcdir"/texmf{.cnf,cnf.lua} -t "$pkgdir"/opt/TeXLive/texmf-dist/web2c/

  echo "export PATH=\$PATH:/opt/TeXLive/bin" | install -Dm644 /dev/stdin "$pkgdir"/etc/profile.d/texlive.sh

  # texlive fonts
  install -dm755 "$pkgdir"/etc/fonts/conf.avail/
  install -Dm644 "$srcdir"/09-texlive-fonts.conf "$pkgdir"/etc/fonts/conf.avail/

  # cleanup
  rm -rf "$pkgdir"/opt/TeXLive/texmf-dist/scripts/context/stubs/mswin/
  rm -rf "$pkgdir"/opt/TeXLive/bin/tlmgr
}
