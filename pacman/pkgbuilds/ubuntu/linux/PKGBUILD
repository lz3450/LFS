# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=6.18.8
pkgrel=1
epoch=
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
options=(!strip)
source=(
  # git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#branch=linux-${pkgver%.*}.y
  git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#tag=v$pkgver
  config
)
sha256sums=('7ebd69543f23745b6cef0d588b0486805d923c8716419e5fb4267f28a5050a09'
            'dffd761054d2a539983cb98839467a1e45aa87dc33a8aead0d1cb674278d5ed8')

export LOCALVERSION="-KZL"
export KBUILD_BUILD_HOST="Ubuntu"
export KBUILD_BUILD_USER="kzl"
export KBUILD_BUILD_TIMESTAMP="$(date '+%Y/%m/%d %T %z') ($pkgrel)"

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/^v//'
}

prepare() {
  cd $pkgbase

  mkdir -p build

  cp "$srcdir"/config build/.config
}

build() {
  cd $pkgbase

  make O=build all
}

package_linux() {
  install=$pkgname.install

  cd $pkgbase

  local _kernelrelease=$(make O=build -s kernelrelease)
  local _image_name=$(make O=build -s image_name)

  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 O=build modules_install

  install -dm600 "$pkgdir"/boot/efi/
  install -Dm644 "build/$_image_name" "$pkgdir/boot/vmlinuz-$_kernelrelease"
  install -Dm644 "build/$_image_name" "$pkgdir/boot/efi/vmlinuz$LOCALVERSION"
  install -Dm644 "$srcdir/config" "$pkgdir/boot/config-$_kernelrelease"

  ln -vsf --no-dereference "/usr/src/linux-headers-$_kernelrelease" "$pkgdir/usr/lib/modules/$_kernelrelease/source"
  ln -vsf --no-dereference "/usr/src/linux-headers-$_kernelrelease/build" "$pkgdir/usr/lib/modules/$_kernelrelease/build"

  install -Dm755 /dev/stdin "$pkgdir"/boot/update-kernel.sh << EOF
#!/bin/bash
#
# update-kernel.sh
#

if [[ \$EUID -ne 0 ]]; then
    echo "This script must be run as root."
    exit 1
fi

update-initramfs -k all -c

cp -v -- /boot/initrd.img /boot/efi/initrd.img
cp -v -- /boot/vmlinuz /boot/efi/vmlinuz
cp -v -- /boot/initrd.img-$_kernelrelease /boot/efi/initrd$LOCALVERSION.img
EOF
}

package_linux-headers() {
  cd $pkgbase

  local _kernelrelease=$(make O=build -s kernelrelease)

  install -dm755 "$pkgdir"/usr/src/
  rsync -a \
    --exclude='*.o' \
    --exclude='*.ko*' \
    --exclude='*.a' \
    --exclude='*.cmd' \
    --exclude='*.o.d' \
    --exclude='*.so.dbg' \
    --exclude='.git*' \
    --exclude='.checked*' \
    --exclude='.clang-format' \
    --exclude='.cocciconfig' \
    --exclude='.get_maintainer.ignore' \
    --exclude='.mailmap' \
    --exclude='.missing-syscalls.d' \
    --exclude='.rustfmt.toml' \
    --exclude='.tmp_vmlinux*' \
    --exclude='.vmlinux' \
    --exclude='vmlinux.bin' \
    --exclude='build/arch/x86/boot/setup.elf' \
    --exclude='COPYING' \
    --exclude='CREDITS' \
    --exclude='Documentation' \
    --exclude='LICENSES' \
    --exclude='MAINTAINERS' \
    --exclude='README' \
    --include='arch/Kconfig' \
    --include='arch/x86' \
    --exclude='arch/*' \
    --exclude='tools/build/**.c' \
    --include='build/**.c' \
    --include='scripts/**.c' \
    --exclude='*.c' \
    "$srcdir/$pkgbase/" "$pkgdir/usr/src/linux-headers-$_kernelrelease"
}
