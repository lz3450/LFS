# Maintainer: KZL <lz3450@outlook.com>

pkgname=rust
pkgver=1.89.0
pkgrel=1
epoch=
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('KZL')
depends=(
  llvm-project
)
source=(
  # git+https://github.com/rust-lang/rust.git#branch=stable
  git+https://github.com/rust-lang/rust.git#tag=$pkgver
)
sha256sums=('83f9e6eeffe12ad235893877188d54d0927a1a7632db0806d37edc531553ddea')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-g.*//;s/-/.g/'
}

build() {
  cd $pkgname

  local _target="$(clang -print-target-triple)"
  local _change_id=$(grep -oP 'change_id: \K\d+' src/bootstrap/src/utils/change_tracker.rs | sort -n | tail -n1)
  local -a _options=(
    --disable-debug
    --disable-docs
    --disable-compiler-docs
    --disable-optimize-tests
    --disable-verbose-tests
    --disable-local-rust
    --enable-rpath
    --disable-codegen-tests
    --disable-ninja
    --disable-locked-deps
    --disable-vendor
    --disable-sanitizers
    --disable-dist-src
    --disable-cargo-native-static
    --disable-profiler
    --disable-full-tools
    --disable-lld
    --disable-llvm-bitcode-linker
    --disable-control-flow-guard
    --disable-patch-binaries-for-nix
    --enable-new-symbol-mangling
    "--prefix=/$PREFIX"
    "--sysconfdir=/etc"
    "--llvm-root=/$PREFIX"
    "--llvm-config=/$PREFIX/bin/llvm-config"
    "--llvm-filecheck=/$PREFIX/bin/FileCheck"
    "--python=/$PREFIX/bin/python3"
    "--release-channel=\"auto-detect\""
    "--release-description=KZL $(date '+%Y/%m/%d %T %z')"
    "--enable-manage-submodules"
    "--tools=cargo,clippy,rustfmt,rust-analyzer,rust-analyzer-proc-macro-srv,src,wasm-component-ld"
    "--build=$_target"
    "--host=$_target"
    "--target=$_target"
    --enable-option-checking
    --enable-verbose-configure
    "--set=change-id=$_change_id"
    "--set=llvm.download-ci-llvm=false"
    "--set=build.compiletest-use-stage0-libtest=false"
    "--set=rust.download-rustc=false"
    "--set=rust.use-lld=true"
    "--set=rust.llvm-tools=false"
    "--set=rust.llvm-bitcode-linker=false"
    "--set=rust.remap-debuginfo=true"
    "--set=rust.jemalloc=true"
    "--set=target.$_target.cc=/$PREFIX/bin/clang"
    "--set=target.$_target.cxx=/$PREFIX/bin/clang++"
    "--set=target.$_target.ar=/$PREFIX/bin/llvm-ar"
    "--set=target.$_target.ranlib=/$PREFIX/bin/llvm-ranlib"
  )

  msg2 "Configuring..."
  ./configure \
    "${_options[@]}"
  cat bootstrap.toml

  msg2 "Compiling..."
  RUST_BACKTRACE=full ./x.py -j "$(nproc)" build
}

check() {
  cd $pkgname

  ./x.py test
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ./x.py -j "$(nproc)" install

  rm -vf "$pkgdir/$PREFIX"/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}
  rm -vrf "$pkgdir/$PREFIX"/share/doc

  # zsh completions
  install -dm755 "$pkgdir"/usr/share
  mv -v "$pkgdir/$PREFIX/share/zsh" "$pkgdir/usr/share/zsh"
  mv -v "$pkgdir/usr/share/zsh/site-functions" "$pkgdir/usr/share/zsh/vendor-completions"
}
