# Maintainer: KZL <lz3450@outlook.com>

pkgname=rust
pkgver=1.93.1
pkgrel=9
epoch=
pkgdesc="Systems programming language focused on safety, speed and concurrency"
arch=('x86_64')
url="https://www.rust-lang.org/"
license=('KZL')
depends=(
  llvm-project
)
source=(
  # git+https://github.com/rust-lang/rust.git#branch=stable
  git+https://github.com/rust-lang/rust.git#tag=$pkgver
)
sha256sums=('858974283b1b813b7bdd31543bd60969d1f35b62f0f7fc544b682155157833ec')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-g.*//;s/-/.g/'
}

build() {
  cd $pkgname

  local _target="$(clang -print-target-triple)"
  local _change_id=$(grep -oP 'change_id: \K\d+' src/bootstrap/src/utils/change_tracker.rs | sort -n | tail -n1)
  local -a _options=(
    --disable-debug
    --disable-docs
    --disable-compiler-docs
    --enable-rpath
    --disable-lld
    --disable-llvm-bitcode-linker
    --enable-new-symbol-mangling
    "--prefix=/$PREFIX"
    "--sysconfdir=/$PREFIX/etc"
    "--llvm-root=/$PREFIX"
    "--llvm-config=/$PREFIX/bin/llvm-config"
    "--llvm-filecheck=/$PREFIX/bin/FileCheck"
    "--python=/$PREFIX/bin/python3"
    "--release-channel=stable"
    "--enable-manage-submodules"
    "--tools=cargo,clippy,rustfmt,rust-analyzer,rust-analyzer-proc-macro-srv,analysis,src"
    "--build=$_target"
    "--host=$_target"
    "--target=$_target"
    --enable-option-checking
    --enable-verbose-configure
    "--set=change-id=$_change_id"
    "--set=llvm.download-ci-llvm=false"
    "--set=gcc.download-ci-gcc=false"
    "--set=rust.download-rustc=false"
    "--set=rust.llvm-tools=false"
    "--set=rust.remap-debuginfo=true"
    "--set=target.$_target.cc=/$PREFIX/bin/clang"
    "--set=target.$_target.cxx=/$PREFIX/bin/clang++"
    "--set=target.$_target.ar=/$PREFIX/bin/llvm-ar"
    "--set=target.$_target.ranlib=/$PREFIX/bin/llvm-ranlib"
    "--set=target.$_target.linker=/$PREFIX/bin/clang"
  )

  msg2 "Configuring..."
  ./configure \
    "${_options[@]}"
  cat bootstrap.toml

  msg2 "Compiling..."
  RUST_BACKTRACE=full ./x.py -j "$(nproc)" build
}

check() {
  cd $pkgname

  ./x.py test
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ./x.py -j "$(nproc)" install

  rm -vf "$pkgdir/$PREFIX"/lib/rustlib/{components,install.log,manifest-*,rust-installer-version,uninstall.sh}
  rm -vrf "$pkgdir/$PREFIX"/share/doc

  # zsh completions
  install -dm755 "$pkgdir"/usr/share
  mv -v "$pkgdir/$PREFIX/share/zsh" "$pkgdir/usr/share/zsh"
  mv -v "$pkgdir/usr/share/zsh/site-functions" "$pkgdir/usr/share/zsh/vendor-completions"
}
