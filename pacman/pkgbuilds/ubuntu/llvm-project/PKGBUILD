# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=llvm-project
pkgver=20.1.7
pkgrel=11
epoch=
pkgdesc="Collection of modular and reusable compiler and toolchain technologies"
arch=('x86_64')
url="https://llvm.org/"
license=('KZL')
source=(
  git+https://github.com/llvm/llvm-project.git#tag=llvmorg-$pkgver
)
sha256sums=('79cfde7b151f2a9810a4de23e1ce9e2c1100bafe1ec4e200ef6f598834860a17')

# https://releases.llvm.org/20.1.0/docs/CMake.html

_llvm_options=(
  # -DLLVM_HOST_TRIPLE="$CHOST"
  # -DLLVM_DEFAULT_TARGET_TRIPLE="x86_64-unknown-linux-gnu"
  ### bolt;clang;clang-tools-extra;compiler-rt;cross-project-tests;libc;libclc;lld;lldb;mlir;openmp;polly;pstl;flang
  -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lld"
  ### libc;libunwind;libcxxabi;pstl;libcxx;compiler-rt;openmp;llvm-libgcc;offload
  -DLLVM_ENABLE_RUNTIMES=""
  -DLLVM_TARGETS_TO_BUILD="X86"
  -DLLVM_PARALLEL_COMPILE_JOBS=$(nproc)
  -DLLVM_PARALLEL_LINK_JOBS=$(nproc)
  -DLLVM_BUILD_LLVM_DYLIB=ON
  -DLLVM_LINK_LLVM_DYLIB=ON
  ################################################################
  -DLLVM_INCLUDE_TOOLS=ON
  -DLLVM_BUILD_TOOLS=ON
  -DLLVM_INCLUDE_DOCS=OFF
  -DLLVM_BUILD_DOCS=OFF
  -DLLVM_INCLUDE_EXAMPLES=OFF
  -DLLVM_BUILD_EXAMPLES=OFF
  -DLLVM_INCLUDE_TESTS=OFF
  -DLLVM_BUILD_TESTS=OFF
  -DLLVM_INCLUDE_BENCHMARKS=OFF
  -DLLVM_BUILD_BENCHMARKS=OFF
  ################################################################
  -DLLVM_ENABLE_FFI=ON
  -DLLVM_ENABLE_RTTI=ON
  -DLLVM_ENABLE_BINDINGS=OFF
  -DLLVM_ENABLE_ZSTD=FORCE_ON
  -DLLVM_INSTALL_UTILS=ON
  -DLLVM_APPEND_VC_REV=OFF
)

_clang_options=(
  -DCLANG_ENABLE_BOOTSTRAP=OFF
  -DCLANG_DEFAULT_LINKER=lld
)

_stage1_options=(
  ################################################################
  ### rtlib
  ################################################################
  -DCLANG_DEFAULT_RTLIB=libgcc
  ################################################################
)

_stage2_options=(
  -DCMAKE_C_COMPILER="/$PREFIX/bin/clang"
  -DCMAKE_CXX_COMPILER="/$PREFIX/bin/clang++"
  ### LLVM_ENABLE_LLD and LLVM_USE_LINKER can't be set at the same time
  # -DLLVM_USE_LINKER=lld
  -DLLVM_ENABLE_LLD=ON
  -DLLVM_ENABLE_LTO=ON
  -DLLVM_ENABLE_FATLTO=OFF
  -DLLVM_ENABLE_Z3_SOLVER=OFF
  ################################################################
  ### rtlib
  ################################################################
  # -DCLANG_DEFAULT_RTLIB=compiler-rt
  # -DCOMPILER_RT_DEFAULT_TARGET_ONLY=ON
  -DCLANG_DEFAULT_RTLIB=libgcc
  ################################################################
  ### libc/libc++
  ################################################################
  # -DLLVM_ENABLE_LLVM_LIBC=ON
  # -DLLVM_ENABLE_LIBCXX=ON
  # -DCLANG_DEFAULT_CXX_STDLIB=libc++
  # -DLIBC_ENABLE_USE_BY_CLANG=ON
  # -DLIBCXX_USE_COMPILER_RT=ON
  # -DLIBCXXABI_USE_COMPILER_RT=ON
  # -DLIBCXXABI_USE_LLVM_UNWINDER=ON
  ################################################################
)

_options=("${_llvm_options[@]}" "${_clang_options[@]}")

if [[ -f "/$PREFIX/bin/llvm-config" ]]; then
  _options+=("${_stage2_options[@]}")
else
  _options+=("${_stage1_options[@]}")
fi

build() {
  cd $pkgname

  msg2 "Configuring..."
  cmake -LAH -S llvm -B build -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX="/$PREFIX" \
    -D CMAKE_INSTALL_LIBEXECDIR="libexec/llvm-${pkgver%%.*}" \
    -D CMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS" \
    -D CMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS" \
    -D CMAKE_EXPORT_COMPILE_COMMANDS=ON \
    "${_options[@]}"

  msg2 "Compiling..."
  ninja -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ninja -C build install
}
