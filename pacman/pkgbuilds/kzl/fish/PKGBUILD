# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fish
pkgver=3.6.1
pkgrel=6
epoch=
pkgdesc="A smart and user-friendly command line shell"
arch=('x86_64')
url="https://fishshell.com/"
license=('KZL')
depends=(ncurses pcre2)
makedepends=()
optdepends=()
backup=(etc/fish/config.fish)
install=$pkgname.install
source=(git+https://github.com/fish-shell/fish-shell.git#tag=$pkgver)
sha256sums=('SKIP')

build() {
  cd fish-shell

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DBUILD_DOCS=False \
    -S . \
    -B build \
    -LAH

  cmake --build build
}

check() {
  cd fish-shell

  cmake --build build --target test
}

package() {
  cd fish-shell

  DESTDIR="$pkgdir" cmake --build build --target install
}
