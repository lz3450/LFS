# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=texinfo
pkgver=7.2
pkgrel=25
epoch=
pkgdesc="GNU documentation system for on-line information and printed output"
arch=('x86_64')
url="https://www.gnu.org/software/texinfo/"
license=('KZL')
groups=(base-devel)
depends=(
  ncurses
)
makedepends=(
  make
)
source=(
  # git+git://git.savannah.gnu.org/texinfo.git#tag=$pkgname-$pkgver
  https://ftp.gnu.org/gnu/texinfo/$pkgname-$pkgver.tar.xz{,.sig}
  texinfo-install.hook
  texinfo-remove.hook
)
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9')
sha256sums=('0329d7788fbef113fa82cb80889ca197a344ce0df7646fe000974c5d714363a6'
            'SKIP'
            '52be0c630f1310a789a8f09b8d3799ab5034ea4e2c0865ae3a8ea5f058e8a158'
            'daa6a96836063bee75146e59e53167cf95ab154439d06d3f3de34e5f6ff8a69c')

# prepare() {
#   cd $pkgname-$pkgver

#   ./autogen.sh
# }

build() {
  cd $pkgname-$pkgver

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/texinfo-{install,remove}.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
