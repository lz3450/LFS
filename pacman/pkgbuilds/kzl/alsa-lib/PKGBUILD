# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=alsa-lib
pkgver=1.2.5
pkgrel=1
epoch=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('alsa-topology-conf' 'alsa-ucm-conf')
provides=('libasound.so' 'libatopology.so')
source=(https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('9092894a8c083b33acf8d6deb901b58f5d20d6da583789f814e8e46f2850ef18'
            'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --without-debug

  make
}

check() {
  cd ${pkgname}-${pkgver}

  LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}" make -k check || warning "Check failed."
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}/" install
  make DESTDIR="${pkgdir}/" -C doc install
}
