# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tzdata
pkgver=2025b
pkgrel=7
epoch=
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64')
url="https://www.iana.org/time-zones"
license=('KZL')
options=(!emptydirs)
source=(https://www.iana.org/time-zones/repository/releases/tzcode$pkgver.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/$pkgname$pkgver.tar.gz{,.asc})
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34')
sha256sums=('05f8fedb3525ee70d49c87d3fae78a8a0dbae4fe87aa565c65cda9948ae135ec'
            'SKIP'
            '11810413345fc7805017e27ea9fa4885fd74cd61b2911711ad038f5d28d71474'
            'SKIP')

_timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' 'southamerica' 'etcetera' 'backward' 'factory')

build() {
  make CC=gcc LFLAGS="$LDFLAGS $LTOFLAGS"
}

package() {
  make DESTDIR="$pkgdir" install

  # install tzdata stuff
  ./zic -d "$pkgdir"/usr/share/zoneinfo ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/posix ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo/right -L leapseconds ${_timezones[@]}
  ./zic -d "$pkgdir"/usr/share/zoneinfo -p US/Central

  # cleanup
  rm "$pkgdir"/etc/localtime
}
