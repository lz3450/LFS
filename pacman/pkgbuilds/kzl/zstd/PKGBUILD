# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zstd
pkgver=1.5.7
pkgrel=7
epoch=
pkgdesc="Zstandard - Fast real-time compression algorithm"
arch=('x86_64')
url="http://www.zstd.net/"
license=('KZL')
depends=(
  lz4
  xz
  zlib
)
source=(git+https://github.com/facebook/zstd.git#tag=v$pkgver)
sha256sums=('e43521ef6e50b3c0f42612f6147c2f74c1c3af18e1c2d56fb91b8ddcb590c699')

build() {
  cd $pkgname/build/cmake

  msg2 "Configuring..."
  cmake -LAH -S . -B build -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -D ZSTD_BUILD_CONTRIB=ON \
    -D ZSTD_BUILD_TESTS=ON \
    -D ZSTD_LZ4_SUPPORT=ON \
    -D ZSTD_LZMA_SUPPORT=ON \
    -D ZSTD_PROGRAMS_LINK_SHARED=ON \
    -D ZSTD_ZLIB_SUPPORT=ON

  msg2 "Compiling..."
  cmake --build build
}

check() {
  cd $pkgname/build/cmake

  cmake --build build --target test
}

package() {
  cd $pkgname/build/cmake

  DESTDIR="$pkgdir" cmake --build build --target install
}
