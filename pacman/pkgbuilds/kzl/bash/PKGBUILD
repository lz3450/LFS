# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bash
pkgver=5.2.g39
pkgrel=2
epoch=
pkgdesc="The GNU Bourne-Again SHell"
arch=('x86_64')
url="https://www.gnu.org/software/bash/"
license=('KZL')
depends=(
  glibc
  ncurses
  readline
)
provides=(sh)
source=(
  git+git://git.savannah.gnu.org/bash.git#branch=master
  dot.bashrc
  dot.bash_logout
  dot.bash_profile
  bash.bashrc
  bash.bash_logout
)
sha256sums=('SKIP'
            'c9a014340a9973b4e59ce500f949491080565a8df6119506de452e89fddce923'
            'b02c0ac51db9f1a5a8ae76056280d41ace41f82cd69194e84ee7a721eae2b907'
            'e297955ec4e6060a539e2a028cb67a4f41d5632c3360415cd14bb7d0af7b030c'
            '3f45e0002bb378a19267655a90e7d6416dc55bf8abc112656f48cce2a20e5f01'
            '6682154699081666aa10f52116d1904a3045df46cda3b7d7ff123e5e93d63f29')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/bash-//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  sed -i \
    -e '/#define STANDARD_UTILS_PATH/{n; s|:/etc:/usr/etc||}' \
    -e '/#define SYS_BASHRC/s|/\*\(.*\)\*/|\1|' \
    -e '/#define SYS_BASH_LOGOUT/s|/\*\(.*\)\*/|\1|' \
    -e '/#define NON_INTERACTIVE_LOGIN_SHELLS/s|/\*\(.*\)\*/|\1|' \
    config-top.h
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --with-bash-malloc=no \
    --with-curses \
    --with-installed-readline

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  ln -vs bash "$pkgdir"/usr/bin/sh

  install -Dm644 "$srcdir"/bash.bashrc "$pkgdir"/etc/bash.bashrc
  install -Dm644 "$srcdir"/bash.bash_logout "$pkgdir"/etc/bash.bash_logout

  install -dm755 "$pkgdir"/etc/skel/
  install -Dm644 "$srcdir"/dot.bashrc "$pkgdir"/etc/skel/.bashrc
  install -Dm644 "$srcdir"/dot.bash_profile "$pkgdir"/etc/skel/.bash_profile
  install -Dm644 "$srcdir"/dot.bash_logout "$pkgdir"/etc/skel/.bash_logout
}
