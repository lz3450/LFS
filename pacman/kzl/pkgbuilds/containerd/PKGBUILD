# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=containerd
pkgver=1.4.1
pkgrel=1
epoch=1
pkgdesc="An open and reliable container runtime"
arch=('x86_64')
url="https://containerd.io/"
depends=(runc)
makedepends=(btrfs-progs go libseccomp)
source=(git+https://github.com/containerd/containerd.git#tag=v$pkgver?signed)
sha256sums=('SKIP')
validpgpkeys=('8C7A111C21105794B0E8A27BF58C5D0A4405ACDB')

prepare () {
  mkdir -p src/github.com/containerd
  cp -r $pkgname src/github.com/containerd
  sed -e 's,/sbin,/usr/bin,;s,/usr/local,/usr,' -i src/github.com/containerd/$pkgname/containerd.service
}

build() {
  cd src/github.com/containerd/$pkgname

  LDFLAGS=${LDFLAGS/-Wl,/}

  export GOPATH="$srcdir"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  make VERSION=v$pkgver REVISION=$pkgrel
}

check() {
  cd src/github.com/containerd/$pkgname

  make GOFLAGS="" test
}

package() {
  cd src/github.com/containerd/$pkgname

  make DESTDIR="$pkgdir/usr" install

  install -Dm644 containerd.service "$pkgdir"/usr/lib/systemd/system/containerd.service
}
