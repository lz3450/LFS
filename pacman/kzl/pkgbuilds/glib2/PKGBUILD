# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=glib2
pkgver=2.68.2
pkgrel=1
epoch=1
pkgdesc="Low level core library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GLib"
depends=(libffi pcre)
makedepends=(elfutils shared-mime-info)
optdepends=("elfutils: gresource inspection tool")
source=(git+https://gitlab.gnome.org/GNOME/glib.git#tag=$pkgver
        gio-querymodules.hook
        glib-compile-schemas.hook)
sha256sums=('SKIP'
            '2a6ed172633430753af2d4eb2bc266c2e12713c0a68d8e12e885ea0595a86460'
            '018c62a1087461cb1f5dd2a879667d7218e4990c1b4c322e584dc3ef311342ca')

build() {
  cd glib

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --default-library both \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D selinux=disabled \
    -D man=false \
    -D gtk_doc=false \
    build

  meson compile -C build
}

check() {
  cd glib

  meson test -C build --no-suite flaky --print-errorlogs || warning "Check failed."
}

package() {
  cd glib

  DESTDIR="$pkgdir/" meson install -C build

  sed -e 's/, sysprof-capture-4//' -i "$pkgdir"/usr/lib/pkgconfig/*.pc

  install -Dm644 "$srcdir"/*.hook -t "$pkgdir"/usr/share/libalpm/hooks
}
