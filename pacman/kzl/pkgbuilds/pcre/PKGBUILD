# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pcre
pkgver=8.45
pkgrel=1
epoch=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('x86_64')
url="https://www.pcre.org/"
license=('KZL')
source=(https://versaweb.dl.sourceforge.net/project/pcre/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('4dae6fdcd2bb0bb6c37b5f97c33c2be954da743985369cddac3546e3218bffb8'
            'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-unicode-properties \
    --enable-pcre16 \
    --enable-pcre32 \
    --enable-jit \
    --enable-pcregrep-libz \
    --enable-pcregrep-libbz2 \
    --enable-pcretest-libreadline

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
