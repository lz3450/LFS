# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gnupg
pkgver=2.2.27
pkgrel=1
epoch=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
arch=('x86_64')
url="https://www.gnupg.org/"
depends=(gnutls libassuan libgcrypt libgpg-error libksba npth pinentry sqlite)
makedepends=(libusb-compat libldap pcsclite)
optdepends=("libusb-compat: scdaemon"
            "libldap: gpg2keys_ldap"
            "pcsclite: scdaemon")
checkdepends=(openssh)
install=$pkgname.install
source=(https://gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('34e60009014ea16402069136e0a5f63d9b65f90096244975db5cea74b3d02399'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA')

prepare() {
  cd $pkgname-$pkgver

	rm doc/gnupg.info*

  rm -rf .git

  sed -e '/^\s*beta=yes/s/yes/no/' \
      -e '/^\s*tmp=\"-unknown\"/s/-unknown//' \
      -i autogen.sh

	./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --sysconfdir=/etc \
    --disable-doc \
    --enable-maintainer-mode

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

	ln -s gpg "${pkgdir}"/usr/bin/gpg2
	ln -s gpgv "${pkgdir}"/usr/bin/gpgv2

  install -Dm644 doc/examples/systemd-user/*.{service,socket} -t "$pkgdir"/usr/lib/systemd/user/
}
