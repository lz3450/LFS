# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libldap
pkgver=2.4.57
pkgrel=1
epoch=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
arch=('x86_64')
url="https://www.openldap.org"
depends=(libsasl)
source=($url/software/download/OpenLDAP/openldap-release/openldap-$pkgver.tgz)
sha256sums=('c7ba47e1e6ecb5b436f3d43281df57abeffa99262141aec822628bc220f6b45a')

prepare() {
  cd openldap-$pkgver

  sed -i 's|-m 644 $(LIBRARY)|-m 755 $(LIBRARY)|' libraries/{liblber,libldap,libldap_r}/Makefile.in
  sed -i 's|#define LDAPI_SOCK LDAP_RUNDIR LDAP_DIRSEP "run" LDAP_DIRSEP "ldapi"|#define LDAPI_SOCK LDAP_DIRSEP "run" LDAP_DIRSEP "openldap" LDAP_DIRSEP "ldapi"|' include/ldap_defaults.h
  sed -i 's|%LOCALSTATEDIR%/run|/run/openldap|' servers/slapd/slapd.{conf,ldif}
  sed -i 's|-$(MKDIR) $(DESTDIR)$(localstatedir)/run|-$(MKDIR) $(DESTDIR)/run/openldap|' servers/slapd/Makefile.in
}

build() {
  cd openldap-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var/lib/openldap \
    --disable-debug \
    --enable-dynamic \
    --enable-syslog \
    --enable-ipv6 \
    --enable-local \
    --disable-slapd \
    --enable-crypt \
    --enable-spasswd \
    --enable-modules \
    --enable-backends=mod \
    --enable-overlays=mod \
    --with-cyrus-sasl \
    --with-threads

  make depend
  make
}

package() {
  cd openldap-$pkgver

  for dir in include libraries doc/man/man3 ; do
    pushd $dir
    make DESTDIR="$pkgdir/" install
    popd
  done

  install -Dm644 doc/man/man5/ldap.conf.5.tmp "$pkgdir"/usr/share/man/man5/ldap.conf.5

  ln -sf liblber.so "${pkgdir}"/usr/lib/liblber.so.2
  ln -sf libldap.so "${pkgdir}"/usr/lib/libldap.so.2

  rm "$pkgdir"/etc/openldap/*.default
}
