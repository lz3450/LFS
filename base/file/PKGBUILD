# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=file
pkgver=5.38
pkgrel=1
pkgdesc="File type identification utility"
arch=(x86_64)
url="https://www.darwinsys.com/file/"
depends=(zlib)
source=(ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('593c2ffc2ab349c5aea0f55fedfe4d681737b6b62376a9b3ad1e77b2cc19fa34'
            'SKIP')
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A')

build() {
  cd $pkgname-$pkgver

  # Fix linking libmagic (vfork needs libpthread)
  CFLAGS+=" -pthread"

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/file \
    --enable-fsect-man5 \
    --enable-libseccomp

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
