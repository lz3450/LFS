# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=util-linux
_pkgmajor=2.35
pkgver=$_pkgmajor.1
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=(x86_64)
url="https://github.com/karelzak/util-linux"
depends=(libcap-ng pam shadow)
optdepends=("words: default dictionary for look")
backup=(etc/pam.d/chfn
        etc/pam.d/chsh
        etc/pam.d/login
        etc/pam.d/runuser
        etc/pam.d/runuser-l
        etc/pam.d/su
        etc/pam.d/su-l)
source=(https://www.kernel.org/pub/linux/utils/util-linux/v$_pkgmajor/$pkgname-$pkgver.tar.{xz,sign}
        pam-{login,common,runuser,su})
sha256sums=('d9de3edd287366cd908e77677514b9387b22bc7b88f45b83e1922c3597f1d7f9'
            'SKIP'
            '993a3096c2b113e6800f2abbd5d4233ebf1a97eef423990d3187d665d3490b92'
            'fc6807842f92e9d3f792d6b64a0d5aad87995a279153ab228b1b2a64d9f32f20'
            '95b7cdc4cba17494d7b87f37f8d0937ec54c55de0e3ce9d9ab05ad5cc76bf935'
            '51eac9c2a2f51ad3982bba35de9aac5510f1eeff432d2d63c6362e45d620afc0')
validpgpkeys=('B0C64D14301CC6EFAEDF60E4E4B71D5EEC39C284')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-fs-paths-default=/usr/bin:/usr/local/bin \
    --enable-raw \
    --enable-vipw \
    --enable-newgrp \
    --enable-chfn-chsh \
    --enable-write \
    --enable-mesg \
    --disable-hardlink \
    --with-python=3

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" usrsbin_execdir=/usr/bin install

  # install PAM files for login-utils
  install -dm755 "$pkgdir"/etc/pam.d
  install -m644 "$srcdir"/pam-common "$pkgdir"/etc/pam.d/chfn
  install -m644 "$srcdir"/pam-common "$pkgdir"/etc/pam.d/chsh
  install -m644 "$srcdir"/pam-login "$pkgdir"/etc/pam.d/login
  install -m644 "$srcdir"/pam-runuser "$pkgdir"/etc/pam.d/runuser
  install -m644 "$srcdir"/pam-runuser "$pkgdir"/etc/pam.d/runuser-l
  install -m644 "$srcdir"/pam-su "$pkgdir"/etc/pam.d/su
  install -m644 "$srcdir"/pam-su "$pkgdir"/etc/pam.d/su-l
}
