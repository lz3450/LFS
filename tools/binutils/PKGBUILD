# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> binutils (pass 1) -> gcc (pass 1) -> glibc

pkgname=lfs-tools-binutils
pkgver=2.34
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64' 'aarch64')
url="https://www.gnu.org/software/binutils/"
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig})
sha256sums=('f00b0e8803dc9bab1e2165bd568528135be734df3fabf8d0161828cd56028952'
            'SKIP')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

prepare() {
  cd binutils-$pkgver

  mkdir build

  sed -e "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" -i libiberty/configure
}

build() {
  cd binutils-$pkgver/build

  ../configure \
    --target=$CHOST \
    --prefix=/usr \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --without-system-zlib \
    --disable-shared \
    --enable-plugins \
    --without-debuginfod \
    --with-pic \
    --enable-deterministic-archives \
    --enable-relro \
    --enable-threads \
    --disable-nls \
    --with-sysroot=/usr/$CHOST \
    --with-build-sysroot=/usr/$CHOST

  make
}

check() {
  cd binutils-$pkgver/build

  make -k LDFLAGS="" check || warning "Check failed."
}

package() {
  cd binutils-$pkgver/build

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/usr/share/
}
