# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> binutils (pass 1) -> gcc (pass 1) -> glibc

pkgname=lfs-tools-glibc
pkgver=2.31
pkgrel=7
arch=('x86_64' 'aarch64')
url="https://www.gnu.org/software/libc"
options=(!strip)
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig})
sha256sums=('9246fe44f68feeec8c666bb87973d590ce0137cca145df014c72ec95be9ffd17'
            'SKIP')
validpgpkeys=('BC7C7372637EC10C57D7AA6579C43DFBF1CF2187')

prepare() {
  cd glibc-$pkgver
  
  mkdir build
}

build() {
  cd glibc-$pkgver/build

  PATH=/tools/bin:$PATH
  BUILD_CC=gcc
  CC=$CHOST-gcc
  CXX=$CHOST-g++
  AR=$CHOST-ar
  RANLIB=$CHOST-ranlib
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}
  LDFLAGS=${LDFLAGS/,-z,now/}

  echo "slibdir=/tools/$CHOST/lib" >> configparms
  echo "rtlddir=/tools/$CHOST/lib" >> configparms
  echo "sbindir=/tools/$CHOST/bin" >> configparms
  echo "rootsbindir=/tools/$CHOST/bin" >> configparms
  echo "build-programs=no" >> configparms

  ../configure \
    --prefix=/tools \
    --sbindir=/tools/$CHOST/bin \
    --libexecdir=/tools/$CHOST/lib \
    --libdir=/tools/$CHOST/lib \
    --includedir=/tools/$CHOST/include \
    --with-headers=/tools/$CHOST/include \
    --enable-kernel=5.4.42 \
    --enable-shared \
    --enable-static-pie \
    --enable-cet \
    --disable-profile \
    --disable-timezone-tools \
    --enable-stack-protector=strong \
    --enable-bind-now \
    --host=$CHOST \
    --with-pkgversion="LFS Tools $pkgver-$pkgrel" \
    --enable-multi-arch
  
  make
}

check() {
  cd glibc-$pkgver/build

  make check || warning "Check failed."
}

package() {
  cd glibc-$pkgver/build

  make install_root="$pkgdir/" install

  rm -rf "$pkgdir"/tools/{etc,var,share/info}
}
