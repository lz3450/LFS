# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zziplib
pkgver=0.13.72
pkgrel=7
epoch=
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('x86_64')
url="http://zziplib.sourceforge.net"
license=('KZL')
source=(git+https://github.com/gdraheim/zziplib.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DZZIPDOCS=OFF \
    -DZZIPTEST=OFF \
    -S . \
    -B build \
    -G Ninja \
    -LAH

  cmake --build build
}

check() {
  cd $pkgname

  cmake --build build --target tests
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" cmake --build build --target install
}
