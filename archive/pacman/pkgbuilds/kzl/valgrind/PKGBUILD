# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=valgrind
pkgver=3.22.0
pkgrel=6
epoch=
pkgdesc="Tool to help find memory-management problems in programs"
arch=('x86_64')
url="http://valgrind.org/"
license=('KZL')
makedepends=(gdb)
checkdepends=(procps-ng)
source=(git+https://sourceware.org/git/valgrind.git#tag=VALGRIND_${pkgver//./_})
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-only64bit \
    --enable-lto

  make
}

check() {
  cd $pkgname

  ./vg-in-place --error-exitcode=1 /bin/true

  make check CPPFLAGS="" CFLAGS="" CXXFLAGS="" LDFLAGS=""
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
