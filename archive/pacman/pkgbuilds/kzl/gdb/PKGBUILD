# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gdb
pkgver=14.2.31
pkgrel=2
epoch=
pkgdesc="The GNU Project debugger"
arch=('x86_64')
url="https://www.gnu.org/software/gdb"
license=('KZL')
depends=(elfutils expat guile mpfr)
# depends=(elfutils expat guile mpfr source-highlight)
backup=(etc/gdb/gdbinit)
source=(git+https://sourceware.org/git/binutils-gdb.git#branch=gdb-14-branch)
sha256sums=('SKIP')

# TODO: source-highlight

pkgver() {
  cd binutils-gdb

  git describe --tags | sed 's/^gdb.//;s/release.//;s/-g.*//;s/-/./g'
}

prepare() {
  cd binutils-gdb

  mkdir -p build
}

build() {
  cd binutils-gdb/build

  "../configure" \
    --prefix=/usr \
    --enable-lto \
    --enable-tui \
    --disable-source-highlight \
    --enable-threading \
    --with-system-zlib \
    --with-zstd \
    --with-system-readline \
    --with-expat \
    --with-python=/usr/bin/python3 \
    --with-guile=yes \
    --with-system-gdbinit=/etc/gdb/gdbinit

  make
}

package() {
  cd binutils-gdb/build

  make -C gdb DESTDIR="$pkgdir" install
  make -C gdbserver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/etc/gdb
  touch "$pkgdir"/etc/gdb/gdbinit
}
