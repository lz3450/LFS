# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=vim
pkgver=8.2.4106
pkgrel=1
epoch=1
pkgdesc="Vi Improved, a highly configurable, improved version of the vi text editor"
arch=('x86_64')
url="https://www.vim.org"
license=('KZL')
# depends=(acl gpm libffi libgcrypt pcre)
depends=(acl libffi libgcrypt pcre)
# makedepends=(lua ruby tcl)
# optdepends=("lua: Lua language support"
#             "ruby: Ruby language support"
#             "tcl: Tcl language support")
source=(https://github.com/vim/vim/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('49dc438e8cef4eae387ab2cdd79eb1f0e1ac47acfa8eb25cd209085fc2896179')

# TODO: gpm

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib/vim \
    --enable-luainterp=dynamic \
    --enable-perlinterp=dynamic \
    --enable-python3interp=dynamic \
    --enable-tclinterp=dynamic \
    --enable-rubyinterp=dynamic \
    --enable-cscope \
    --disable-netbeans \
    --enable-multibyte \
    --disable-gui \
    --disable-canberra \
    --enable-acl \
    --disable-gpm \
    --with-features=huge \
    --with-compiledby="KZL" \
    --without-x

  make
}

check() {
  cd $pkgname-$pkgver

  make TERM=xterm -j1 test
}

package() {
  cd $pkgname-$pkgver

  make VIMRCLOC="/etc" DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/etc
  touch "$pkgdir"/etc/vimrc
}
