# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=graphite
pkgver=1.3.14.96
pkgrel=4
epoch=
pkgdesc="A system that can be used to create “smart fonts” capable of displaying writing systems with various complex behaviors"
arch=('x86_64')
url="https://github.com/silnrsi/graphite"
license=('KZL')
makedepends=(freetype2)
source=(git+https://github.com/silnrsi/graphite.git)
sha256sums=('SKIP')

pkgver() {
  cd graphite

  git describe --tags | sed 's/-g.*//;s/-/./'
}

build() {
  cd graphite

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -S . \
    -B build \
    -LAH

  cmake --build build
}

check() {
  cd graphite

  cmake --build build --target tests
}

package() {
  cd graphite

  DESTDIR="$pkgdir" cmake --build build --target install
}
