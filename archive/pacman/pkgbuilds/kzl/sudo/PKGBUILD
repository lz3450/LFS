# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=sudo
pkgver=1.9.14p3
pkgrel=4
epoch=
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64')
url="https://www.sudo.ws/sudo/"
depends=(
  glibc
  libgcrypt
  openldap
  openssl
  pam
)
backup=(
  etc/sudo.conf
  etc/sudoers
  etc/pam.d/sudo
)
source=(
  git+https://github.com/sudo-project/sudo.git#tag=SUDO_${pkgver//./_}
  sudo.pam
)
sha256sums=('SKIP'
            'b8270d4fbd5896428a83e8cd4a2d7217a3cc04c798da178911933465b1e85d1e')

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/libexec/sudo \
    --enable-tmpfiles.d \
    --disable-sasl \
    --enable-openssl \
    --with-linux-audit \
    --with-sssd \
    --with-pam \
    --with-logging=syslog \
    --with-passprompt="[sudo] password for %p: " \
    --with-rundir=/run/sudo \
    --with-vardir=/var/db/sudo \
    --with-editor="/usr/bin/nano:/usr/bin/vim:/usr/bin/vi" \
    --with-env-editor \
    --with-all-insults \
    --with-ldap \
    --with-ldap-conf-file=/etc/openldap/ldap.conf

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  # remove /run/sudo directory, using systemd-tmpfiles
  rmdir "$pkgdir/run/sudo"
  rmdir "$pkgdir/run"


  install -Dm644 "$srcdir"/sudo.pam "$pkgdir"/etc/pam.d/sudo
}
