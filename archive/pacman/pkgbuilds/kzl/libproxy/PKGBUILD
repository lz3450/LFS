# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libproxy
pkgver=0.4.15+70+gcd619d3
pkgrel=1
epoch=1
pkgdesc="Automatic proxy configuration management library"
arch=('x86_64')
url="https://libproxy.github.io/libproxy/"
depends=(glib2)
makedepends=(js60 libnm webkit2gtk)
optdepends=("networkmanager: NetworkManager configuration module")
source=(git+https://github.com/libproxy/libproxy.git#commit=cd619d3e9d683237f6317f979d5c6a7290d7e429)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname

  cmake \
    -DBIPR=0 \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DPERL_VENDORINSTALL=ON \
    -DWITH_PYTHON2=OFF \
    -DWITH_WEBKIT3=ON \
    -S . \
    -B build

  cmake --build build
}

package() {
  cd $pkgname

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
