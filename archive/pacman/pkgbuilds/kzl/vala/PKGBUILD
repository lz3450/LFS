# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=vala
pkgver=0.56.3
pkgrel=1
epoch=1
pkgdesc="Compiler Using the GObject Type System"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/Vala"
license=('KZL')
depends=(glib2 gobject-introspection graphviz)
makedepends=(libxslt vala)
options=(debug)
source=(git+https://gitlab.gnome.org/GNOME/vala.git#tag=$pkgver)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
