# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=mtools
pkgver=4.0.43
pkgrel=4
epoch=
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('KZL')
backup=(etc/$pkgname.conf)
source=(https://ftp.gnu.org/gnu/mtools/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('541e179665dc4e272b9602f2074243591a157da89cc47064da8c5829dbd2b339'
            'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839')

prepare() {
  cd $pkgname-$pkgver

  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-floppyd \
    --without-x

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 mtools.conf -t "$pkgdir"/etc/
}
