# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tinyxml
pkgver=2.6.2
pkgrel=1
epoch=1
pkgdesc="Simple, small XML parser"
arch=('x86_64')
url="http://grinninglizard.com/tinyxml/"
makedepends=(setconf)
source=(https://downloads.sourceforge.net/$pkgname/${pkgname}_${pkgver//./_}.tar.gz)
sha256sums=('15bdfdcec58a7da30adc87ac2b078e4417dbe5392f3afb719f9ba6d062645593')

prepare() {
  cd $pkgname

  setconf Makefile TINYXML_USE_STL YES
  setconf Makefile RELEASE_CFLAGS "$CXXFLAGS -fPIC"
}

build() {
  cd $pkgname

  make
  g++ "$CXXFLAGS" -fPIC -shared -o lib$pkgname.so.0.$pkgver -Wl,-soname,"lib$pkgname.so.0" $(ls *.o | grep -v xmltest)
}

check() {
  cd $pkgname

  ./xmltest || warning "Check failed."
}

package() {
  cd $pkgname

  install -dm755 "$pkgdir"/usr/{lib,include}
  install -Dm755 lib$pkgname.so.0.$pkgver "$pkgdir"/usr/lib/
  install -Dm644 $pkgname.h tinystr.h "$pkgdir"/usr/include
  echo | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/pkgconfig/$pkgname.pc << EOF
prefix=/usr
exec_prefix=\${prefix}
libdir=\${exec_prefix}/lib
includedir=\${prefix}/include

Name: TinyXml
Description: simple, small, C++ XML parser
Version: $pkgver
Libs: -L\${libdir} -ltinyxml
Cflags: -I\${includedir}
EOF
}
