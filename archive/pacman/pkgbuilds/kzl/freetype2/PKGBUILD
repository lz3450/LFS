# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# freetype2 (without harfbuzz) -> harfbuzz -> freetype2 (with harfbuzz)

pkgname=freetype2
pkgver=2.13.1
pkgrel=7
epoch=
pkgdesc="Font rasterization library"
arch=('x86_64')
url="https://www.freetype.org/"
license=('KZL')
depends=(brotli harfbuzz libpng)
backup=(etc/profile.d/freetype2.sh)
source=(git+https://gitlab.freedesktop.org/freetype/freetype.git#tag=VER-${pkgver//./-})
sha256sums=('SKIP')
build() {
  cd freetype

  meson setup \
    --prefix /usr \
    --auto-features enabled \
    --optimization 2 \
    -D b_lto=true \
    -D b_pie=true \
    -D brotli=enabled \
    -D harfbuzz=enabled \
    -D png=enabled \
    build

  meson compile -C build
}

check() {
  cd freetype

  meson test -C build --print-errorlogs
}

package() {
  cd freetype

  meson install -C build --destdir="$pkgdir"
}
