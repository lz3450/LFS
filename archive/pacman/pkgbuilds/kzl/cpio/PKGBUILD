# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cpio
pkgver=2.14
pkgrel=15
epoch=
pkgdesc="A tool to copy files into or out of a cpio or tar archive"
arch=('x86_64')
url="https://www.gnu.org/software/cpio"
license=('KZL')
source=(git://git.savannah.gnu.org/cpio.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  rmdir gnulib
  ln -sr "$srcdir"/gnulib
  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/libexec/rmt
  rmdir "$pkgdir"/usr/libexec/
  rm "$pkgdir"/usr/share/man/man8/rmt.8
  rmdir "$pkgdir"/usr/share/man/man8/
}
