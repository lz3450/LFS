# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libunwind
pkgver=1.6.2
pkgrel=1
epoch=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"
arch=('x86_64')
url="https://www.nongnu.org/libunwind"
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('4a6aec666991fb45d0889c44aede8ad6eb108071c3554fcdff671f9c94794976'
            'SKIP')
validpgpkeys=('75D2CFC56CC2E935A4143297015A268A17D55FA4')

build() {
  cd $pkgname-$pkgver

  CFLAGS+=" -fcommon"

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
