# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=smartmontools
pkgver=7.3
pkgrel=7
epoch=
pkgdesc="Control and monitor storage systems using the Self-Monitoring, Analysis and Reporting Technology System (SMART) built into most modern ATA/SATA, SCSI/SAS and NVMe disks"
arch=('x86_64')
url="https://www.smartmontools.org/"
license=('KZL')
depends=(libcap-ng)
backup=(etc/smartd.conf
        etc/conf.d/smartd)
source=(git+https://github.com/smartmontools/smartmontools.git#tag=RELEASE_${pkgver//./_}
        smartd)
sha256sums=('SKIP'
            '0f10b0945e4d136e039cb7e8318a2854c2c13fdaffc48f4726959103f8a92d97')
validpgpkeys=('SKIP')

prepare() {
  cd $pkgname/$pkgname

  # use `uname -n` instead of `hostname`
  sed -e "/^os_hostname/c os_hostname=\"'uname -n'\"" -i configure.ac

  ./autogen.sh
}

build() {
  cd $pkgname/$pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-drivedbdir=yes \
    --with-smartdscriptdir=/usr/share/smartmontools \
    --with-smartdplugindir=/usr/share/smartmontools/smartd_warning.d \
    --with-savestates=yes \
    --with-attributelog=yes \
    --with-libcap-ng=yes \
    --with-libsystemd=yes \
    --with-systemdenvfile=/etc/conf.d/smartd

  make
}

package() {
  cd $pkgname/$pkgname

  sed -e "s:sysconfig/smartmontools:conf.d/smartd:g" \
      -e "s:smartd_opts:SMARTD_OPTS:g" \
      -i smartd.service

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/smartd "$pkgdir"/etc/conf.d/smartd
}
