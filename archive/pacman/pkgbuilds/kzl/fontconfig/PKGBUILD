# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fontconfig
pkgver=2.14.2
pkgrel=6
epoch=
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('KZL')
depends=(expat freetype2)
makedepends=(gperf)
install=$pkgname.install
source=(git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#tag=$pkgver
        fontconfig.hook)
sha256sums=('SKIP'
            '8883f7e6e9d574ed52b89256507a6224507925715ddc85b3dfab9683df2f1e25')

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-docbook \
    --disable-docs \
    --disable-cache-build \
    --with-default-fonts="/usr/share/fonts" \
    --with-add-fonts="/usr/local/share/fonts"

  make
}

check() {
  cd $pkgname

  make -k check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/fontconfig.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
