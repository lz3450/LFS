# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nspr
pkgver=4.35
pkgrel=1
epoch=
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
license=('KZL')
depends=(glibc)
source=(https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v$pkgver/src/$pkgname-$pkgver.tar.gz)
sha256sums=('7ea3297ea5969b5d25a5dd8d47f2443cda88e9ee746301f6e1e1426f8a6abc8f')

build() {
  cd $pkgname-$pkgver/$pkgname

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --includedir=/usr/include/nspr \
    --enable-optimize \
    --disable-debug \
    --enable-64bit

  make
}

package() {
  cd $pkgname-$pkgver/$pkgname

  make DESTDIR="$pkgdir/" install
  ln -s nspr.pc "$pkgdir"/usr/lib/pkgconfig/mozilla-nspr.pc
  rm -r "$pkgdir"/usr/include/nspr/md
  rm "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties}
}
