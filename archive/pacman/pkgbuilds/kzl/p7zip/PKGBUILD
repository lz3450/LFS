# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p7zip
pkgver=17.04
pkgrel=1
epoch=2
pkgdesc="Command-line file archiver with high compression ratio"
arch=('x86_64')
url="https://github.com/jinfeihan57/p7zip"
license=('KZL')
source=(https://github.com/jinfeihan57/p7zip/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
        oemcp-iconv.patch::https://github.com/jinfeihan57/p7zip/commit/c104127e6a93.patch
        oemcp-cygwin.patch::https://github.com/jinfeihan57/p7zip/commit/0e0a1a8316b6.patch
        oemcp-apple.patch::https://github.com/jinfeihan57/p7zip/commit/334a01e3f8bc.patch)
sha256sums=('ea029a2e21d2d6ad0a156f6679bd66836204aa78148a4c5e498fe682e77127ef'
            '50f2068124c1c6adc2d9ccf455e9816eccd39d31920b953ab8fcb853232d452c'
            '789789216ca46e5ea9b6780e353f964a1f46e9a2bcaa6b16413adcf9a89eb374'
            'dfe1a625f80078c3ec68038f0aae8ba3a82ea96d0aeb97cb3586dc7fb3ef9650')

prepare() {
  cd $pkgname-$pkgver

  # https://github.com/jinfeihan57/p7zip/issues/112
  patch -Rp1 -i "$srcdir"/oemcp-apple.patch
  patch -Rp1 -i "$srcdir"/oemcp-cygwin.patch
  patch -Rp1 -i "$srcdir"/oemcp-iconv.patch

}

build() {
  cd $pkgname-$pkgver

  make OPTFLAGS="$CPPFLAGS $CFLAGS" 7z 7zr 7za
}

package() {
  cd $pkgname-$pkgver

  make DEST_DIR="$pkgdir/" DEST_HOME=/usr DEST_MAN=/usr/share/man install
  rm -r "$pkgdir"/usr/share/doc/p7zip/DOC/MANUAL/fm
}
