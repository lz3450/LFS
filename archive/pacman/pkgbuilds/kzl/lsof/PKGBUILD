# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=lsof
pkgver=4.95.0
pkgrel=1
epoch=1
pkgdesc="Lists open files for running Unix processes"
arch=('x86_64')
url="https://github.com/lsof-org/lsof"
license=('KZL')
depends=(libtirpc)
source=(https://github.com/lsof-org/lsof/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8ff4c77736cc7d9556da9e2c7614cc4292a12f1979f20bd520d3c6f64b66a4d7')

prepare() {
  cd $pkgname-$pkgver
  
  sed -i 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h
  sed -i "s|.so ./version|.ds VN $pkgver|" -i Lsof.8
}

###############################################################################

build() {
  cd $pkgname-$pkgver

  ./Configure -n linux

  make CC="gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS}"
}

package() {
  cd $pkgname-$pkgver

  install -Dm 755 lsof -t "$pkgdir"/usr/bin
  install -Dm 644 Lsof.8 "$pkgdir"/usr/share/man/man8/lsof.8
}
