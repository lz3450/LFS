# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=screen
pkgver=4.9.0
pkgrel=1
epoch=1
pkgdesc="Full-screen window manager that multiplexes a physical terminal"
arch=('x86_64')
url="https://www.gnu.org/software/screen/"
license=('KZL')
backup=(etc/screenrc
        etc/pam.d/screen)
source=(https://ftp.gnu.org/gnu/screen/$pkgname-$pkgver.tar.gz{,.sig}
        pam
        tmpfiles)
sha256sums=('f9335281bb4d1538ed078df78a20c2f39d3af9a4e91c57d084271e0289c730f4'
            'SKIP'
            '9573753d2da9e88cfa00f95f1df67b515b855746aea452fd3d4bda805bdb37db'
            '1f33ce4faca7bd05dd80403411af31e682d5d23e79558e884ae5a35f1dd96223')
validpgpkeys=('B4560FD8C9F2DE3BE308EBA3933AD21886F69FBF')

prepare() {
  cd $pkgname-$pkgver

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
		--prefix=/usr \
		--enable-pam \
		--enable-telnet \
		--enable-colors256 \
		--enable-rxvt_osc \
		--with-socket-dir=/run/screens \
		--with-pty-group=5 \
		--with-sys-screenrc=/etc/screenrc

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

	install -Dm644 etc/etcscreenrc "$pkgdir"/etc/screenrc
	install -Dm644 etc/screenrc "$pkgdir"/etc/skel/.screenrc

  install -Dm644 "$srcdir"/pam "$pkgdir"/etc/pam.d/screen
	install -Dm644 "$srcdir"/tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/screen.conf
}
