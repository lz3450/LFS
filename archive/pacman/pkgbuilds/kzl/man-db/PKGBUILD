# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=man-db
pkgver=2.11.2
pkgrel=2
epoch=
pkgdesc="A utility for reading man pages"
arch=('x86_64')
url="https://www.nongnu.org/man-db/"
license=('KZL')
depends=(groff less libpipeline libseccomp)
backup=(etc/man_db.conf)
source=(git+https://gitlab.com/man-db/man-db.git#tag=$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  GNULIB_REFDIR="$srcdir"/gnulib ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/sbin \
    --sysconfdir=/etc \
    --enable-cache-owner=root \
    --disable-setuid \
    --enable-mandirs=GNU \
    --with-db=gdbm \
    --with-sections="1 n l 8 3 0 2 5 4 9 6 7"

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
