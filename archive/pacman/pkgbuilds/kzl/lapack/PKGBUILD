# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=lapack
pkgname=(lapack blas cblas lapacke)
pkgver=3.10.0
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.netlib.org/lapack"
source=($pkgname-$pkgver.tar.gz::https://github.com/Reference-LAPACK/lapack/archive/v$pkgver.tar.gz)
sha256sums=('328c1bea493a32cac5257d84157dc686cc3ab0b004e2bea22044e0a59f6f8a19')

build() {
  cd $pkgbase-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DLAPACKE_WITH_TMG=ON \
    -DCBLAS=ON \
    -DBUILD_DEPRECATED=ON \
    -S . \
    -B build

  cmake --build build
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"

  cd $pkgbase-$pkgver

  cmake --build build/BLAS --target install -- DESTDIR="$pkgdir/"
}

package_cblas() {
  pkgdesc="C interface to BLAS"
  depends=(blas)
  
  cd $pkgbase-$pkgver

  cmake --build build/CBLAS --target install -- DESTDIR="$pkgdir/"
}

package_lapack() {
  pkgdesc="Linear Algebra PACKage"
  depends=(blas)
  
  cd $pkgbase-$pkgver
  
  cmake --build build --target install -- DESTDIR="$pkgdir/"

  rm -r "$pkgdir"/usr/lib/{libblas.*,libcblas.*,liblapacke.*}
  rm -r "$pkgdir"/usr/lib/pkgconfig/{blas.*,cblas.*,lapacke.*}
  rm -r "$pkgdir"/usr/lib/cmake/{cblas*,lapacke*}
  rm -r "$pkgdir"/usr/include
}

package_lapacke() {
  pkgdesc="C interface to LAPACK"
  depends=(lapack)
  
  cd $pkgbase-$pkgver

  cmake --build build/LAPACKE --target install -- DESTDIR="$pkgdir/"
}
