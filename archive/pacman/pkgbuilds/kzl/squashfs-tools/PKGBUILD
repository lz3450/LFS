# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=squashfs-tools
pkgver=4.5
pkgrel=1
epoch=1
pkgdesc="Tools for squashfs, a highly compressed read-only filesystem for Linux"
arch=('x86_64')
url="https://github.com/plougher/squashfs-tools"
license=('KZL')
depends=(lz4 lzo)
source=($pkgname-$pkgver.tar.gz::https://github.com/plougher/$pkgname/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('b9e16188e6dc1857fe312633920f7d71cc36b0162eb50f3ecb1f0040f02edddd')

prepare() {
  cd $pkgname-$pkgver

  sed -e '/ln -fs \$(INSTALL_DIR)\/unsquashfs/s|\$(INSTALL_DIR)\/||' \
      -e '/ln -fs \$(INSTALL_DIR)\/mksquashfs/s|\$(INSTALL_DIR)\/||' \
      -i $pkgname/Makefile
}

build() {
  cd $pkgname-$pkgver

  make -C $pkgname \
    GZIP_SUPPORT=1 \
    LZ4_SUPPORT=1 \
    LZMA_XZ_SUPPORT=1 \
    LZO_SUPPORT=1 \
    XATTR_SUPPORT=1 \
    XZ_SUPPORT=1 \
    ZSTD_SUPPORT=1
}

package() {
  cd $pkgname-$pkgver

  make -C $pkgname INSTALL_DIR="$pkgdir"/usr/bin install
}
