# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=wayland
pkgver=1.21.0
pkgrel=1
epoch=1
pkgdesc="a protocol for a compositor to talk to its clients as well as a C library implementation of that protocol"
arch=('x86_64')
url="https://wayland.freedesktop.org"
license=('KZL')
depends=(expat libffi libxml2)
source=(https://gitlab.freedesktop.org/wayland/wayland/-/releases/$pkgver/downloads/wayland-$pkgver.tar.xz)
sha256sums=('6dc64d7fc16837a693a51cfdb2e568db538bfdc9f457d4656285bb9594ef11ac')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48')

build() {
  cd $pkgname-$pkgver

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D documentation=false \
    build

  ninja -C build
}

check() {
  cd $pkgname-$pkgver

  ninja -C build test
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
}
