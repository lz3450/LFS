# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=sqlite
pkgver=3.42.0
pkgrel=4
epoch=
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
url="https://www.sqlite.org"
license=('KZL')
makedepends=(tcl)
source=(git+https://github.com/sqlite/sqlite.git#tag=version-$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  # https://www.sqlite.org/compile.html
  export CPPFLAGS="$CPPFLAGS \
    -DSQLITE_SECURE_DELETE=1 \
    -DSQLITE_ENABLE_COLUMN_METADATA=1 \
    -DSQLITE_ENABLE_DBPAGE_VTAB=1
    -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
    -DSQLITE_ENABLE_STMTVTAB=1 \
    -DSQLITE_ENABLE_UNLOCK_NOTIFY=1"

  ./configure \
    --prefix=/usr \
    --enable-static=no \
    --disable-amalgamation \
    --enable-fts5 \
    --enable-rtree

  make
  make showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -Dm755 showdb showjournal showstat4 showwal sqldiff sqlite3_analyzer "$pkgdir"/usr/bin/
}
