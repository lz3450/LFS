# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=poppler
pkgver=21.11.0
pkgrel=1
epoch=1
pkgdesc=""
arch=('x86_64')
url="https://poppler.freedesktop.org/"
depends=(boost cairo curl fontconfig gobject-introspection lcms2 libjpeg nss openjpeg2 poppler-data)
provides=(poppler-glib)
conflicts=(poppler-glib)
replaces=(poppler-glib)
source=(https://poppler.freedesktop.org/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('31b76b5cac0a48612fdd154c02d9eca01fd38fb8eaa77c1196840ecdeb53a584'
            'SKIP')
validpgpkeys=('CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7')

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DENABLE_GTK_DOC=OFF \
    -DENABLE_QT5=OFF \
    -DENABLE_QT6=OFF \
    -DWITH_GTK:BOOL=OFF \
    -S . \
    -B build

  cmake --build build
}

check() {
  cd $pkgname-$pkgver

  cmake --build build --target tests || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
