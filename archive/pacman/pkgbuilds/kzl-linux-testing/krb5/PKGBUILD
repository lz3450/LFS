# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=krb5
pkgver=1.21.3
pkgrel=1
epoch=
pkgdesc="The Kerberos network authentication protocol"
arch=('x86_64')
url="https://web.mit.edu/kerberos/"
license=('KZL')
depends=(
  e2fsprogs
  keyutils
  libedit
  lmdb
  openssl
)
backup=(
  etc/krb5.conf
  var/krb5kdc/kdc.conf
)
options=(!lto)
source=(git+https://github.com/krb5/krb5.git#tag=$pkgname-$pkgver-final)
sha256sums=('8b17d1c874b49d36fd59c99c00b930d350ff1af03d4a74561faf46c75e6d9960')

prepare() {
  cd $pkgname/src

  autoreconf -vfi
}

build() {
  cd $pkgname/src

  # https://web.mit.edu/kerberos/krb5-latest/doc/build/options2configure.html
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --enable-dns-for-realm \
    --disable-rpath \
    --with-system-et \
    --with-system-ss \
    --with-crypto-impl=openssl \
    --with-tls-impl=openssl \
    --with-lmdb

  make
}

package() {
  cd $pkgname/src

  make DESTDIR="$pkgdir" EXAMPLEDIR="/usr/share/doc/$pkgname/examples" install

  install -Dpm644 config-files/krb5.conf -t "$pkgdir"/etc/$pkgname/
  install -Dpm644 config-files/kdc.conf -t "$pkgdir"/var/$pkgname/krb5kdc/
}
