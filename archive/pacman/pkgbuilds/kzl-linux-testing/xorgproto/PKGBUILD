# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# When releasing a xorgproto version with updated keysyms, rebuild libx11

pkgname=xorgproto
pkgver=2021.3
pkgrel=1
epoch=1
pkgdesc="combined X.Org X11 Protocol headers"
arch=('any')
url="https://xorg.freedesktop.org/"
makedepends=(xorg-util-macros)
source=(https://xorg.freedesktop.org/archive/individual/proto/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('4c732b14fc7c7db64306374d9e8386d6172edbb93f587614df1938b9d9b9d737'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF')

build() {
  cd $pkgname-$pkgver

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    build

  meson compile -C build
}

check() {
  cd $pkgname-$pkgver

  meson test -C build || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir/" meson install -C build

  # cleanup
  rm -f "$pkgdir"/usr/include/X11/extensions/apple*
  rm -f "$pkgdir"/usr/share/licenses/$pkgname/COPYING-{apple,windows}wmproto
  rm -f "$pkgdir"/usr/share/pkgconfig/applewmproto.pc
}
