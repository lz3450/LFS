# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxfixes
pkgver=5.0.3
pkgrel=1
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=(libx11)
makedepends=(xorg-util-macros xorgproto)
source=($url/releases/individual/lib/libXfixes-$pkgver.tar.bz2{,.sig})
sha256sums=('de1cd33aff226e08cefd0e6759341c2c8e8c9faf8ce9ac6ec38d43e287b22ad6'
            'SKIP')
validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8')

build() {
  cd libXfixes-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd libXfixes-$pkgver

  make DESTDIR="$pkgdir/" install
}
