# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=xcb-proto
pkgver=1.14.1
pkgrel=1
epoch=1
pkgdesc="XML-XCB protocol descriptions"
arch=('x86_64')
url="https://xcb.freedesktop.org/"
makedepends=(libxml2)
source=(https://xorg.freedesktop.org/archive/individual/proto/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f04add9a972ac334ea11d9d7eb4fc7f8883835da3e4859c9afa971efdf57fcc3'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
