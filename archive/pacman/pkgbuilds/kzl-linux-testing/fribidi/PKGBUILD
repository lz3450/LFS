# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fribidi
pkgver=1.0.11
pkgrel=1
epoch=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
arch=('x86_64')
url="https://github.com/fribidi/fribidi/"
license=('KZL')
source=(git+https://github.com/fribidi/fribidi#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D docs=false \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
