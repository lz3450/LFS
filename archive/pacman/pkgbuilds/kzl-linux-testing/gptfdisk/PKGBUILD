# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gptfdisk
pkgver=1.0.8
pkgrel=1
epoch=1
pkgdesc="A text-mode partitioning tool that works on GUID Partition Table (GPT) disks"
arch=('x86_64')
url="https://www.rodsbooks.com/gdisk/"
license=('KZL')
depends=(popt)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('95d19856f004dabc4b8c342b2612e8d0a9eebdd52004297188369f152e9dc6df')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://sourceforge.net/p/gptfdisk/discussion/939590/thread/bc29ca06f8/
  sed -e '/^#include /s:ncursesw/::' -i gptcurses.cc
}

build() {
  cd $pkgname-$pkgver

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  install -d "$pkgdir"/usr/{bin,share/{doc/gdisk,man/man8}}
  install {,c,s}gdisk fixparts -t "$pkgdir"/usr/bin
  install -m644 {{,c,s}gdisk,fixparts}.8 -t "$pkgdir"/usr/share/man/man8
}
