# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libice
pkgver=1.0.10
pkgrel=1
epoch=1
pkgdesc="X11 Inter-Client Exchange library"
arch=('x86_64')
url="https://xorg.freedesktop.org"
makedepends=(xorg-util-macros xorgproto xtrans)
source=($url/releases/individual/lib/libICE-$pkgver.tar.bz2{,.sig})
sha256sums=('6f86dce12cf4bcaf5c37dddd8b1b64ed2ddf1ef7b218f22b9942595fb747c348'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')

build() {
  cd libICE-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd libICE-$pkgver

  make DESTDIR="$pkgdir/" install
}
