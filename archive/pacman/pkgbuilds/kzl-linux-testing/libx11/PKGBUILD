# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libx11
pkgver=1.7.0
pkgrel=1
epoch=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=(libxcb xorgproto)
makedepends=(xtrans)
source=($url/releases/individual/lib/libX11-$pkgver.tar.bz2{,.sig})
sha256sums=('36c8f93b6595437c8cfbc9f08618bcb3041cbd303e140a0013f88e4c2977cb54'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011')

build() {
  cd libX11-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-xf86bigfont

  make
}

check() {
  cd libX11-$pkgver

  make check || warning "Check failed."
}

package() {
  cd libX11-$pkgver

  make DESTDIR="$pkgdir/" install
}
