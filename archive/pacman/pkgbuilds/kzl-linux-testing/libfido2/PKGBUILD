# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libfido2
pkgver=1.10.0
pkgrel=1
epoch=1
pkgdesc="Library functionality for FIDO 2.0, including communication with a device over USB"
arch=('x86_64')
url="https://developers.yubico.com/libfido2"
license=('KZL')
depends=(hidapi libcbor openssl)
source=(https://developers.yubico.com/$pkgname/Releases/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('526efd3d56af706c05d09f3d21f18ee3b0b15ac0c1f5c5da1acbc27c2730b99b'
            'SKIP')
validpgpkeys=('EE90AE0D19774C8386628FAAB428949EF7914718')

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -S . \
    -B build
  
  cmake --build build
}

package() {
  cd $pkgname-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
