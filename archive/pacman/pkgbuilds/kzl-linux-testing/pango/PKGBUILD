# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pango
pkgver=1.50.7
pkgrel=1
epoch=2
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
url="https://www.pango.org/"
license=('KZL')
depends=(cairo fontconfig freetype2 fribidi glib2 harfbuzz libthai )
makedepends=(gobject-introspection help2man)
source=(git+https://gitlab.gnome.org/GNOME/pango.git#tag=$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    -D gtk_doc=false \
    -D xft=disabled \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir/" meson install -C build
}
