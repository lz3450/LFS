# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxcb
pkgver=1.14
pkgrel=1
epoch=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xcb.freedesktop.org/"
depends=(libxau libxdmcp xcb-proto)
makedepends=(libxslt xorgproto)
source=(https://xorg.freedesktop.org/archive/individual/lib/$pkgname-$pkgver.tar.xz)
sha256sums=('a55ed6db98d43469801262d81dc2572ed124edc3db31059d4e9916eb9f844c34')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static

  make
}

check() {
  cd $pkgname-$pkgver

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
