# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=clang
pkgver=13.0.1
pkgrel=1
epoch=1
pkgdesc="C language family frontend for LLVM"
arch=('x86_64')
url="https://clang.llvm.org/"
license=('KZL')
depends=(compiler-rt llvm)
optdepends=("openmp: OpenMP support in clang with -fopenmp")
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-project-$pkgver.src.tar.xz{,.sig})
sha256sums=('326335a830f2e32d06d0a36393b5455d17dc73e0bd1211065227ee014f92cbf8'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

_cmake_options=(
  -DCMAKE_BUILD_TYPE=Release
  -DCMAKE_INSTALL_PREFIX=/usr
  -DCMAKE_INSTALL_LIBDIR=lib
  -DCMAKE_INSTALL_LIBEXECDIR=lib
  -DCMAKE_SKIP_RPATH=ON
  # -DCMAKE_C_COMPILER=/usr/bin/clang
  # -DCMAKE_CXX_COMPILER=/usr/bin/clang++
  # -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS"
  # -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS"
  # -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=lld"
  # -DCMAKE_SHARED_LINKER_FLAGS="-fuse-ld=lld"
  # -DCMAKE_MODULE_LINKER_FLAGS="-fuse-ld=lld"
  # -DCMAKE_AR=/usr/bin/llvm-ar
  # -DCMAKE_NM=/usr/bin/llvm-nm
  # -DCMAKE_RANLIB=/usr/bin/llvm-ranlib
  # -DCMAKE_OBJCOPY=/usr/bin/llvm-objcopy
  # -DCMAKE_OBJDUMP=/usr/bin/llvm-objdump
  # -DCMAKE_READELF=/usr/bin/llvm-readelf
)

_llvm_options=(
  -DLLVM_MAIN_SRC_DIR="$srcdir"/llvm-project-$pkgver.src/llvm
  -DLLVM_EXTERNAL_LIT=/usr/bin/lit
  # -DLLVM_ENABLE_LTO=Full
)

prepare() {
  cd llvm-project-$pkgver.src/$pkgname

  mv ../clang-tools-extra tools/extra
}

build() {
  cd llvm-project-$pkgver.src/$pkgname

  cmake \
    "${_cmake_options[@]}" \
    "${_llvm_options[@]}" \
    -S . \
    -B build \
    -G Ninja

  ninja -C build all
}

check() {
  cd llvm-project-$pkgver.src/$pkgname

  LD_LIBRARY_PATH=$PWD/build/lib ninja -C build check-clang{,-tools}
}

package() {
  cd llvm-project-$pkgver.src/$pkgname

  DESTDIR="$pkgdir/" ninja -C build install

  sed -e 's|libexec|lib/clang|' -i \
      "$pkgdir"/usr/bin/scan-build
  mv "$pkgdir"/usr/libexec/* "$pkgdir"/usr/lib/clang/
  rmdir "$pkgdir"/usr/libexec
}
