# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=cunit
pkgver=2.1
pkgrel=3
epoch=1
pkgdesc="A Unit Testing Framework for C"
arch=('x86_64')
url="http://cunit.sourceforge.net"
license=('KZL')
source=(https://downloads.sourceforge.net/project/cunit/CUnit/$pkgver-$pkgrel/CUnit-$pkgver-$pkgrel.tar.bz2)
sha256sums=('f5b29137f845bb08b77ec60584fdb728b4e58f1023e6f249a464efa49a40f214')

prepare() {
  cd CUnit-$pkgver-$pkgrel

  libtoolize --force --copy
  aclocal
  autoheader
  automake --add-missing --include-deps --copy
  autoconf
}

build() {
  cd CUnit-$pkgver-$pkgrel

  ./configure \
    --prefix=/usr \
    --enable-test

  make
}

check() {
  cd CUnit-$pkgver-$pkgrel

  ./CUnit/Sources/Test/test_cunit
}

package() {
  cd CUnit-$pkgver-$pkgrel

  make DESTDIR="$pkgdir/" install

  rm -r "$pkgdir"/usr/share/CUnit
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/
}
