# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libc++
pkgver=13.0.1
pkgrel=1
epoch=1
pkgdesc="a new implementation of the C++ standard library, targeting C++11 and above."
arch=('x86_64')
url="https://libcxx.llvm.org/"
license=('KZL')
depends=(llvm)
makedepends=(clang lld llvm)
provides=(libc++abi)
conflicts=(libc++abi)
replaces=(libc++abi)
source=(https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-$pkgver.src.tar.xz{,.sig}
        https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/libcxx-$pkgver.src.tar.xz{,.sig}
        https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/libcxxabi-$pkgver.src.tar.xz{,.sig})
sha256sums=('ec6b80d82c384acad2dc192903a6cf2cdbaffb889b84bfb98da9d71e630fc834'
            'SKIP'
            '2f446acc00bb7cfb4e866c2fa46d1b6dbf4e7d2ab62e3c3d84e56f7b9e28110f'
            'SKIP'
            'db5fa6093c786051e8b1c85527240924eceb6c95eeff0a2bbc57be8422b3cef1'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A')

_cmake_options=(
  -DCMAKE_BUILD_TYPE=Release
  -DCMAKE_INSTALL_PREFIX=/usr
  -DCMAKE_SKIP_RPATH=ON
  -DCMAKE_C_COMPILER=/usr/bin/clang
  -DCMAKE_CXX_COMPILER=/usr/bin/clang++
  -DCMAKE_C_FLAGS="${CFLAGS/-O2/-O3} $CPPFLAGS"
  -DCMAKE_CXX_FLAGS="${CXXFLAGS/-O2/-O3} $CPPFLAGS"
  -DCMAKE_LINKER=/usb/bin/ld.lld
  -DCMAKE_EXE_LINKER_FLAGS="-fuse-ld=lld"
  -DCMAKE_SHARED_LINKER_FLAGS="-fuse-ld=lld"
  -DCMAKE_MODULE_LINKER_FLAGS="-fuse-ld=lld"
  -DCMAKE_AR=/usr/bin/llvm-ar
  -DCMAKE_NM=/usr/bin/llvm-nm
  -DCMAKE_RANLIB=/usr/bin/llvm-ranlib
  -DCMAKE_OBJCOPY=/usr/bin/llvm-objcopy
  -DCMAKE_OBJDUMP=/usr/bin/llvm-objdump
  -DCMAKE_READELF=/usr/bin/llvm-readelf
)

_llvm_options=(
  -DLLVM_TARGETS_TO_BUILD="X86"
  -DLLVM_BUILD_LLVM_DYLIB=ON
  -DLLVM_BUILD_TESTS=ON
  -DLLVM_ENABLE_FFI=ON
  -DLLVM_ENABLE_RTTI=ON
  -DLLVM_INSTALL_UTILS=ON
  -DLLVM_LINK_LLVM_DYLIB=ON
  -DLLVM_USE_LINKER=lld
  -DLLVM_ENABLE_LTO=OFF
  -DLLVM_ENABLE_LIBCXX=ON
)

prepare() {
  mv "$srcdir"/libcxx-$pkgver.src llvm-$pkgver.src/projects/libcxx
  mv "$srcdir"/libcxxabi-$pkgver.src llvm-$pkgver.src/projects/libcxxabi
}

build() {
  cd llvm-$pkgver.src

  cmake \
    "${_cmake_options[@]}" \
    "${_llvm_options[@]}" \
    -S . \
    -B build \
    -G Ninja

  ninja -C build cxx
}

check() {
  cd llvm-$pkgver.src

  LD_LIBRARY_PATH=$PWD/build/lib ninja -C build check-cxx check-cxxabi
}

package() {
  cd llvm-$pkgver.src

  DESTDIR="$pkgdir" ninja -C build install-cxx
  DESTDIR="$pkgdir" ninja -C build install-cxxabi
}
