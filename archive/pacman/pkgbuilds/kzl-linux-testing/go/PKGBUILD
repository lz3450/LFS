# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=go
pkgver=1.15.5
pkgrel=1
epoch=3
pkgdesc="Core compiler tools for the Go programming language"
arch=('x86_64')
url="https://golang.org/"
makedepends=(go)
options=(!strip staticlibs)
source=(https://github.com/golang/go/archive/$pkgname$pkgver.tar.gz)
sha256sums=('268c12ffae000a5911a85992749eaf683a7e107018b99b1cbfedd8362ee39410')

build() {
  export GOROOT="$srcdir"/$pkgname-$pkgname$pkgver
  export GOROOT_FINAL=/usr/lib/go
  export GOPATH="$srcdir/"
  export GOBIN="$GOROOT"/bin
  export GOOS=linux
  export GOARCH=amd64

  cd $pkgname-$pkgname$pkgver/src

  ./make.bash --no-clean -v

  PATH="$GOBIN:$PATH" go install -v -race std
  PATH="$GOBIN:$PATH" go install -v -buildmode=shared std
}


check() {
  export GOROOT="$srcdir"/$pkgname-$pkgname$pkgver
  export GOROOT_FINAL=/usr/lib/go
  export GOPATH="$srcdir/"
  export GOBIN="$GOROOT"/bin
  export GOOS=linux
  export GOARCH=amd64
  export PATH="$GOBIN:$PATH"

  cd $pkgname-$pkgname$pkgver/src

  ./run.bash --no-rebuild -v -v -v -k
}

package() {
  cd $pkgname-$pkgname$pkgver

  install -dm755 "$pkgdir"/usr/bin 
  install -dm755 "$pkgdir"/usr/lib/go 
  install -dm755 "$pkgdir"/usr/share/doc/go

  cp -a bin pkg src lib misc api test "$pkgdir"/usr/lib/go
  cp -r doc/* "$pkgdir"/usr/share/doc/go

  ln -sf /usr/lib/go/bin/go "$pkgdir"/usr/bin/go
  ln -sf /usr/lib/go/bin/gofmt "$pkgdir"/usr/bin/gofmt
  ln -sf /usr/share/doc/go "$pkgdir"/usr/lib/go/doc

  install -Dm644 VERSION "$pkgdir"/usr/lib/go/VERSION

  rm -rf "$pkgdir"/usr/lib/go/pkg/bootstrap "$pkgdir"/usr/lib/go/pkg/tool/*/api
  rm -rf "$pkgdir"/usr/lib/go/pkg/obj/go-build
}
