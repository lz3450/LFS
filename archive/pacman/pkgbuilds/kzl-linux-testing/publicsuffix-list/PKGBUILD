# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=publicsuffix-list
pkgver=20220219.1236.2db6f46
pkgrel=1
epoch=1
pkgdesc="Cross-vendor public domain suffix database"
arch=('any')
url="https://github.com/publicsuffix/list"
license=('KZL')
source=(git+$url#commit=2db6f4601303b2ac2a5014a33356b72164088e41)
sha256sums=('SKIP')

pkgver() {
  cd list

  printf "%s.%s.%s" \
    "$(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

package() {
  cd list

  install -Dm644 public_suffix_list.dat tests/test_psl.txt -t "$pkgdir"/usr/share/publicsuffix
  ln -s public_suffix_list.dat "$pkgdir"/usr/share/publicsuffix/effective_tld_names.dat
}
