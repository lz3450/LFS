# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux-rpi3 linux-headers-rpi3)
pkgver=5.15
pkgrel=1
pkgdesc="LTS Linux Kernel"
arch=('aarch64')
url="https://www.kernel.org"
license=('KZL')
options=(!strip)
source=(git+https://github.com/raspberrypi/linux.git#branch=rpi-$pkgver.y
        config)
sha256sums=('SKIP'
            '41a36a952312f380e77d0c1d5b5d312c4414517c4ae45a9e632c68687c085446')

export KERNEL=kernel8
export KBUILD_BUILD_HOST=RPi
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $pkgbase

  mkdir build

  make O=build bcmrpi3_defconfig
  cp build/.config "$srcdir"/bcmrpi3_defconfig

  cp "$srcdir"/config build/.config

  make O=build nconfig
}

build() {
  cd $pkgbase

  make O=build --output-sync=target Image modules dtbs
}

package_linux-rpi3() {
  conflicts=(linux-rpi4)

  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)
  local image_name="arch/arm64/boot/Image"
  local modulesdir="/usr/lib/modules/$kernelrelease"

  msg2 "kernelrelease=$kernelrelease"
  msg2 "image_name=$image_name"

  make INSTALL_MOD_STRIP="--strip-unneeded" INSTALL_MOD_PATH="$pkgdir/usr" O=build --output-sync=target modules_install

  # make INSTALL_PATH="$pkgdir/boot" O=build install

  install -Dm755 build/$image_name "$pkgdir/$modulesdir"/kernel8.img
  install -Dm755 build/$image_name "$pkgdir"/boot/kernel8.img
  install -Dm644 build/arch/arm64/boot/dts/broadcom/*.dtb -t "$pkgdir"/boot/
  install -Dm644 build/arch/arm64/boot/dts/overlays/*.dtb* -t "$pkgdir"/boot/overlays/

  rm "$pkgdir"/usr/lib/modules/$kernelrelease/{build,source}
}

package_linux-headers-rpi3() {
  conflicts=(linux-rpi4)

  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)

  install -dm0755 "$pkgdir"/usr/lib/modules/$kernelrelease/
  cp -a "$srcdir"/$pkgbase "$pkgdir"/usr/lib/modules/$kernelrelease/source
  mv "$pkgdir"/usr/lib/modules/$kernelrelease/{source/,}build
}
