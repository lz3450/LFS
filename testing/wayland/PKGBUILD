# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=wayland
pkgver=1.18.0
pkgrel=1
pkgdesc="A computer display server protocol"
arch=('x86_64')
url="https://wayland.freedesktop.org"
depends=(libffi libxml2)
makedepends=()
source=($url/releases/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4675a79f091020817a98fd0484e7208c8762242266967f55a67776936c2e294d'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48')

build() {
  cd $pkgname-$pkgver

  meson setup \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --buildtype plain \
    --wrap-mode nodownload \
    -D b_lto=true \
    -D b_pie=true \
    build

  ninja -C build
}

check() {
  cd $pkgname-$pkgver

  meson test -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir/" meson install -C build 
}
