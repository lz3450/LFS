# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ruby
pkgver=2.7.6
pkgrel=1
epoch=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('x86_64')
url="https://www.ruby-lang.org"
license=('KZL')
depends=(gmp libffi libxcrypt openssl)
provides=(irb rubygems ruby-bundler)
conflicts=(irb rubygems ruby-bundler)
replaces=(irb rubygems ruby-bundler)
source=(https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('54dcd3044726c4ab75a9d4604720501442b229a3aed6a55fe909567da8807f24')

build() {
  cd $pkgname-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ruby \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --disable-rpath \
    --enable-shared \
    --disable-install-doc

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
