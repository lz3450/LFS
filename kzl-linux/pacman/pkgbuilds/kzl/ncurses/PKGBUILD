# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ncurses
pkgver=6.5.20250510
pkgrel=1
epoch=
pkgdesc="A free software emulation of curses in System V Release 4.0 (SVr4), and more"
arch=('x86_64')
url="https://invisible-island.net/ncurses/ncurses.html"
license=('KZL')
source=(https://invisible-mirror.net/archives/$pkgname/current/$pkgname-${pkgver%.*}-${pkgver##*.}.tgz)
sha256sums=('5886d54b13c661c55c16a340033be4b2bc11df6ab07602cd87cf812da3877833')

prepare() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  sed -i 's/@EXTRA_PKG_LDFLAGS@ //g' misc/gen-pkgconfig.in misc/ncurses-config.in
}

build() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --without-ada \
    --without-manpages \
    --enable-pc-files \
    --with-shared \
    --with-cxx-shared \
    --with-xterm-kbs=DEL \
    --disable-root-environ \
    --disable-root-access \
    --disable-setuid-environ

  msg2 "Compiling..."
  make
}

package() {
  cd $pkgname-${pkgver%.*}-${pkgver##*.}

  make DESTDIR="$pkgdir" install

  for lib in ncurses ncurses++ form menu panel; do
    echo "INPUT(-l${lib}w)" > "$pkgdir/usr/lib/lib$lib.so"
    ln -vs "${lib}w.pc" "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done

  echo "INPUT(-lncursesw)" > "$pkgdir/usr/lib/libcursesw.so"
  echo "INPUT(-lncurses)" > "$pkgdir/usr/lib/libcurses.so"

  for lib in tic tinfo; do
    echo "INPUT(-lncurses)" > "$pkgdir/usr/lib/lib$lib.so"
    ln -vs ncurses.pc "$pkgdir/usr/lib/pkgconfig/$lib.pc"
  done

  install -dm755 "$pkgdir"/usr/include/ncurses{,w}
  for include in "$pkgdir"/usr/include/*.h; do
    ln -vs "../$(basename $include)" "$pkgdir"/usr/include/ncurses
    ln -vs "../$(basename $include)" "$pkgdir"/usr/include/ncursesw
  done
}
