# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=lvm2
pkgname=(lvm2 device-mapper)
pkgver=2.03.20
pkgrel=2
epoch=
arch=('x86_64')
url="https://sourceware.org/lvm2/"
license=('KZL')
depends=(libedit)
makedepends=(thin-provisioning-tools)
source=(git+https://sourceware.org/git/$pkgbase.git#tag=v${pkgver//./_})
sha256sums=('SKIP')

build() {
  cd $pkgbase

  ./configure \
    --prefix=/usr \
    --disable-readline \
    --enable-editline \
    --enable-lvmpolld \
    --enable-cmdlib \
    --enable-pkgconfig \
    --enable-dmeventd \
    --with-default-pid-dir=/run \
    --with-default-dm-run-dir=/run \
    --with-default-run-dir=/run/lvm \
    --with-default-locking-dir=/run/lock/lvm

  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"

  cd $pkgbase

  make DESTDIR="$pkgdir" install_device-mapper
  make DESTDIR="$pkgdir" install_systemd_units

  rm -f "$pkgdir"/usr/lib/systemd/system/{blk-availability.service,lvm2-*}
}

package_lvm2() {
  depends=(device-mapper thin-provisioning-tools)
  backup=(etc/lvm/lvm.conf
          etc/lvm/lvmlocal.conf)

  cd $pkgbase

  make DESTDIR="$pkgdir" install_lvm2
  make DESTDIR="$pkgdir" install_systemd_units
  make DESTDIR="$pkgdir" install_systemd_generators

  rm -f "$pkgdir"/usr/lib/systemd/system/dm-*
}
