# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=harfbuzz
pkgver=8.0.1
pkgrel=9
epoch=
pkgdesc="A text shaping engine"
arch=('x86_64')
url="https://github.com/harfbuzz/harfbuzz"
license=('KZL')
depends=(cairo freetype2 graphite icu)
source=(git+https://github.com/harfbuzz/harfbuzz.git#tag=$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  CFLAGS="${CFLAGS/-fexceptions/}"
  CXXFLAGS="${CXXFLAGS/-fexceptions/}"

  meson setup \
    --prefix /usr \
    --optimization 2 \
    -D b_lto=true \
    -D b_pie=true \
    -D cairo=enabled \
    -D chafa=disabled \
    -D docs=disabled \
    -D freetype=enabled \
    -D glib=enabled \
    -D gobject=enabled \
    -D graphite=enabled \
    -D graphite2=enabled \
    -D icu=enabled \
    -D introspection=enabled \
    build

  meson compile -C build
}

check() {
  cd $pkgname

  mkdir -p "$srcdir"/tmp
  TMPDIR="$srcdir/tmp" meson test -C build
}

package() {
  cd $pkgname

  meson install -C build --destdir="$pkgdir"
}
