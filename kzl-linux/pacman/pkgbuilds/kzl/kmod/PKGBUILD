# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=kmod
pkgver=30.32
pkgrel=1
epoch=
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git"
license=('KZL')
source=(git+$url
        depmod-search.conf)
sha256sums=('SKIP'
            '1a92bfeae870f61ce814577e69d2a147a9c0caf6aed1131243e4179241fcc4a8')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;s/-g.*//;s/-/./g'
}

prepare() {
  cd $pkgname

  # autogen.sh
  touch libkmod/docs/gtk-doc.make
  autoreconf --force --install --symlink
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-manpages \
    --with-zstd \
    --with-xz \
    --with-zlib \
    --with-openssl

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -d -m 755 "$pkgdir"/usr/sbin
  for tool in modprobe modinfo insmod rmmod depmod lsmod; do
    ln -s ../bin/kmod "$pkgdir"/usr/sbin/$tool
  done

  install -d -m 755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d
  install -Dm644 "$srcdir"/depmod-search.conf "$pkgdir"/usr/lib/depmod.d/search.conf
}
