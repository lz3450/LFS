# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=elfutils
pkgver=0.189
pkgrel=6
epoch=
pkgdesc="A collection of utilities and libraries to read, create and modify ELF binary files, find and handle DWARF debug data, symbols, thread state and stacktraces for processes and core files on GNU/Linux"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('KZL')
# makedepends=(curl libarchive libmicrohttpd sqlite)
source=(git://sourceware.org/git/elfutils.git#tag=$pkgname-$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  # remove failing test
  # https://bugs.archlinux.org/task/74875
  sed -i -e 's/run-backtrace-native.sh //g' tests/Makefile.am
  sed -i -e 's/run-backtrace-native-core.sh //g' tests/Makefile.am

  autoreconf -vfi
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --program-prefix="eu-" \
    --enable-maintainer-mode \
    --disable-libdebuginfod \
    --disable-debuginfod

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
