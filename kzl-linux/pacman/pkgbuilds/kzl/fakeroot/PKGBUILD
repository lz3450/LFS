# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakeroot
pkgver=1.37.1.2
pkgrel=2
epoch=
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="https://packages.debian.org/fakeroot"
license=('KZL')
source=(
  git+https://salsa.debian.org/clint/fakeroot.git#tag=upstream/$pkgver
)
sha256sums=('41dda8c3dd030b9ee4d159e3f76150b3bef3ecac37ad89e4684b68e41c94159e')

prepare() {
  cd $pkgname

  rm -rf doc
  sed -i '/SUBDIRS=/s/doc //' Makefile.am
  sed -i '/doc\//d' configure.ac

  ./bootstrap
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --with-ipc=sysv

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
