# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=fakeroot
pkgver=1.36
pkgrel=1
epoch=
pkgdesc="Tool for simulating superuser privileges"
arch=('x86_64')
url="https://packages.debian.org/fakeroot"
license=('KZL')
source=(
  git+https://salsa.debian.org/clint/fakeroot.git#tag=upstream/$pkgver
)
sha256sums=('41895b87a7ae1625014fb7e4a6e43dccab7dcd5ce902b45455b679d17b3cd206')

prepare() {
  cd $pkgname

  rm -rf doc
  sed -i '/SUBDIRS=/s/doc //' Makefile.am
  sed -i '/doc\//d' configure.ac

  ./bootstrap
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --with-ipc=sysv

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
