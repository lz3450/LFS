# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=ghostscript
pkgver=10.01.2
pkgrel=10
epoch=
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://www.ghostscript.com/"
depends=(expat fontconfig freetype2 lcms2 libidn2 libjpeg-turbo libpaper libpng libtiff openjpeg)
source=(https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver//./}/ghostpdl-$pkgver.tar.xz)
sha256sums=('SKIP')

prepare() {
  cd ghostpdl-$pkgver

  NOCONFIGURE=1 ./autogen.sh

  rm -rf cups/libs expat freetype jpeg lcms2mt libpng openjpeg tiff zlib
}

build() {
  cd ghostpdl-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-cups \
    --disable-gtk \
    --disable-compile-inits \
    --without-tesseract \
    --with-system-libtiff \
    --without-ijs \
    --with-fontpath="/usr/share/fonts/gsfonts" \
    --without-versioned-path

  make so
}

package() {
  cd ghostpdl-$pkgver

  make DESTDIR="$pkgdir" soinstall

  rm -f "$pkgdir"/usr/bin/{lprsetup.sh,unix-lpr.sh}
  rm -r "$pkgdir"/usr/share/doc

  ln -s gsc "$pkgdir"/usr/bin/gs
}
