# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libpsl
pkgver=0.21.5
pkgrel=2
epoch=
pkgdesc="C library to handle the Public Suffix List"
arch=('x86_64')
url="https://github.com/rockdaboot/libpsl"
depends=(
  libidn2
)
source=(git+$url.git#tag=$pkgver)
sha256sums=('d2f564ed0b45737054bfadf285436310f50f102d7a437091abd6b3e378f4bf08')

prepare() {
  cd $pkgname

  git submodule update --init
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  meson setup \
    --prefix /usr \
    --auto-features disabled \
    --buildtype plain \
    -D builtin=true \
    -D docs=false \
    -D runtime=libidn2 \
    build

  msg2 "Compiling..."
  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  meson install -C build --destdir="$pkgdir"
}
