# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bzip2
pkgver=1.0.8
pkgrel=4
epoch=
pkgdesc="A high-quality data compression program"
arch=('x86_64')
url="https://sourceware.org/bzip2/index.html"
license=('KZL')
source=(
  git+git://sourceware.org/git/bzip2.git#tag=$pkgname-$pkgver
  $pkgname.pc.in
)
sha256sums=('a720711f84adceed2729cd23142537613fac924bf70ca58ef8bd7d28df6b7079'
            'c70901e311f37c4d33797204ae8ff92c998abb279dea5f6d9c02226f8d1a3b8d')

prepare() {
  cd $pkgname

  sed -i \
    -e "s/^CFLAGS=/CFLAGS=$CFLAGS $CPPFLAGS /" \
    -e '/^CFLAGS=/s/ -g//' \
    -e 's/$(PREFIX)\/man/$(PREFIX)\/share\/man/g' \
    -e 's|ln -s -f $(PREFIX)/bin/bzgrep|ln -s -f bzgrep|g' \
    -e 's|ln -s -f $(PREFIX)/bin/bzmore|ln -s -f bzgrep|g' \
    -e 's|ln -s -f $(PREFIX)/bin/bzdiff|ln -s -f bzgrep|g' \
    Makefile

  sed -i \
    -e "s/^CFLAGS=/CFLAGS=$CFLAGS $CPPFLAGS $LDFLAGS /" \
    -e '/^CFLAGS=/s/ -g//' \
    Makefile-libbz2_so
}

build() {
  cd $pkgname

  make -f Makefile-libbz2_so
  make
}

package() {
  cd $pkgname

  make PREFIX="$pkgdir/usr" install

  install -Dm755 libbz2.so.$pkgver "$pkgdir"/usr/lib
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so.${pkgver%.*}
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so.${pkgver%%.*}
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib/libbz2.so

  install -Dm644 "$srcdir"/$pkgname.pc.in "$pkgdir"/usr/lib/pkgconfig/$pkgname.pc
  sed -i -e "s|@VERSION@|$pkgver|" "$pkgdir"/usr/lib/pkgconfig/$pkgname.pc
}
