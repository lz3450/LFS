# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libcap-ng
pkgver=0.8.5
pkgrel=2
epoch=
pkgdesc="Library making programming with POSIX capabilities easier than traditional libcap"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/libcap-ng/"
license=('KZL')
makedepends=(
  libtool
  swig
)
source=(git+https://github.com/stevegrubb/libcap-ng.git#tag=v$pkgver)
sha256sums=('f08359cefc7f4e648db8f2641cccbddea5668495476ccd4a013191b705f85b56')

prepare() {
  cd libcap-ng

  ./autogen.sh
}

build() {
  cd libcap-ng

  ./configure \
    --prefix=/usr \
    --with-python=no \
    --with-python3=no

  make
}

check() {
  cd libcap-ng

  make check
}

package() {
  cd libcap-ng

  make DESTDIR="$pkgdir" install
}
