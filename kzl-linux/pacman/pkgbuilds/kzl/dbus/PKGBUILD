# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=dbus
pkgver=1.14.8
pkgrel=3
epoch=
pkgdesc="Freedesktop.org message bus system"
arch=('x86_64')
url="https://wiki.freedesktop.org/www/Software/dbus/"
license=('KZL')
depends=(audit expat)
source=(git+https://gitlab.freedesktop.org/dbus/dbus.git#tag=$pkgname-$pkgver
        dbus-reload.hook)
sha256sums=('SKIP'
            '40a68351784cff90591656db57e77c9a97840ebf9e7a63eb1ccd6538e9c090f7')

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --runstatedir=/run \
    --enable-static=no \
    --enable-asserts \
    --disable-xml-docs \
    --disable-doxygen-docs \
    --disable-ducktype-docs \
    --disable-selinux \
    --disable-apparmor \
    --enable-libaudit \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-dbus-user=dbus \
    --without-x

  # meson setup \
  #   --prefix /usr \
  #   --optimization 2 \
  #   -D asserts=true \
  #   -D dbus_user=dbus \
  #   -D doxygen_docs=false \
  #   -D ducktype_docs=false \
  #   -D inotify=true \
  #   -D libaudit=true \
  #   -D qt_help=false \
  #   -D runtime_dir=/run \
  #   -D selinux=false \
  #   -D system_socket=/run/dbus/system_bus_socket \
  #   -D systemd=true \
  #   -D user_session=true \
  #   -D x11_autolaunch=false \
  #   -D xml_docs=false \
  #   build

  make
  # meson compile -C build
}

check() {
  cd $pkgname

  make check
  # meson test -C build
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  # meson install -C build --destdir="$pkgdir/"

  rm -r "$pkgdir"/{etc,var}

  echo 'u dbus 81 "System Message Bus"' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/dbus.conf

  install -Dm644 "$srcdir"/dbus-reload.hook -t "$pkgdir"/usr/share/libalpm/hooks
}
