# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=readline
pkgver=8.2.g13
pkgrel=3
epoch=
pkgdesc="GNU readline library"
arch=('x86_64')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
license=('KZL')
depends=(
  ncurses
)
backup=(
  etc/inputrc
)
options=(!emptydirs)
source=(
  git+https://git.savannah.gnu.org/git/readline.git#branch=master
  inputrc
)
sha256sums=('SKIP'
            '03229dcde6a4492bd44b76d05b2b30cfec27787d41fe57cc2ee9e3b656165ebe')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/readline-//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  ./configure -C \
    --prefix=/usr \
    --disable-install-examples \
    --with-curses
}

build() {
  cd $pkgname

  make SHLIB_LIBS="-lncursesw"

}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/inputrc "$pkgdir"/etc/inputrc
}
