# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=shadow
pkgver=4.17.4
pkgrel=3
epoch=
pkgdesc="Programs for converting UNIX password files to the shadow password format, plus programs for managing user and group accounts"
arch=('x86_64')
url="https://github.com/shadow-maint/shadow"
depends=(
  acl
  attr
  audit
  glibc
  libxcrypt
  pam
  systemd
)
makedepends=(
  libtool
)
backup=(
  etc/default/useradd
  etc/login.defs
  etc/pam.d/{chage,chfn,chgpasswd,chpasswd,chsh,groupadd,groupdel,groupmems,groupmod,login,newusers,passwd,useradd,userdel,usermod}
)
install=$pkgname.install
source=(
  git+https://github.com/shadow-maint/shadow.git#tag=$pkgver
  useradd.defaults
  shadow.{service,timer}
  $pkgname.install
)
sha256sums=('ef2340e422c16d25d44ecfd01d74e333b667c0c15f68ed61849aa5711dd30fd9'
            'bc9fef974321e399fb47c229a82e37d81a8f39424082ef8e4915d0dbee9ff7c2'
            'aa10a39f3c004cd805313dadc196bef730c9caae652dcdd2652019d3ecff49c4'
            '736eb7b16fc2fff9c902d4823641114338f290a6825e66831f41c5474e994207'
            'ad57255fdcff5023040a80dfca89cf3939d3cff77f79709cb97ae854d6e2d914')

prepare() {
  cd $pkgname

  # login.defs
  sed -i -E \
      -e '/^MOTD_FILE/c\MOTD_FILE' \
      -e '/^#ISSUE_FILE/c\ISSUE_FILE' \
      -e '/^ENV_SUPATH/c\ENV_SUPATH\tPATH=/usr/local/bin:/usr/local/sbin:/usr/bin:/usr/sbin:/bin:/sbin' \
      -e '/^ENV_PATH/c\ENV_PATH\tPATH=/usr/local/bin:/usr/local/sbin:/usr/bin:/usr/sbin:/bin:/sbin' \
      -e '/^PASS_MIN_LEN/c\PASS_MIN_LEN\t4' \
      -e '/^#ENCRYPT_METHOD/c\ENCRYPT_METHOD\tSHA512' \
      etc/login.defs

  autoreconf -vfi
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --enable-shadowgrp=yes \
    --enable-account-tools-setuid \
    --enable-lastlog=no \
    --enable-logind=no \
    --with-audit=yes \
    --with-libpam=yes \
    --with-btrfs=no \
    --with-selinux=no \
    --with-acl=yes \
    --with-attr=yes \
    --with-sha-crypt=yes \
    --with-bcrypt=no \
    --with-yescrypt=yes \
    --with-nscd=no \
    --with-sssd=no \
    --with-group-name-max-length=32 \
    --with-su=no \
    --with-libbsd=no \
    --with-fcaps=no

  msg2 "Compiling..."
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  # useradd defaults
  install -Dm600 "$srcdir"/useradd.defaults "$pkgdir"/etc/default/useradd

  # systemd units
  install -Dm644 "$srcdir"/shadow.timer "$pkgdir"/usr/lib/systemd/system/shadow.timer
  install -Dm644 "$srcdir"/shadow.service "$pkgdir"/usr/lib/systemd/system/shadow.service

  # cleanup
  for i in chfn chsh expiry faillog login logoutd nologin sg su vigr vipw; do
    find "$pkgdir" -name "$i*" -delete
  done
}
