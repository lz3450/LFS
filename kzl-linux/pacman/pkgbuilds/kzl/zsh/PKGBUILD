# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=zsh
pkgver=5.9
pkgrel=7
epoch=
arch=('x86_64')
url="https://www.zsh.org/"
license=('KZL')
depends=(libcap pcre)
install=$pkgname.install
source=(git://git.code.sf.net/p/zsh/code#tag=$pkgname-$pkgver
        zprofile)
sha256sums=('SKIP'
            '230832038c3b8f67fdb1b284ac5f68d709cdb7f1bc752b0e60657b9b9d091045')

prepare() {
  cd code

	sed -e 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' -i Completion/Unix/Command/_loadkeys
	sed -e 's#/usr/share/misc/usb.ids#/usr/share/hwdata/usb.ids#g' -i Completion/Linux/Command/_lsusb

	# Remove unneeded and conflicting completion scripts
	for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
		rm -rf Completion/$_fpath
		sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
	done
	rm -f Completion/Linux/Command/_pkgtool

  autoreconf -vfi
}

build() {
  cd code

  ./configure \
    --prefix=/usr \
    --enable-etcdir=/etc/zsh \
    --enable-zshenv=/etc/zsh/zshenv \
    --enable-zshrc=/etc/zsh/zshrc \
    --enable-zprofile=/etc/zsh/zprofile \
    --enable-zlogin=/etc/zsh/zlogin \
    --enable-zlogout=/etc/zsh/zlogout \
    --enable-fndir=/usr/share/zsh/functions \
    --enable-function-subdirs \
    --enable-scriptdir=/usr/share/zsh/scripts \
    --enable-maildir-support \
    --enable-pcre \
    --enable-cap \
    --enable-multibyte \
    --with-term-lib='ncursesw' \
    --with-tcsetpgrp

  make
}

check() {
  cd code

  make check
}

package() {
  cd code

  # make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install.bin
  make DESTDIR="$pkgdir" install.modules
  # make DESTDIR="$pkgdir" install.man
  make DESTDIR="$pkgdir" install.fns
  # make DESTDIR="$pkgdir" install.info
  install -D -m644 "$srcdir"/zprofile "$pkgdir"/etc/zsh/zprofile
}
