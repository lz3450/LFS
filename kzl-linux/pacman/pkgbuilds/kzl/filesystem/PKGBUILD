# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=filesystem
pkgver=2025.05.08
pkgrel=1
epoch=
pkgdesc="Root file system"
arch=('any')
license=('KZL')
depends=(iana-etc)
backup=(
  etc/crypttab
  etc/fstab
  etc/group
  etc/gshadow
  etc/host.conf
  etc/hosts
  etc/locale.conf
  etc/motd
  etc/passwd
  etc/profile
  etc/resolv.conf
  etc/shadow
  etc/shells
  etc/subgid
  etc/subuid
)
source=(
  https://src.fedoraproject.org/rpms/setup/raw/rawhide/f/generate-sysusers-fragments.sh
  crypttab
  fstab
  https://src.fedoraproject.org/rpms/setup/raw/rawhide/f/group
  host.conf
  locale.conf
  hosts
  motd
  https://src.fedoraproject.org/rpms/setup/raw/rawhide/f/passwd
  https://src.fedoraproject.org/rpms/setup/raw/rawhide/f/profile
  resolv.conf
  shells
  subgid
  subuid
)
sha256sums=('95e89d92e3a99e86c3cad760564e871e488d9aea6adfd2acaecb3cd0b4b75759'
            'a6f0ad7acc6054199fbab680a53769a936a4518a2ee751404839b77fb2a4be79'
            '63ea61478aeaab0640ac76d915e94ddf3a0c8a661ee1188c8365536141c7afc2'
            '12e361821edc8bb813b7c38b11f61aa39c262e6b35bb2462896cb51edc821e2d'
            '4d7b647169063dfedbff5e1e22cee77bd1a4183dbcfd5e802e68939da4bbf733'
            '075066b9785889daa04db68caba1f67d1dca283898b225254c121544437f8821'
            'ac629be54947f1796855a26abd9607797d0c62cb27a71ac33da51122121df4e8'
            '404c3f2e1d3d4097cedd08a4484119cb37d9adfc0ead49bdf5960e227e9159de'
            '2484582c0044ccbe58601ce437cf51b672de4b6ea743ab4d4223ddf2ce0afb68'
            '5d2cb175897062d889c4b2f210a3514fab06abc2aeb8dbb545b1236c58ac37a8'
            '5557d8e601b17a80d1ea7de78a9869be69637cb6a02fbfe334e22fdf64e61d4c'
            'dd8d9d43fbe6925149c1635728e6570168ffc91fb8fabeb46fb4f6f115f389d5'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855')

# Reference:
# https://src.fedoraproject.org/rpms/setup/blob/rawhide/f/setup.spec

prepare() {
  sed -i 's|-setup-|-basic-|g;s|etc/||g;s|sysusers.d/||g' generate-sysusers-fragments.sh
}

build() {
  # /etc/shadow
  sed -e "s/:.*/:*:`expr $(date +%s) / 86400`:0:99999:7:::/" passwd >shadow
  # /etc/gshadow
  sed -e 's/:[0-9]\+:/::/g; s/:x:/::/' group >gshadow

  bash generate-sysusers-fragments.sh
}

package() {
  cd $pkgdir

  # root filesystem
  install -dm 755 {boot,dev,etc,home,mnt,usr,var,opt,srv,run}
  install -dm 750 root
  install -dm 555 {proc,sys}
  install -dm 1777 tmp

  # /etc
  install -d -m 755 etc/{skel,profile.d}
  for f in fstab group host.conf locale.conf hosts motd passwd profile resolv.conf shells subgid subuid; do
    install -D -m 644 "$srcdir/$f" etc/
  done
  for f in crypttab gshadow shadow; do
    install -D -m 600 "$srcdir/$f" etc/
  done
  install -d -m 755 etc/motd.d
  ln -s ../proc/self/mounts etc/mtab

  # /usr
  install -d -m 755 usr/{bin,sbin,include,lib,libexec,share,src}
  install -d -m 755 usr/share/man/man{1..8}
  ln -s usr/lib lib
  ln -s usr/lib lib64
  ln -s lib usr/lib64
  ln -s usr/bin bin
  ln -s usr/sbin sbin

  # /usr/local
  install -d -m 755 usr/local/{bin,sbin,etc,include,lib,libexec,share,src}

  # /var
  install -d -m 755 var/{cache,lib,local,log,mail,opt,spool,www}
  install -d -m 1777 var/tmp
  ln -s ../mail var/spool/mail

  # setup default system groups/users
  install -d -m 755 usr/lib/sysusers.d
  install -D -m 644 "$srcdir"/20-basic-groups.conf usr/lib/sysusers.d/
  install -D -m 644 "$srcdir"/20-basic-users.conf usr/lib/sysusers.d/

  # tmpfiles for motd
  install -d -m 755 usr/lib/tmpfiles.d
  echo "f /run/motd 0644 root root -" > usr/lib/tmpfiles.d/motd.conf
  echo "d /run/motd.d 0755 root root -" >> usr/lib/tmpfiles.d/motd.conf
}
