# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libarchive
pkgver=3.7.9
pkgrel=4
epoch=
pkgdesc="A portable, efficient C library that can read and write streaming archives in a variety of formats"
arch=('x86_64')
url="https://libarchive.org/"
license=('KZL')
depends=(
  acl
  attr
  bzip2
  libxml2
  lz4
  openssl
  xz
  zlib
  zstd
)
source=(git+https://github.com/libarchive/libarchive.git#tag=v$pkgver)
sha256sums=('564839b9c4ecae3d87ea0e3b6a9af1e5e26774c8d5dbb2f465480873a7dc0ef8')

build() {
  cd $pkgname

  msg2 "Configuring..."
  cmake -LAH -S . -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -D ENABLE_ACL=ON \
    -D ENABLE_BZip2=ON \
    -D ENABLE_LIBXML2=ON \
    -D ENABLE_LZ4=ON \
    -D ENABLE_LZMA=ON \
    -D ENABLE_LZO=OFF \
    -D ENABLE_NETTLE=OFF \
    -D ENABLE_OPENSSL=ON \
    -D ENABLE_TAR=ON \
    -D ENABLE_UNZIP=ON \
    -D ENABLE_XATTR=ON \
    -D ENABLE_ZLIB=ON \
    -D ENABLE_ZSTD=ON

  msg2 "Compiling..."
  cmake --build build
}

check() {
  cd $pkgname

  cmake --build build --target tests
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" cmake --build build --target install
}
