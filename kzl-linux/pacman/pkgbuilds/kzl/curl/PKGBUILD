# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=curl
pkgver=8.13.0
pkgrel=1
epoch=
pkgdesc="A command-line tool for transferring data specified with URL syntax"
arch=('x86_64')
url="https://curl.se/"
license=('KZL')
depends=(
  libidn2
  libpsl
  libssh2
  nghttp2
  openssl
  zlib
  zstd
)
source=(
  git+https://github.com/curl/curl.git#tag=curl-${pkgver//./_}
)
sha256sums=('cf0be1fdf3caef407bfe30e45d7cd54a0afe929ec2cfd91b262ce7195544c747')

prepare() {
  cd $pkgname

  sed -i "/\WLIBCURL_VERSION\W/s/-DEV//" include/curl/curlver.h
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  cmake -LAH -S . -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -D BUILD_EXAMPLES=OFF \
    -D BUILD_LIBCURL_DOCS=OFF \
    -D BUILD_MISC_DOCS=OFF \
    -D BUILD_SHARED_LIBS=ON \
    -D BUILD_STATIC_LIBS=ON \
    -D BUILD_TESTING=OFF \
    -D CURL_BROTLI=OFF \
    -D CURL_CA_PATH=/etc/pki/tls/certs \
    -D CURL_DISABLE_KERBEROS_AUTH=ON \
    -D CURL_DISABLE_LDAP=ON \
    -D CURL_DISABLE_LDAPS=ON \
    -D CURL_LTO=ON \
    -D CURL_USE_LIBPSL=ON \
    -D CURL_USE_LIBSSH2=ON \
    -D CURL_USE_OPENSSL=ON \
    -D CURL_USE_PKGCONFIG=ON \
    -D CURL_ZLIB=ON \
    -D CURL_ZSTD=ON \
    -D ENABLE_ARES=OFF \
    -D ENABLE_CURL_MANUAL=OFF \
    -D USE_LIBIDN2=ON \
    -D USE_NGHTTP2=ON

  msg2 "Compiling..."
  cmake --build build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" cmake --build build --target install
}
