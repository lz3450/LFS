# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=python
pkgver=3.12.1
pkgrel=2
epoch=
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
url="https://www.python.org/"
license=('KZL')
depends=(
  expat
  gdbm
  glibc
  libedit
  libffi
  libnsl
  libxcrypt
)
makedepends=(
  sqlite
  # tk
)
source=(git+https://github.com/python/cpython.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd cpython

  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -fno-semantic-interposition"

  ./configure \
    --prefix=/usr \
    --enable-shared=yes \
    --disable-gil=no \
    --enable-experimental-jit=no \
    --enable-optimizations=yes \
    --enable-bolt=no \
    --enable-loadable-sqlite-extensions=yes \
    --enable-ipv6=yes \
    --enable-big-digits=30 \
    --with-pkg-config=yes \
    --with-lto=yes \
    --with-tzpath=/usr/share/zoneinfo \
    --with-system-expat=yes \
    --with-system-libmpdec=no \
    --with-decimal-contextvar=yes \
    --with-dbmliborder=gdbm:ndbm \
    --with-doc-strings=yes \
    --with-readline=editline \
    --with-ensurepip=no

  make
}

package() {
  cd cpython

  make DESTDIR="$pkgdir" install

  ln -s python${pkgver%.*} "$pkgdir"/usr/bin/python

  rm -vr "$pkgdir/usr/lib/python${pkgver%.*}"/{test,idlelib/idle_test}
}
