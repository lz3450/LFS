# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=coreutils
pkgver=9.2
pkgrel=4
epoch=
pkgdesc="The GNU core utilities"
arch=('x86_64')
url="https://www.gnu.org/software/coreutils/"
license=('KZL')
depends=(acl attr glibc gmp libcap openssl)
source=(git+https://github.com/coreutils/coreutils.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  ./configure  \
    --prefix=/usr \
    --enable-install-program=arch,hostname \
    --enable-no-install-program=groups,kill,uptime \
    --with-linux-crypto \
    --with-openssl=no \
    --without-selinux

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
