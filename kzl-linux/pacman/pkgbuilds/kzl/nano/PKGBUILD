# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=nano
pkgver=7.2
pkgrel=7
epoch=
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
url="https://www.nano-editor.org"
license=('KZL')
backup=(etc/nanorc)
source=(git://git.savannah.gnu.org/nano.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  ln -s ../gnulib
  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-color \
    --enable-nanorc \
    --enable-multibuffer

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname/doc/sample.nanorc "$pkgdir"/etc/nanorc
}
