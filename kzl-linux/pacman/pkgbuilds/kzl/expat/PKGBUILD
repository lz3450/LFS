# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=expat
pkgver=2.7.1
pkgrel=1
epoch=
pkgdesc="An XML parser library"
arch=('x86_64')
url="https://libexpat.github.io/"
license=('KZL')
source=(
  git+https://github.com/libexpat/libexpat.git#tag=R_${pkgver//./_}
)
sha256sums=('e8de176a49c83e6cdeed9b9237eb5a0f8c73e68ac0d45cfb0798c6747d1961c8')

build() {
  cd libexpat/$pkgname

  msg2 "Configuring..."
  cmake -LAH -S . -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS"

  msg2 "Compiling..."
  cmake --build build
}

check() {
  cd libexpat/$pkgname

  cmake --build build --target tests
}

package() {
  cd libexpat/$pkgname

  DESTDIR="$pkgdir" cmake --build build --target install
}
