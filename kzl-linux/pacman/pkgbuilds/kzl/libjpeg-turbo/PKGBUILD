# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libjpeg-turbo
pkgver=2.1.5.1
pkgrel=4
epoch=
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('KZL')
makedepends=(nasm)
provides=(libjpeg)
source=(git+https://github.com/libjpeg-turbo/libjpeg-turbo.git#tag=$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DWITH_JAVA=OFF \
    -DWITH_JPEG8=ON \
    -S . \
    -B build \
    -W no-dev \
    -G Ninja \
    -LAH

  ninja -C build
}

check() {
  cd $pkgname

  ninja -C build test
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ninja -C build install
}
