# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=gcc
pkgver=15.1.0.g8
pkgrel=4
epoch=
pkgdesc="The GNU Compiler Collection"
arch=('x86_64')
url="https://gcc.gnu.org"
license=('KZL')
depends=(
  binutils
  glibc
  isl
  mpc
  zlib
  zstd
)
# checkdepends=(
#   dejagnu
#   expect
#   inetutils
#   python
# )
options=(
  !emptydirs
  !lto
)
source=(
  # git+git://gcc.gnu.org/git/gcc.git#tag=releases/gcc-$pkgver
  git+git://gcc.gnu.org/git/gcc.git#branch=releases/gcc-${pkgver%%.*}
)

sha256sums=('SKIP')

pkgver() {
  cd gcc

  git describe --tags | sed 's|releases/gcc-||;s/-g.*//;s/-/.g/'
}

prepare() {
  cd gcc

  rm -r zlib
  sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  mkdir -p build
}

build() {
  cd gcc/build

  msg2 "Configuring..."
  # https://gcc.gnu.org/install/configure.html
  "../configure" -C \
    --with-pkgversion="KZL $pkgver-$pkgrel `date -u '+%Y/%m/%d %T %z'`" \
    --prefix=/usr \
    --build=$CHOST \
    --host=$CHOST \
    --target=$CHOST \
    --enable-default-compressed-debug-sections-algorithm=zstd \
    --enable-host-shared \
    --disable-multilib \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --enable-link-serialization \
    --enable-linker-build-id \
    --enable-gnu-indirect-function \
    --enable-languages=c,c++,fortran,lto,jit \
    --disable-libssp \
    --enable-default-pie \
    --enable-default-ssp \
    --enable-checking=release \
    --enable-linker-build-id \
    --with-linker-hash-style=gnu \
    --enable-gnu-unique-object \
    --enable-lto \
    --enable-cet \
    --with-zstd \
    --with-target-system-zlib \
    --enable-plugin

  msg2 "Compiling..."
  make
}

check() {
  cd gcc/build

  make check || warning "Check failed."
}

package() {
  cd gcc/build

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/lib/bfd-plugins/
  ln -s /usr/libexec/gcc/$CHOST/$(cat "$srcdir"/gcc/BASE-VER)/liblto_plugin.so "$pkgdir"/usr/lib/bfd-plugins/

  ln -s gcc "$pkgdir"/usr/bin/cc
}
