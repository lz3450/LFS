# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=librsvg
pkgver=2.54.0
pkgrel=1
epoch=3
pkgdesc="SVG rendering library"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/LibRsvg"
license=('KZL')
depends=(cairo freetype2 gdk-pixbuf glib2 harfbuzz libxml2 pango)
makedepends=(gobject-introspection rust)
checkdepends=(dejavu-fonts)
source=(git+https://gitlab.gnome.org/GNOME/librsvg.git#tag=$pkgver)
sha256sums=('SKIP')

# python: gi-docgen

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-g.*//'
}

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-vala

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -i libtool

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
