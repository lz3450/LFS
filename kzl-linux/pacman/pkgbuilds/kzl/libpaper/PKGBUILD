# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libpaper
pkgver=2.1.1
pkgrel=3
epoch=
pkgdesc="Library for handling paper characteristics"
arch=('x86_64')
url="https://github.com/rrthomas/libpaper"
license=('KZL')
backup=(etc/papersize)
source=(git+https://github.com/rrthomas/libpaper.git#tag=v$pkgver
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/etc/
  install -dm755 "$pkgdir"/etc/libpaper.d/
  echo '# Simply write the paper name. See papersize(5) for possible values' > "$pkgdir"/etc/papersize
}
