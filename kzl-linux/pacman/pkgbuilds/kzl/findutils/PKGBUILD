# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=findutils
pkgver=4.9.0.32
pkgrel=2
epoch=
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
url="https://www.gnu.org/software/findutils/"
license=('KZL')
depends=(glibc)
source=(git+https://git.savannah.gnu.org/git/findutils.git
        git+https://git.savannah.gnu.org/git/gnulib.git)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed "s/^v//;s/-g.*//;s/-/./"
}

prepare() {
  cd $pkgname

  GNULIB_REFDIR="$srcdir"/gnulib ./bootstrap
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/bin/{locate,updatedb}
  rm "$pkgdir"/usr/share/man/man1/{locate,updatedb}.1
  rm -r "$pkgdir"/usr/share/man/man5
  rm -r "$pkgdir"/usr/var
}
