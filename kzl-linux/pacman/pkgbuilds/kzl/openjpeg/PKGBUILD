# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openjpeg
pkgver=2.5.0
pkgrel=4
epoch=
pkgdesc="An open-source JPEG 2000 codec written in C language"
arch=('x86_64')
url="https://github.com/uclouvain/openjpeg"
depends=(lcms2 libpng libtiff)
source=(git+https://github.com/uclouvain/openjpeg.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd $pkgname

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_DOC=OFF \
    -S . \
    -B build \
    -G Ninja

  cmake --build build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" cmake --build build --target install
}
