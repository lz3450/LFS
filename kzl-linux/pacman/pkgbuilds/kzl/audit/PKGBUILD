# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=audit
pkgver=4.0.3
pkgrel=4
epoch=
pkgdesc="User space components of the audit framework"
arch=('x86_64')
url="https://people.redhat.com/sgrubb/audit"
license=('KZL')
depends=(
  libcap-ng
)
makedepends=(
  libtool
  swig
)
backup=(
  etc/libaudit.conf
  etc/audit/audisp-remote.conf
  etc/audit/auditd.conf
  etc/audit/audit-stop.rules
  etc/audit/plugins.d/af_unix.conf
  etc/audit/plugins.d/au-remote.conf
  etc/audit/plugins.d/syslog.conf
)
source=(git+https://github.com/linux-audit/audit-userspace.git#tag=v$pkgver)
sha256sums=('1d0978d6cc2e9a7809de4d87cfa1fdc2277417de17df915bdb8bac1564fb3515')

prepare() {
  cd audit-userspace

  ./autogen.sh
}

build() {
  cd audit-userspace

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-io_uring \
    --with-libcap-ng=yes

  make
}

package() {
  cd audit-userspace

  make DESTDIR="$pkgdir" install

  install -dm0700 "$pkgdir"/var/log/audit

  rm -rf \
    "$pkgdir"/etc/rc.d \
    "$pkgdir"/etc/sysconfig

  chmod 644 "$pkgdir"/usr/lib/systemd/system/auditd.service
}
