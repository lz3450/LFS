# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=guile
pkgver=3.0.9
pkgrel=8
epoch=
pkgdesc="Portable, embeddable Scheme implementation written in C"
arch=('x86_64')
url="https://www.gnu.org/software/guile/"
license=('KZL')
depends=(gc gmp libffi libunistring)
source=(git://git.savannah.gnu.org/guile.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-3.?.so.*-gdb.scm
}
