# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgbase=linux
pkgname=(linux linux-headers)
pkgver=6.1.46
pkgrel=1
epoch=
pkgdesc="LTS Linux Kernel"
arch=('x86_64')
url="https://www.kernel.org"
license=('KZL')
makedepends=(bc pahole rsync)
options=(!strip)
source=(git+https://git.kernel.org/pub/scm/linux/kernel/git/stable/linux.git#branch=linux-${pkgver%.*}.y
        config)
sha256sums=('SKIP'
            '3df325f8b328c02ccc1b1032de9be4d15e95e3548e6bab5731306c3714b65b60')

export KBUILD_BUILD_HOST=kzl-linux
export KBUILD_BUILD_USER=kzl
export KBUILD_BUILD_TIMESTAMP="$(date -u '+%Y/%m/%d %T %z')"

pkgver() {
  cd $pkgname

  git describe --tags | sed -e 's/^v//'
}

prepare() {
  cd $pkgbase

  mkdir -p build

  cp "$srcdir"/config build/.config
}

build() {
  cd $pkgbase

  make O=build all
}

package_linux() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)
  local image_name=$(make O=build -s image_name)
  local modulesdir="/usr/lib/modules/$kernelrelease"

  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 O=build modules_install

  install -d -m 755 $pkgdir/boot/
  # make INSTALL_PATH="$pkgdir/boot" O=build install
  install -Dm755 build/$image_name "$pkgdir/$modulesdir"/vmlinuz
  install -Dm755 build/$image_name "$pkgdir"/boot/vmlinuz
  # install -Dm644 build/System.map -t "$pkgdir/$modulesdir/"
  # install -Dm644 build/System.map -t "$pkgdir"/boot/

  rm "$pkgdir"/usr/lib/modules/$kernelrelease/{build,source}
  ln -sf /usr/src/linux-headers-$kernelrelease "$pkgdir"/usr/lib/modules/$kernelrelease/source
  ln -sf /usr/src/linux-headers-$kernelrelease/build "$pkgdir"/usr/lib/modules/$kernelrelease/build
}

package_linux-headers() {
  cd $pkgbase

  local kernelrelease=$(make O=build -s kernelrelease)

  install -d -m 0755 "$pkgdir"/usr/src/
  rsync -a \
    --exclude='*.o' \
    --exclude='*.ko*' \
    --exclude='*.a' \
    --exclude='*.o.cmd' \
    --exclude='*.o.d' \
    --exclude='*.so.dbg' \
    --exclude='.tmp_vmlinux.btf' \
    --exclude='.git*' \
    --exclude='.clang-format' \
    --exclude='.cocciconfig' \
    --exclude='.get_maintainer.ignore' \
    --exclude='.mailmap' \
    --exclude='.rustfmt.toml' \
    "$srcdir/$pkgbase"/ "$pkgdir"/usr/src/linux-headers-$kernelrelease
}
