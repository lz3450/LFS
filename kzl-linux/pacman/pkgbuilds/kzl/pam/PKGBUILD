# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pam
pkgver=1.7.0
pkgrel=5
epoch=
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
url="https://github.com/linux-pam/linux-pam"
license=('KZL')
depends=(
  audit
  gdbm
  libxcrypt
  openssl
  pam-config
)
source=(git+https://github.com/linux-pam/linux-pam.git#tag=v$pkgver)
sha256sums=('66ba7e8d6f8d1b985432a07180280e5bbc8c84bfc43fab7b1e071c26a04e2bde')

build() {
  cd linux-pam

  msg2 "Configuring..."
  meson setup \
    --prefix /usr \
    --auto-features disabled \
    --buildtype plain \
    -D audit=enabled \
    -D db=gdbm \
    -D docs=disabled \
    -D econf=disabled \
    -D i18n=enabled \
    -D logind=disabled \
    -D nis=disabled \
    -D openssl=enabled \
    -D pam_lastlog=enabled \
    -D pam_unix=enabled \
    -D pam_userdb=enabled \
    -D selinux=disabled \
    build

  msg2 "Compiling..."
  meson compile -C build
}

check() {
  cd linux-pam

  meson test -C build --print-errorlogs
}

package() {
  cd linux-pam

  meson install -C build --destdir "$pkgdir"
}
