# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=less
pkgver=629
pkgrel=1
epoch=
pkgdesc="A terminal based program for viewing text files"
arch=('x86_64')
url="http://www.greenwoodsoftware.com/less"
license=('KZL')
depends=(ncurses pcre2)
source=(git+https://github.com/gwsw/less.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  make -f Makefile.aut distfiles
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-regex=pcre2

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
