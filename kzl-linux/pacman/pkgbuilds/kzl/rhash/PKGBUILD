# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rhash
pkgver=1.4.5
pkgrel=4
epoch=
pkgdesc="Utility for verifying hash sums of files"
arch=('x86_64')
url="https://github.com/rhash/RHash"
license=('KZL')
depends=(
  openssl
)
backup=(
  etc/rhashrc
)
source=(git+https://github.com/rhash/RHash.git#tag=v$pkgver)
sha256sums=('SKIP')

build() {
  cd RHash

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gettext \
    --enable-openssl=runtime \
    --enable-lib-static \
    --enable-lib-shared \
    --extra-cflags="$CFLAGS $CPPFLAGS" \
    --extra-ldflags="$LDFLAGS"

  msg2 "Compiling..."
  make
}

check() {
  cd RHash

  make test test-lib
}

package() {
  cd RHash

  make DESTDIR="$pkgdir" install install-lib-so-link
}
