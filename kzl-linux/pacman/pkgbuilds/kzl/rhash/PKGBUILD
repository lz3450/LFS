# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rhash
pkgver=1.4.6
pkgrel=1
epoch=
pkgdesc="Utility for verifying hash sums of files"
arch=('x86_64')
url="https://github.com/rhash/RHash"
license=('KZL')
depends=(
  openssl
)
backup=(
  etc/rhashrc
)
source=(git+https://github.com/rhash/RHash.git#tag=v$pkgver)
sha256sums=('45de8f49566658a2aff2d901e5024b19256ecc88d7276a82b2b9949e8e87d32d')

build() {
  cd RHash

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gettext \
    --enable-openssl=runtime \
    --enable-lib-static \
    --enable-lib-shared \
    --extra-cflags="$CFLAGS $CPPFLAGS" \
    --extra-ldflags="$LDFLAGS"

  msg2 "Compiling..."
  make
}

check() {
  cd RHash

  make test test-lib
}

package() {
  cd RHash

  make DESTDIR="$pkgdir" install install-lib-so-link
}
