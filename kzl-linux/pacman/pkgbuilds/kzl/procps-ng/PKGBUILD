# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=procps-ng
pkgver=4.0.3
pkgrel=9
epoch=
pkgdesc="Utilities for monitoring your system and its processes"
arch=('x86_64')
url="https://gitlab.com/procps-ng/procps"
license=('KZL')
depends=(glibc ncurses)
makedepends=(systemd)
source=(git+https://gitlab.com/procps-ng/procps.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd procps

  ./autogen.sh

  sed 's:<ncursesw/:<:g' -i src/watch.c
}

build() {
  cd procps

  ./configure \
    --prefix=/usr \
    --disable-nls \
    --enable-watch8bit \
    --disable-kill \
    --with-systemd \
    --disable-modern-top

  make
}

check() {
  cd procps

  make check
}

package() {
  cd procps

  make DESTDIR="$pkgdir" install
}
