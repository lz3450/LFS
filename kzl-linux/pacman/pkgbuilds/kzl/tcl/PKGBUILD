# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=tcl
pkgver=8.6.13
pkgrel=6
epoch=
pkgdesc="The Tcl scripting language"
arch=('x86_64')
url="https://core.tcl-lang.org/index.html"
license=('KZL')
source=(https://downloads.sourceforge.net/sourceforge/tcl/$pkgname$pkgver-src.tar.gz)
sha256sums=('43a1fae7412f61ff11de2cfd05d28cfc3a73762f354a417c62370a54e2caf066')

prepare() {
  cd $pkgname$pkgver/pkgs

  rm -r sqlite*
}

build() {
  cd $pkgname$pkgver/unix

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-threads \
    --enable-64bit

  make
}

check() {
  cd $pkgname$pkgver/unix

  make test
}

package() {
  cd $pkgname$pkgver/unix

  make DESTDIR="$pkgdir" install install-private-headers

  ln -sf tclsh${pkgver%.*} "$pkgdir"/usr/bin/tclsh
  ln -sf libtcl${pkgver%.*}.so "$pkgdir"/usr/lib/libtcl.so
  install -Dm644 tcl.m4 -t "$pkgdir"/usr/share/aclocal

  sed -e "s#$srcdir/$pkgname$pkgver/unix#/usr/lib#" \
      -e "s#$srcdir/$pkgname$pkgver#/usr/include#" \
      -e "s#'{/usr/lib} '#'/usr/lib/$pkgname${pkgver%.*}'#" \
      -i "$pkgdir"/usr/lib/tclConfig.sh

  tdbcver=`ls $srcdir/$pkgname$pkgver/pkgs | grep -oP 'tdbc\d\.\d\.\d'`
  sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/$tdbcver#/usr/lib/$tdbcver#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver/generic#/usr/include#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver/library#/usr/lib/$pkgname${pkgver%.*}#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$tdbcver#/usr/include#" \
      -i "$pkgdir"/usr/lib/$tdbcver/tdbcConfig.sh

  itclver=`ls $srcdir/$pkgname$pkgver/pkgs | grep -oP 'itcl\d\.\d\.\d'`
  sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/$itclver#/usr/lib/$itclver#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$itclver/generic#/usr/include#" \
      -e "s#$srcdir/$pkgname$pkgver/pkgs/$itclver#/usr/include#" \
      -i "$pkgdir"/usr/lib/$itclver/itclConfig.sh
}
