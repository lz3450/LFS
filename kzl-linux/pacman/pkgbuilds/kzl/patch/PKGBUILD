# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=patch
pkgver=2.8.g4
pkgrel=2
epoch=
pkgdesc="A utility to apply patch files to original sources"
arch=('x86_64')
url="https://www.gnu.org/software/patch/"
license=('KZL')
groups=(base-devel)
depends=(
  ed
)
makedepends=(
  autoconf
  automake
  bison
  diffutils
)
source=(
  git+git://git.savannah.gnu.org/patch.git#branch=master
  git+https://git.savannah.gnu.org/git/gnulib.git
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/^v//;;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib
}

build() {
  cd $pkgname

  msg2 "Configuring..."
  ./configure \
    --prefix=/usr

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}
