# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=systemd
pkgver=254.1
pkgrel=3
epoch=
arch=('x86_64')
url="https://github.com/systemd/systemd-stable"
license=('KZL')
depends=(
  acl
  audit
  bzip2
  cryptsetup
  dbus
  elfutils
  glibc
  iptables
  kbd
  kmod
  libcap
  libgcrypt
  libidn2
  libseccomp
  lz4
  openssl
  pam
  pcre2
  xz
  zlib
  zstd
)
makedepends=(curl kexec-tools gnutls gperf libmicrohttpd libxslt p11-kit quota util-linux)
optdepends=("curl: libcurl support"
            "gnutls: gnutls support"
            "libbpf: build BPF programs from source code in restricted C"
            "libmicrohttpd: libÂµhttpd support"
            "p11-kit: p11kit support"
            "polkit: polkit support"
            "quota: support for the quotacheck tools"
            "tpm2-tss: TPM2 support")
backup=(etc/pam.d/systemd-user
        etc/systemd/coredump.conf
        etc/systemd/homed.conf
        etc/systemd/journald.conf
        etc/systemd/journal-remote.conf
        etc/systemd/journal-upload.conf
        etc/systemd/logind.conf
        etc/systemd/networkd.conf
        etc/systemd/oomd.conf
        etc/systemd/pstore.conf
        etc/systemd/resolved.conf
        etc/systemd/sleep.conf
        etc/systemd/system.conf
        etc/systemd/timesyncd.conf
        etc/systemd/user.conf
        etc/udev/udev.conf)
install=systemd.install
source=(git+$url.git#tag=v$pkgver
        systemd-user.pam
        systemd.install
        20-systemd-sysusers.hook
        30-systemd-binfmt.hook
        30-systemd-catalog.hook
        30-systemd-daemon-reload.hook
        30-systemd-hwdb.hook
        30-systemd-sysctl.hook
        30-systemd-tmpfiles.hook
        30-systemd-udev-reload.hook
        30-systemd-udev-trigger.hook)
sha256sums=('SKIP'
            'f90be7586197edc4e77ab38bd4f204953e8c5f72b47ef93badd2943e1501444f'
            'ed6b88d29cdf3a5f8a7ef19fba2721610485b8613ac5f9f60572113cc450f1bb'
            '61623bab3be1dc5a4c72145e6cf36bccb08e24372142ef41e4e195cc342b2ec7'
            '393c963a4a3e11f7e7c9faa517179c55926e6663d71eaa9f5a7937cfe0e16af0'
            '5bc028f276e41553680aed71540fe3ca8255488f7a94ed1f52076aafb4b6f08c'
            '315f872f1a2a191fbfad2fc0a519b4e7146fafc226ead0d7b45bd99a79609e92'
            '2bcfa728a45e1177397ccfa0874ae358388eca5216f2db95876552631314596c'
            '0149385b55f58f8cf8ad160fcfb9bb7bd74351708d8cc13984ccb43d4453ed08'
            '7e21a3d6a8c8916fa152041db04bdc9d686732d856804e83b6ccb9e371f23df5'
            '3b0141de66e70697df5b56f72a85001b5ce1f0070295db298d1b001401fce3c0'
            'e9a8a6263c97768d1431a628e3508340c6de5c5dc09441d4d65aff8931500af7')

build() {
  cd $pkgname-stable

  local _timeservers=({0..3}.arch.pool.ntp.org)

  meson setup \
    --prefix /usr \
    --optimization 2 \
    -D b_lto=true \
    -D b_pie=true \
    -D acl=true \
    -D apparmor=false \
    -D audit=true \
    -D bashcompletiondir=/usr/share/bash-completion/completions \
    -D blkid=true \
    -D bootloader=true \
    -D bpf-framework=false \
    -D bzip2=true \
    -D dbus=true \
    -D default-dnssec=no \
    -D default-kill-user-processes=false \
    -D dns-servers= \
    -D elfutils=true \
    -D fdisk=true \
    -D gcrypt=true \
    -D gnutls=true \
    -D homed=true \
    -D kexec-path=/usr/sbin/kexec \
    -D kmod=true \
    -D kmod-path=/usr/bin/kmod \
    -D libcryptsetup=true \
    -D libcurl=true \
    -D libidn2=true \
    -D libiptc=true \
    -D localegen-path=/usr/sbin/locale-gen \
    -D lz4=true \
    -D microhttpd=true \
    -D mode=release \
    -D ntp-servers="${_timeservers[*]}" \
    -D openssl=true \
    -D p11kit=true \
    -D pam=true \
    -D pcre2=true \
    -D polkit=false \
    -D remote=true \
    -D repart=true \
    -D rpmmacrosdir=no \
    -D sbat-distro='KZL' \
    -D sbat-distro-generation=1 \
    -D sbat-distro-pkgname=$pkgname \
    -D sbat-distro-summary='KZL Linux' \
    -D sbat-distro-url="https://github.com/kongzelun/LFS" \
    -D sbat-distro-version=$pkgver \
    -D seccomp=true \
    -D split-bin=true \
    -D split-usr=false \
    -D status-unit-format-default=combined \
    -D tpm2=false \
    -D ukify=true \
    -D user-path=/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin \
    -D version-tag="$pkgver-$pkgrel-kzl $(date -u '+%Y/%m/%d %T %z')" \
    -D xz=true \
    -D zlib=true \
    -D zshcompletiondir=/usr/share/zsh/site-functions \
    -D zstd=true \
    -D adm-gid=4 \
    -D wheel-gid=10 \
    -D tty-gid=5 \
    -D disk-gid=6 \
    -D lp-gid=7 \
    -D kmem-gid=9 \
    -D cdrom-gid=11 \
    -D dialout-gid=18 \
    -D utmp-gid=22 \
    -D tape-gid=33 \
    -D kvm-gid=36 \
    -D video-gid=39 \
    -D audio-gid=63 \
    -D users-gid=100 \
    -D input-gid=104 \
    -D render-gid=105 \
    -D sgx-gid=106 \
    build

  meson compile -C build
}

check() {
  cd $pkgname-stable

  meson test -C build
}

package() {
  cd $pkgname-stable

  meson install -C build --destdir="$pkgdir"

  install -Dm644 "$srcdir"/systemd-user.pam "$pkgdir"/etc/pam.d/systemd-user

  chmod -R 2755 "$pkgdir"/var/log/journal/
  # chmod -R 750 "$pkgdir"/usr/share/polkit-1/rules.d/

  install -Dm0644 "$srcdir"/*.hook -t "$pkgdir"/usr/share/libalpm/hooks/
}
