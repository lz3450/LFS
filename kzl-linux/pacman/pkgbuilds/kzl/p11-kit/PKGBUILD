# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p11-kit
pkgver=0.25.5
pkgrel=55
epoch=
pkgdesc="Loads and enumerates PKCS#11 modules"
arch=('x86_64')
url="https://p11-glue.freedesktop.org"
license=('KZL')
depends=(
  libffi
  libtasn1
)
makedepends=(
  bash-completion
)
source=(git+https://github.com/p11-glue/p11-kit.git#tag=$pkgver)
sha256sums=('930b25ef02695046604eab4641626b4fde082157d1baf581f0e5e3732e89de1e')

build() {
  cd $pkgname

  msg2 "Configuring..."
  meson setup \
    --prefix /usr \
    --auto-features disabled \
    --buildtype plain \
    -D bash_completion=enabled \
    -D libffi=enabled \
    -D systemd=enabled \
    -D trust_module=enabled \
    -D trust_paths=/etc/pki/ca-trust/source:/usr/share/pki/ca-trust-source \
    build

  msg2 "Compiling..."
  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  meson install -C build --destdir "$pkgdir"
}
