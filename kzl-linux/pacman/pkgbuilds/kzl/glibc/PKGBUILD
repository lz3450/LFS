# Maintainer: Zelun Kong <zelun.kong@outlook.com>

# order
# linux-api-headers -> glibc -> binutils -> gcc -> glibc -> binutils -> gcc

pkgname=glibc
pkgver=2.40.g70
pkgrel=5
epoch=
arch=('x86_64')
url="https://www.gnu.org/software/libc"
license=('KZL')
depends=(
  filesystem
  linux-api-headers
  tzdata
)
options=(staticlibs)
source=(
  git+https://sourceware.org/git/glibc.git#branch=release/${pkgver%.g*}/master
  # git+https://sourceware.org/git/glibc.git#tag=$pkgname-$pkgver
  ld.so.conf
  # https://salsa.debian.org/glibc-team/glibc/-/raw/sid/debian/local/usr_sbin/locale-gen
)
backup=(
  etc/ld.so.conf
  etc/ld.so.conf.d/default.conf
  etc/gai.conf
)
install=$pkgname.install
sha256sums=('SKIP'
            'dad04a370e488aa85fb0a813a5c83cf6fd981ce01883fc59685447b092de84b5')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/glibc-//;s/-g.*//;s/-/.g/'
}

prepare() {
  cd $pkgname

  mkdir -p build

  # See `Makeconfig`
  echo 'prefix = /usr
build-programs = yes
exec_prefix = $(prefix)
libdir = $(exec_prefix)/lib
slibdir = $(exec_prefix)/lib
rtlddir = $(slibdir)
includedir = $(prefix)/include
datadir = $(prefix)/share
bindir = $(exec_prefix)/bin
libexecdir = $(exec_prefix)/libexec
rootsbindir = $(exec_prefix)/sbin
sbindir = $(exec_prefix)/sbin
sysconfdir = /etc' > build/configparms
}

build() {
  cd $pkgname/build

  msg2 "Configuring..."
  ./../configure \
    --prefix=/usr \
    --disable-timezone-tools \
    --enable-bind-now \
    --enable-stack-protector=strong \
    --enable-kernel=6.6 \
    --disable-werror \
    --disable-nscd \
    --enable-cet \
    --enable-fortify-source=3 \
    --with-pkgversion="KZL $pkgver-$pkgrel" \
    --with-headers=/usr/include

  msg2 "Compiling..."
  make
}

check() {
  cd $pkgname/build

  make -k check || warning "Check failed."
}

package() {
  cd $pkgname/build

  make install_root="$pkgdir" install

  ### ldconfig
  install -Dm644 "$srcdir"/ld.so.conf "$pkgdir"/etc/ld.so.conf
  install -dm755 "$pkgdir"/etc/ld.so.conf.d
  echo "/usr/local/lib" | install -Dm644 /dev/stdin "$pkgdir"/etc/ld.so.conf.d/default.conf

  ### getaddrinfo(3) configuration file
  install -Dm644 "$srcdir"/$pkgname/posix/gai.conf "$pkgdir"/etc/gai.conf

  ### locale
  make install_root="$pkgdir" -C ../localedata objdir=$(pwd) install-locale-files
  install -dm755 "$pkgdir"/usr/lib/locale
  sed -e '/^#/d' -e '/^SUPPORTED-LOCALES/d' -e 's|/| |g' -e 's| \\||g' ../localedata/SUPPORTED >"$pkgdir"/usr/share/i18n/SUPPORTED
  find "$pkgdir"/usr/lib/locale -maxdepth 1 -mindepth 1 -type d \
    ! -name 'zh_CN.utf8' \
    ! -name 'en_US.utf8' \
    ! -name 'C.utf8' \
    -exec rm -vr {} +

  ## only keep zh_CN and en_US (not tested)
  # sed -n -e '/^\(zh_CN\|en_US\)/p' ../localedata/SUPPORTED \
  #   | sed -e '/^#/d' -e '/^SUPPORTED-LOCALES/d' -e 's|/| |g' -e 's| \\||g' >"$pkgdir"/usr/share/i18n/SUPPORTED
  # find "$pkgdir"/usr/lib/locale -maxdepth 1 -mindepth 1 -type d ! -name 'zh_CN*' ! -name 'en_US*' -exec rm -vr {} +
  # find "$pkgdir"/usr/share/i18n/locales -maxdepth 1 -type f ! -name 'zh_CN*' ! -name 'en_US*' -exec rm -v {} +
  # find "$pkgdir"/usr/share/i18n/charmaps -maxdepth 1 -type f \
  #   ! -name 'GB*.gz' \
  #   ! -name 'ISO-8859-1.gz' \
  #   ! -name 'UTF-8.gz' \
  #   -exec rm -v {} +

  ### clean up
  rm -f "$pkgdir"/etc/ld.so.cache
}
