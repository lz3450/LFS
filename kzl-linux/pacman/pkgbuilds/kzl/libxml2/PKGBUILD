# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxml2
pkgver=2.14.2
pkgrel=7
epoch=
pkgdesc="the XML C parser and toolkit"
arch=('x86_64')
url="http://xmlsoft.org"
license=('KZL')
depends=(
  icu
  readline
  xz
  zlib
)
source=(
  git+https://gitlab.gnome.org/GNOME/libxml2.git#tag=v$pkgver
)
sha256sums=('ea5bb9e9949bd71c725013b8fff73046433c44bb22773a3103f83c932bc63273')

build() {
  cd $pkgname

  msg2 "Configuring..."
  meson setup \
    --prefix /usr \
    --auto-features enabled \
    --buildtype plain \
    -D icu=enabled \
    -D lzma=enabled \
    -D python=disabled \
    -D readline=enabled \
    -D zlib=enabled \
    build

  msg2 "Compiling..."
  meson compile -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  meson install -C build --destdir "$pkgdir"
}
