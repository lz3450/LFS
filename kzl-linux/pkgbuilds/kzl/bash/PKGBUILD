# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=bash
pkgver=5.1
pkgrel=16
epoch=1
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64')
url="https://www.gnu.org/software/bash/bash.html"
optdepends=("bash-completion: for tab completion")
provides=(sh)
source=(https://ftp.gnu.org/gnu/bash/bash-$pkgver.tar.gz{,.sig}
        https://ftp.gnu.org/gnu/bash/bash-$pkgver-patches/bash${pkgver//.}-{001..016}{,.sig}
        dot.bashrc
        dot.bash_logout
        dot.bash_profile
        system.bashrc
        system.bash_logout)
sha256sums=('cc012bc860406dcf42f64431bcd3d2fa7560c02915a601aba9cd597a39329baa'
            'SKIP'
            'ebb07b3dbadd98598f078125d0ae0d699295978a5cdaef6282fe19adef45b5fa'
            'SKIP'
            '15ea6121a801e48e658ceee712ea9b88d4ded022046a6147550790caf04f5dbe'
            'SKIP'
            '22f2cc262f056b22966281babf4b0a2f84cb7dd2223422e5dcd013c3dcbab6b1'
            'SKIP'
            '9aaeb65664ef0d28c0067e47ba5652b518298b3b92d33327d84b98b28d873c86'
            'SKIP'
            'cccbb5e9e6763915d232d29c713007a62b06e65126e3dd2d1128a0dc5ef46da5'
            'SKIP'
            '75e17d937de862615c6375def40a7574462210dce88cf741f660e2cc29473d14'
            'SKIP'
            'acfcb8c7e9f73457c0fb12324afb613785e0c9cef3315c9bbab4be702f40393a'
            'SKIP'
            'f22cf3c51a28f084a25aef28950e8777489072628f972b12643b4534a17ed2d1'
            'SKIP'
            'e45cda953ab4b4b4bde6dc34d0d8ca40d1cc502046eb28070c9ebcd47e33c3ee'
            'SKIP'
            'a2c8d7b2704eeceff7b1503b7ad9500ea1cb6e9393faebdb3acd2afdd7aeae2a'
            'SKIP'
            '58191f164934200746f48459a05bca34d1aec1180b08ca2deeee3bb29622027b'
            'SKIP'
            '10f189c8367c4a15c7392e7bf70d0ff6953f78c9b312ed7622303a779273ab98'
            'SKIP'
            'c7acb66df435d284304c16ca83a5265f9edd9368612095b01a733d45c77ed5ad'
            'SKIP'
            '6a4ee0c81b437b96279a792c1efcec4ba56f009195a318083db6b53b096f83d0'
            'SKIP'
            '1b37692ef1f6cc3dcec246773443276066e6b1379868f8c14e01f4dfd4df80f0'
            'SKIP'
            '8899144f76a5db1fb41a89ed881c9f19add95728dd71db324f772ef225c5384f'
            'SKIP'
            '3e22bf86ae6708df7a6bceb88c67a00118275f9c0b5268f453dd388af7c43b53'
            '4330edf340394d0dae50afb04ac2a621f106fe67fb634ec81c4bfb98be2a1eb5'
            'e149407c2bee17779caec70a7edd3d0000d172e7e4347429b80cb4d55bcec9c2'
            '5fdc20c44bc9058f728d11111327f4dbb5598fec4d948dd5265211598667f9f0'
            '025bccfb374a3edce0ff8154d990689f30976b78f7a932dc9a6fcef81821811e')
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB')

prepare() {
  cd $pkgname-$pkgver

  for p in ../bash${pkgver//.}-{001..016}; do
    msg "applying patch $p"
    patch -p0 -i $p
  done
}

build() {
  cd $pkgname-$pkgver

  _bashconfig=(
    -DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin\"\'\
    -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
    -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
    -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
    -DNON_INTERACTIVE_LOGIN_SHELLS
  )

  CFLAGS+=" ${_bashconfig[@]}"

  ./configure \
    --prefix=/usr \
    --with-curses \
    --enable-readline \
    --without-bash-malloc \
    --with-installed-readline

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  ln -s bash "$pkgdir"/usr/bin/sh

  # system-wide configuration files
  install -Dm644 "$srcdir"/system.bashrc "$pkgdir"/etc/bash.bashrc
  install -Dm644 "$srcdir"/system.bash_logout "$pkgdir"/etc/bash.bash_logout

  # user configuration file skeletons
  install -dm755 "$pkgdir"/etc/skel/
  install -Dm644 "$srcdir"/dot.bashrc "$pkgdir"/etc/skel/.bashrc
  install -Dm644 "$srcdir"/dot.bash_profile "$pkgdir"/etc/skel/.bash_profile
  install -Dm644 "$srcdir"/dot.bash_logout "$pkgdir"/etc/skel/.bash_logout

  # remove reference to $srcdir
  sed -i "/^BUILD_DIR = /s|\\$srcdir\/$pkgname-$_basever||" "$pkgdir"/usr/lib/bash/Makefile.inc
}
