# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=python
pkgver=3.8.2
pkgrel=1
pkgdesc="Next generation of the python high-level scripting language"
arch=(x86_64)
url="https://www.python.org/"
depends=(expat gdbm libffi libnsl openssl)
makedepends=(mpdecimal sqlite valgrind)
optdepends=("sqlite"
            "mpdecimal: for decimal")
source=(https://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.xz{,.asc})
sha256sums=('2646e7dc233362f59714c6193017bb2d6f7b38d6ab4a0cb5fbac5c36c4d845df'
            'SKIP')
validpgpkeys=('0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D'
              'E3FF2839C048B25C084DEBE9B26995E310250568')

build() {
  cd Python-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS="$LDFLAGS -fno-semantic-interposition"

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-optimizations \
    --enable-loadable-sqlite-extensions \
    --enable-ipv6 \
    --enable-big-digits \
    --with-universal-archs="intel-64" \
    --with-lto \
    --with-system-expat \
    --with-system-ffi \
    --with-system-libmpdec \
    --with-dbmliborder=gdbm:ndbm \
    --with-valgrind \
    --with-computed-gotos \
    --with-ensurepip=yes

  make
}

package() {
  cd Python-$pkgver

  CFLAGS="${CFLAGS/-O2/-O3}"

  make DESTDIR="$pkgdir/" EXTRA_CFLAGS="$CFLAGS" install

  ln -s python3 "$pkgdir"/usr/bin/python
}
