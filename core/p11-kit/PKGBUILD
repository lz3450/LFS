# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=p11-kit
pkgver=0.23.20
pkgrel=1
pkgdesc="Loads and enumerates PKCS#11 modules"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
depends=(libffi libtasn1)
install=p11-kit.install
source=(git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver
        p11-kit.install)
sha256sums=('SKIP'
            '68ae9a444ace4971d7cd4cab9359847b96969e3b0917ea6391776fb0f16756f1')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF'
              '462225C3B46F34879FC8496CD605848ED7E69871')

build() {
  cd $pkgname

  meson setup \
    -Dauto_features=enabled \
    -Dbuildtype=plain \
    -Dwrap_mode=nodownload \
    -Db_lto=true \
    -Db_pie=true \
    -Dprefix=/usr \
    -Dlibexecdir=lib \
    -Dsbindir=bin \
    -Dtrust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source \
    build

    ninja -C build
}

check() {
  cd $pkgname

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" meson install -C build
}
