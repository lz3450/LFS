# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=filesystem
pkgver=2020.05
pkgrel=1
arch=(any)
depends=(iana-etc)
backup=(etc/crypttab
        etc/fstab
        etc/group
        etc/gshadow
        etc/host.conf
        etc/hosts
        etc/os-release
        etc/passwd
        etc/profile
        etc/resolv.conf
        etc/securetty
        etc/shells)
source=(crypttab
        fstab
        group
        gshadow
        host.conf
        hosts
        os-release
        passwd
        profile
        resolv.conf
        securetty
        shadow
        shells
        systemd.environment-generator)
sha256sums=('e03bede3d258d680548696623d5979c6edf03272e801a813c81ba5a5c64f4f82'
            'e54626e74ed8fee4173b62a545ab1c3a3a069e4217a0ee8fc398d9933e9c1696'
            '244f0718ee2a9d6862ae59d6c18c1dd1568651eada91a704574fa527fbac2b3a'
            '90d879374f77bac47f132164c1e7fc4892e994ff1d1ac376efa0c1c26ea37273'
            '4d7b647169063dfedbff5e1e22cee77bd1a4183dbcfd5e802e68939da4bbf733'
            'd9cd8a77d9e0aa5e90d7f4ed74c8745c17b525e720e28e4c44364150003c35f9'
            '9178f1df5caa9f18ba20844c3fa5582d61994ae411751ca918505a1bfc5a198b'
            '5e06477834f51abf42ea4e8dc199632afc6afbfd8c44354685a271e9a48d2c0a'
            '5da078777cda24e4df697e2928451723f2303bfdbb2ce9551c822188c7945d25'
            '5557d8e601b17a80d1ea7de78a9869be69637cb6a02fbfe334e22fdf64e61d4c'
            'd88be2b45b43605ff31dd83d6a138069b6c2e92bc8989b7b9ab9eba8da5f8c7b'
            '8ce994663d7588143ad7ed4441b07f468f4f7d3590164dd73ddfa3ea307ece8e'
            'c390b31fffc4a2b5d78ae8c89f5317aadef1f71baac09cfb467b675db1406d61'
            'cb54cecc63f47bfaa5b27fa2409c07c35531342c82b9bb1f2956334b33792853')

package() {
  cd $pkgdir
  
  # root filesystem
  for d in boot dev etc home mnt usr var opt srv run; do
    install -dm755 $d
  done
  install -dm555 proc
  install -dm555 sys
  install -dm0750 root
  install -dm1777 tmp

  # /etc
  install -d etc/{skel,profile.d} usr/share/factory/etc
  for f in fstab group host.conf hosts os-release passwd profile resolv.conf securetty shells; do
    install -m644 "$srcdir"/$f etc/
    install -m644 "$srcdir"/$f usr/share/factory/etc/
  done
  ln -s ../proc/self/mounts etc/mtab
  for f in crypttab gshadow shadow; do
    install -m600 "$srcdir"/$f etc/
    install -m600 "$srcdir"/$f usr/share/factory/etc/
  done

  # /usr
  for d in bin include lib share src; do
    install -d -m755 usr/$d
  done
  for d in {1..8}; do
    install -d -m755 usr/share/man/man$d
  done

  # /usr/local
  for d in bin etc include lib man share src; do
    install -d -m755 usr/local/$d
  done
  ln -s bin usr/local/sbin
  ln -s ../man usr/local/share/man


  # bin symlinks
  ln -s usr/bin bin
  ln -s usr/bin sbin
  ln -s bin usr/sbin

  # lib symlinks
  ln -s usr/lib lib
  ln -s usr/lib lib64
  ln -s lib usr/lib64

  # systemd.environment-generator
  install -Dm755 "$srcdir"/systemd.environment-generator usr/lib/systemd/system-environment-generators/path

  # systemd-sysctl
  # install -Dm644 "$srcdir"/sysctl usr/lib/sysctl.d/10-arch.conf

  # systemd-sysusers
  # install -Dm644 "$srcdir"/sysusers usr/lib/sysusers.d/arch.conf

  # systemd-tmpfiles
  # install -Dm644 "$srcdir"/tmpfiles usr/lib/tmpfiles.d/arch.conf

  # add logo
  # install -D -m644 "$srcdir"/archlinux.{svg,png} usr/share/pixmaps
}
