# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=krb5
pkgver=1.18
pkgrel=1
pkgdesc="The Kerberos network authentication system"
arch=(x86_64)
url="https://web.mit.edu/kerberos/"
depends=(keyutils openldap)
backup=(etc/krb5.conf
        var/lib/krb5kdc/kdc.conf)
source=(https://web.mit.edu/kerberos/dist/krb5/$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('73913934d711dcf9d5f5605803578edb44b9a11786df3c1b2711f4e1752f2c88'
            'SKIP')
validpgpkeys=('2C732B1C0DBEF678AB3AF606A32F17FD0055C305'
              'C4493CB739F4A89F9852CBC20CBA08575F8372DF')

build() {
  cd $pkgname-$pkgver/src

  export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
  export CPPFLAGS+=" -I/usr/include/et"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --enable-shared \
    --enable-dns-for-realm \
    --disable-rpath \
    --with-system-et \
    --with-system-ss \
    --with-ldap \
    --without-tcl \
    --without-system-verto

  make
}

package() {
  cd $pkgname-$pkgver/src

  make DESTDIR="$pkgdir/" EXAMPLEDIR=/usr/share/doc/$pkgname/examples install

  install -Dpm644 config-files/krb5.conf -t "$pkgdir"/etc/
  install -Dpm644 config-files/kdc.conf -t "$pkgdir"/var/lib/krb5kdc/

  # TODO: systemd
}
