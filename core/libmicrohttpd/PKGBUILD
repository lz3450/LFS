# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libmicrohttpd
pkgver=0.9.70
pkgrel=1
pkgdesc="A small C library that is supposed to make it easy to run an HTTP server as part of another application"
arch=(x86_64)
url="https://www.gnu.org/software/libmicrohttpd/"
depends=(gnutls)
checkdepends=(curl)
source=(https://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('90d0a3d396f96f9bc41eb0f7e8187796049285fabef82604acd4879590977307'
            'SKIP')
validpgpkeys=('EA812DBEFA5A7EF17DA8F2C1460A317C3326D2AE'
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-curl \
    --enable-https \
    --enable-largefile \
    --enable-messages \
    --disable-dependency-tracking \
    --disable-examples \
    --with-pic

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
