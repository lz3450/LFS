# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=curl
pkgver=7.69.1
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=(x86_64)
url="https://curl.haxx.se"
depends=(ca-certificates krb5 libidn2 libnghttp2 libpsl libssh2 openssl zlib)
source=(https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('01ae0c123dee45b01bbaef94c0bc00ed2aec89cb2ee0fd598e0d302a6b5e0a98'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-ipv6 \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --with-gssapi \
    --with-libssh2 \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  make -C scripts DESTDIR="$pkgdir" install
}
