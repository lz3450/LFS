# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=guile
pkgver=3.0.2
pkgrel=1
pkgdesc="Portable, embeddable Scheme implementation written in C"
arch=('x86_64')
url="https://www.gnu.org/software/guile/"
depends=(gc gmp libffi libunistring)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('04485e759b2aec44e33cdd06ece28bbb859c1dbef73e8aeac02e4e7ca2fe0e57'
            'SKIP')
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5'
              'FF478FB264DE32EC296725A3DDC0F5358812F8F2'
              '4FD4D288D445934E0A14F9A5A8803732E4436885')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check || warning "Check failed."
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/lib/libguile-?.?.so.*-gdb.scm
}
