# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=shadow
pkgver=4.8.1
pkgrel=1
pkgdesc="A package contains programs for handling passwords in a secure way"
arch=('x86_64')
url="https://github.com/shadow-maint/shadow"
depends=(acl attr audit pam)
makedepends=(git)
backup=(etc/default/useradd
        etc/login.defs
        etc/pam.d/{chage,chgpasswd,chpasswd,groupadd,groupdel,groupmems,groupmod,newusers,shadow,passwd,useradd,userdel,usermod})
source=(https://github.com/shadow-maint/shadow/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
        chgpasswd
        chpasswd
        defaults.pam
        login.defs
        newusers
        passwd
        shadow.service
        shadow.timer
        useradd.defaults)
sha256sums=('a3ad4630bdc41372f02a647278a8c3514844295d36eefe68ece6c3a641c1ae62'
            'a955ecbd82a801102fad756bb98dca778c1eba14a3367c601d6237b75642c36b'
            '7409cd2ab04592859d95986467123e356ada4e11413b314ad84e32dc24ca8c92'
            'fc6807842f92e9d3f792d6b64a0d5aad87995a279153ab228b1b2a64d9f32f20'
            '96357804a5ec6cb70bd08fafd48cd77632b28950f0e3099f56321b5630c7cc97'
            '7409cd2ab04592859d95986467123e356ada4e11413b314ad84e32dc24ca8c92'
            '75428a0f016cccbd9091c012b4d6b778d6ef8d97b607849b5ba1aca0314ed75b'
            'c391494a31f3260a49887bd4109f87a3a8fc368b3ec89d9d15be7466bd8975cb'
            '736eb7b16fc2fff9c902d4823641114338f290a6825e66831f41c5474e994207'
            'bc9fef974321e399fb47c229a82e37d81a8f39424082ef8e4915d0dbee9ff7c2')
validpgpkeys=('D5C2F9BFCA128BBA22A77218872F702C4D6E25A8')

build() {
  cd $pkgname-$pkgver
  
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --sysconfdir=/etc \
    --with-audit \
    --with-libpam \
    --with-acl \
    --with-attr \
    --with-group-name-max-length=32 \
    --without-selinux

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" rootsbindir="$pkgdir/usr/bin/" install

  # useradd defaults
  install -Dm600 "$srcdir"/useradd.defaults "$pkgdir"/etc/default/useradd

  # login.defs
  install -Dm644 "$srcdir"/login.defs "$pkgdir"/etc/

  # PAM config - custom
  rm "$pkgdir"/etc/pam.d/*
  install -t "$pkgdir"/etc/pam.d -m644 "$srcdir"/{passwd,chgpasswd,chpasswd,newusers}
  
  # PAM config - from source
  install -Dm644 etc/pam.d/groupmems "$pkgdir"/etc/pam.d/groupmems

  # Use the 'defaults.pam' PAM file for other similar utilities
  for file in chage groupadd groupdel groupmod shadow useradd usermod userdel; do
    install -Dm644 "$srcdir"/defaults.pam "$pkgdir"/etc/pam.d/$file
  done

  # Remove evil/broken tools
  rm "$pkgdir"/usr/sbin/logoutd

  # Remove utilities provided by util-linux
  rm "$pkgdir"/usr/bin/{login,su,chsh,chfn,sg,nologin}
  rm "$pkgdir"/usr/sbin/{vipw,vigr}
  find "$pkgdir"/usr/share/man \
    '(' -name 'chsh.1'    -o \
        -name 'chfn.1'    -o \
        -name 'su.1'      -o \
        -name 'logoutd.8' -o \
        -name 'login.1'   -o \
        -name 'nologin.8' -o \
        -name 'vipw.8'    -o \
        -name 'vigr.8'    -o \
        -name 'newgrp.1' ')' \
    -delete
  rmdir "$pkgdir"/usr/share/man/{fi,id,zh_TW}/man1
  rmdir "$pkgdir"/usr/share/man/{fi,ko/man8}

  # Keep newgrp, as sg is really an alias to it
  mv "$pkgdir"/usr/bin/{newgrp,sg}

  mv "$pkgdir"/usr/sbin/* "$pkgdir"/usr/bin
  rmdir "$pkgdir"/usr/sbin

  # systemd units
  install -Dm644 "$srcdir"/shadow.timer "$pkgdir"/usr/lib/systemd/system/shadow.timer
  install -Dm644 "$srcdir"/shadow.service "$pkgdir"/usr/lib/systemd/system/shadow.service
  install -dm755 "$pkgdir"/usr/lib/systemd/system/timers.target.wants
  ln -s ../shadow.timer "$pkgdir"/usr/lib/systemd/system/timers.target.wants/shadow.timer
}
