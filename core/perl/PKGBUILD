# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=perl
pkgver=5.30.2
pkgrel=1
pkgdesc="A highly capable, feature-rich programming language"
arch=(x86_64)
url="https://www.perl.org"
depends=(db gdbm)
options=(!purge emptydirs)
source=(https://www.cpan.org/src/5.0/$pkgname-$pkgver.tar.xz)
sha256sums=('a1aa88bd6fbbdc2e82938afbb76c408b0ea847317737b712dc196cc7907a5259')

build() {
  cd $pkgname-$pkgver

  ./Configure -des \
    -Dprefix=/usr \
    -Dvendorprefix=/usr \
    -Dman1dir=/usr/share/man/man1 \
    -Dman3dir=/usr/share/man/man3 \
    -Doptimize="$CFLAGS" \
    -Dcccdlflags='-fPIC' \
    -Dldflags="$LDFLAGS" \
    -Dlddlflags="-shared $LDFLAGS" \
    -Duseshrplib \
    -Dusethreads \
    -Dmyuname="kzl" \
    -Dmyhostname="kzl" \
    -Dcf_time="`date -u --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}"`"

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
