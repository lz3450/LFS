# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman
pkgver=5.2.1
pkgrel=1
arch=(x86_64)
url="https://www.archlinux.org/pacman/"
depends=(curl gpgme libarchive)
checkdepends=(fakechroot)
backup=(etc/pacman.conf
        etc/makepkg.conf)
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman-5.2.1-fix-pactest-package-tar-format.patch
        makepkg-fix-one-more-file-seccomp-issue.patch
        pacman.conf
        makepkg.conf)
sha256sums=('1930c407265fd039cb3a8e6edc82f69e122aa9239d216d9d57b9d1b9315af312'
            'SKIP'
            '824a5c9dd458fb27b05a9a0b4b5d75b7a392de0dae79a18f5cfe8beaf4d82f0c'
            'e481a161bba76729cd434c97e0b319ddfcb1d93b2e4890d72b4e8a32982531d9'
            '0e084cac91121c37cd3dc019693e6fadfcb68060af45d77a81dc2a03687f998d'
            '09be030709a6f8817c388ba4377db94147b5175a188369374a3d9f5d2c250128')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'
              'B8151B117037781095514CA7BBDFFC92306B1121')
              
prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "$srcdir"/pacman-5.2.1-fix-pactest-package-tar-format.patch
  patch -Np1 -i "$srcdir"/makepkg-fix-one-more-file-seccomp-issue.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig

  make V=1
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir"/etc/
  install -dm755 "$pkgdir"/var/
  install -Dm644 "$srcdir"/pacman.conf "$pkgdir"/etc/
  install -Dm644 "$srcdir"/makepkg.conf "$pkgdir"/etc/
}
