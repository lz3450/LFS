# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman
pkgver=5.2.1
pkgrel=5
arch=('x86_64')
url="https://www.archlinux.org/pacman/"
depends=(curl gpgme libarchive)
checkdepends=(fakechroot)
backup=(etc/pacman.conf
        etc/makepkg.conf)
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman-5.2.1-fix-pactest-package-tar-format.patch
        makepkg-fix-one-more-file-seccomp-issue.patch
        pacman-5.2.1-reproducible-libprovides.patch)
sha256sums=('1930c407265fd039cb3a8e6edc82f69e122aa9239d216d9d57b9d1b9315af312'
            'SKIP'
            '824a5c9dd458fb27b05a9a0b4b5d75b7a392de0dae79a18f5cfe8beaf4d82f0c'
            'e481a161bba76729cd434c97e0b319ddfcb1d93b2e4890d72b4e8a32982531d9'
            '667ba659f85e3740fda9808e4751a44a63e0484072594d961b87e474c607b79c')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'
              'B8151B117037781095514CA7BBDFFC92306B1121')
              
prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "$srcdir"/pacman-5.2.1-fix-pactest-package-tar-format.patch
  patch -Np1 -i "$srcdir"/makepkg-fix-one-more-file-seccomp-issue.patch
  patch -Np1 -i "$srcdir"/pacman-5.2.1-reproducible-libprovides.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-doc \
    --with-pkg-ext='.pkg.tar.zst' \
    --with-src-ext='.src.tar.zst' \
    --with-scriptlet-shell=/bin/bash \
    --with-ldconfig=/sbin/ldconfig

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  
  sed -e '/CPPFLAGS=/s/^#//' \
      -e '/CPPFLAGS=/s/"\(.*\)"/"-D_FORTIFY_SOURCE=2"/' \
      -e '/CFLAGS=/s/^#//' \
      -e '/CFLAGS=/s/"\(.*\)"/"-march= -O2 -pipe -fno-plt"/' \
      -e '/CXXFLAGS=/s/^#//' \
      -e '/CXXFLAGS=/s/"\(.*\)"/"-march= -O2 -pipe -fno-plt"/' \
      -e '/LDFLAGS=/s/^#//' \
      -e '/LDFLAGS=/s/"\(.*\)"/"-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"/' \
      -e '/RUSTFLAGS=/s/^#//' \
      -e '/RUSTFLAGS=/s/"\(.*\)"/"-C opt-level=2"/' \
      -e '/MAKEFLAGS=/s/^#//' \
      -e '/MAKEFLAGS=/s/"\(.*\)"/"-j$(nproc)"/' \
      -e '/DEBUG_CFLAGS=/s/^#//' \
      -e '/DEBUG_CFLAGS=/s/"\(.*\)"/"-g -fvar-tracking-assignments"/' \
      -e '/DEBUG_CXXFLAGS=/s/^#//' \
      -e '/DEBUG_CXXFLAGS=/s/"\(.*\)"/"-g -fvar-tracking-assignments"/' \
      -e '/DEBUG_RUSTFLAGS=/s/"\(.*\)"/"-C debuginfo=2"/' \
      -e '/BUILDDIR=\/tmp\/makepkg/s/^#//' \
      -e '/PKGDEST=/s/^#//' \
      -e '/SRCDEST=/s/^#//' \
      -e '/SRCPKGDEST=/s/^#//' \
      -e '/LOGDEST=/s/^#//' \
      -e '/PACKAGER=/s/^#//' \
      -e '/INTEGRITY_CHECK=/s/md5/sha256/' \
      -e '/PKGDEST=/s/\/home/~\/makepkg/' \
      -e '/SRCDEST=/s/\/home/~\/makepkg/' \
      -e '/SRCPKGDEST=/s/\/home/~\/makepkg/' \
      -e '/LOGDEST=/s/\/home/~\/makepkg/' \
      -e '/PACKAGER=/s/John Doe <john@doe.com>/Zelun Kong <zelun.kong@outlook.com>/' \
      -i "$pkgdir"/etc/makepkg.conf

  cat >> "$pkgdir"/etc/pacman.conf << EOF

[core]
SigLevel = Optional TrustAll
Server = file:///var/repository/\$repo

[extra]
SigLevel = Optional TrustAll
Server = file:///var/repository/\$repo

[community]
SigLevel = Optional TrustAll
Server = file:///var/repository/\$repo
EOF
}
