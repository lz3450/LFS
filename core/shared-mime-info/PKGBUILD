# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=shared-mime-info
pkgver=1.15
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Specifications/shared-mime-info-spec/"
depends=(glib2 libxml2)
makedepends=(itstool)
source=(https://gitlab.freedesktop.org/xdg/shared-mime-info/uploads/b27eb88e4155d8fccb8bb3cd12025d5b/$pkgname-$pkgver.tar.xz)
sha256sums=('f482b027437c99e53b81037a9843fccd549243fd52145d016e9c7174a4f5db90')

build() {
  cd $pkgname-$pkgver

  # https://bugs.archlinux.org/task/38836
  # https://bugs.freedesktop.org/show_bug.cgi?id=70366
  export ac_cv_func_fdatasync=no

  ./configure \
    --prefix=/usr \
    --disable-update-mimedb

  make
}

check() {
  cd $pkgname-$pkgver

  make -k check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
