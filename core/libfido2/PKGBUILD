# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libfido2
pkgver=1.3.1
pkgrel=2
pkgdesc="Library functionality for FIDO 2.0, including communication with a device over USB"
arch=('x86_64')
url="https://developers.yubico.com/libfido2"
depends=(hidapi libcbor openssl)
source=(https://developers.yubico.com/$pkgname/Releases/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('ba35e22016b60c1e4be66dff3cd6a60c1fe4bfa0d91ec0b89ca9da25ebeaaf41'
            'SKIP')
validpgpkeys=('EE90AE0D19774C8386628FAAB428949EF7914718'
              '1D7308B0055F5AEF36944A8F27A9C24D9588EA0F')

build() {
  cd $pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -S . \
    -B build
  
  cmake --build build
}

package() {
  cd $pkgname-$pkgver
  
  cmake --build build --target install -- DESTDIR="$pkgdir/"
}
