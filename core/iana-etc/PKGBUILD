# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=iana-etc
pkgver=2020.04
pkgrel=1
pkgdesc="/etc/protocols and /etc/services provided by IANA"
arch=(any)
url="https://www.iana.org/protocols"
groups=(base)
backup=(etc/{protocols,services})
source=(https://www.iana.org/assignments/service-names-port-numbers/service-names-port-numbers.xml
        https://www.iana.org/assignments/protocol-numbers/protocol-numbers.xml)
sha256sums=('8db1bc893c8611a27c381cc0fc26b3db0a03f0e5b7e50fa93be87ff9ebfc1fc5'
            '7de8f70197974b5238edd5a5a318392d7c262147c7495b7fb64f66b9966d4ec8')

package() {
  install -dm755 "$pkgdir"/etc
	install -Dm644 service-names-port-numbers.xml "$pkgdir"/usr/share/iana-etc/service-names-port-numbers.xml
	install -Dm644 protocol-numbers.xml "$pkgdir"/usr/share/iana-etc/protocol-numbers.xml

  	gawk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/protocol-numbers.xml\n" }
(/<record/) { v=n="" }
(/<value/) { v=$3 }
(/<name/ && $3!~/ /) { n=$3 }
(/<\/record/ && n && v!="") { printf "%-12s %3i %s\n", tolower(n),v,n }
' protocol-numbers.xml > "$pkgdir"/etc/protocols

	gawk -F"[<>]" '
BEGIN { print "# Full data: /usr/share/iana-etc/service-names-port-numbers.xml\n" }
(/<record/) { n=u=p=c="" }
(/<name/ && !/\(/) { n=$3 }
(/<number/) { u=$3 }
(/<protocol/) { p=$3 }
(/Unassigned/ || /Reserved/ || /historic/) { c=1 }
(/<\/record/ && n && u && p && !c) { printf "%-15s %5i/%s\n", n,u,p }
' service-names-port-numbers.xml > "$pkgdir"/etc/services
}
