# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=rhash
pkgver=1.3.9
pkgrel=1
pkgdesc="Utility for verifying hash sums of files"
arch=(x86_64)
url="https://github.com/rhash/RHash"
source=(https://github.com/rhash/RHash/archive/v$pkgver.tar.gz
        https://github.com/rhash/RHash/releases/download/v$pkgver/v$pkgver.tar.gz.asc)
sha256sums=('42b1006f998adb189b1f316bf1a60e3171da047a85c4aaded2d0d26c1476c9f6'
            'SKIP')
validpgpkeys=('2875F6B1C2D27A4F0C8AF60B2A714497E37363AE')

build() {
  cd RHash-$pkgver

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd RHash-$pkgver

  make test test-lib
}

package() {
  cd RHash-$pkgver

  make DESTDIR="$pkgdir/" install
  make -C librhash DESTDIR="$pkgdir/" install-lib-headers install-lib-shared install-so-link
}
