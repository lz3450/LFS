# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=gnutls
pkgver=3.6.13
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=(x86_64)
url="https://www.gnutls.org/"
depends=(libidn2 libtasn1 libunistring nettle p11-kit)
optdepends=("guile: for use with Guile bindings")
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/v3.6/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('32041df447d9f4644570cf573c9f60358e865637d69b7e59d1159b7240b52f38'
            'SKIP')
validpgpkeys=('0424D4EE81A0E3D119C6F835EDA21E94B565716F'
              '1F42418905D8206AA754CCDC29EE58B996865171')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-guile \
    --disable-static \
    --with-idn \
    --with-zlib \
    --with-default-trust-store-pkcs11="pkcs11:model=p11-kit-trust;manufacturer=PKCS%2311%20Kit" \
    --without-guile-site-dir

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
