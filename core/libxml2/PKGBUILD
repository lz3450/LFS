# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=libxml2
pkgver=2.9.10
pkgrel=1
pkgdesc="XML parsing library, version 2"
arch=(x86_64)
url="http://xmlsoft.org"
depends=(icu)
source=(git+https://gitlab.gnome.org/GNOME/libxml2.git#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --with-threads \
    --with-history \
    --with-python=/usr/bin/python3 \
    --with-icu

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool

  PYTHONHASHSEED=0 make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install
}
