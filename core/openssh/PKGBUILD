# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openssh
pkgver=8.2.P1
pkgrel=1
arch=('x86_64')
url="https://www.openssh.com/portable.html"
depends=(krb5 libedit openssl)
# depends=(krb5 ldns libedit openssl)
makedepends=(libfido2)
optdepends=("libfido2: FIDO/U2F support"
            "x11-ssh-askpass: input passphrase in X"
            "xorg-xauth: X11 forwarding")
backup=(etc/ssh/ssh_config
        etc/ssh/sshd_config
        etc/pam.d/sshd)
source=(git+git://anongit.mindrot.org/openssh.git#tag=V_${pkgver//./_}
        sshd.conf
        sshd.pam
        sshd.service
        sshdgenkeys.service)
sha256sums=('SKIP'
            '4effac1186cc62617f44385415103021f72f674f8b8e26447fc1139c670090f6'
            '64576021515c0a98b0aaf0a0ae02e0f5ebe8ee525b1e647ab68f369f81ecd846'
            'e40f8b7c8e5e2ecf3084b3511a6c36d5b5c9f9e61f2bb13e3726c71dc7d4fbc7'
            '4031577db6416fcbaacf8a26a024ecd3939e5c10fe6a86ee3f0eea5093d533b7')

# TODO: ldns support (--with-ldns)

build() {
  cd $pkgname

  autoreconf

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh \
    --with-libedit \
    --with-pam \
    --with-ssl-engine \
    --with-security-key-builtin \
    --with-privsep-user=nobody \
    --with-kerberos5=/usr \
    --with-xauth=/usr/bin/xauth \
    --with-md5-passwords \
    --with-pid-dir=/run \
    --with-default-path="/usr/local/bin:/usr/bin"

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/sshdgenkeys.service "$pkgdir"/usr/lib/systemd/system/sshdgenkeys.service
  install -Dm644 "$srcdir"/sshd.service "$pkgdir"/usr/lib/systemd/system/sshd.service
  install -Dm644 "$srcdir"/sshd.conf "$pkgdir"/usr/lib/tmpfiles.d/sshd.conf
  install -Dm644 "$srcdir"/sshd.pam "$pkgdir"/etc/pam.d/sshd

  install -Dm755 contrib/findssl.sh "$pkgdir"/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id "$pkgdir"/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1 "$pkgdir"/usr/share/man/man1/ssh-copy-id.1

	sed -e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
      -e '/^#PrintMotd yes$/c PrintMotd no # pam does that' \
      -e '/^#UsePAM no$/c UsePAM yes' \
      -i "$pkgdir"/etc/ssh/sshd_config
}
