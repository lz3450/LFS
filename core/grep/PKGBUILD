# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=grep
pkgver=3.4
pkgrel=1
pkgdesc="A string search utility"
arch=(x86_64)
url="https://www.gnu.org/software/grep/"
depends=(pcre)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('58e6751c41a7c25bfc6e9363a41786cff3ba5709cf11d5ad903cf7cce31cc3fb'
            'SKIP')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --without-included-regex

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
