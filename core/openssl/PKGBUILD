# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=openssl
pkgver=1.1.1f
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
arch=(x86_64)
url="https://www.openssl.org"
backup=(etc/ssl/openssl.cnf)
source=(https://www.openssl.org/source/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('186c6bfe6ecfba7a5b48c47f8a1673d0f3b0e5ba2e25602dd23b629975da3f35'
            'SKIP')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491'
              '7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C')

build() {
  cd $pkgname-$pkgver

  ./Configure \
    --prefix=/usr \
    --libdir=lib \
    --openssldir=/etc/ssl \
    shared \
    zlib-dynamic \
    no-ssl3-method \
    enable-ec_nistp_64_gcc_128 \
    linux-x86_64 \
    "-Wa,--noexecstack $CPPFLAGS $CFLAGS $LDFLAGS"

  make depend
  make
}

check() {
  cd $pkgname-$pkgver

	make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" MANSUFFIX=ssl install
}
