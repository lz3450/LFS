# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pacman-contrib
pkgver=1.3.0
pkgrel=1
epoch=1
pkgdesc="Contributed scripts and tools for pacman systems"
arch=(x86_64)
url="https://git.archlinux.org/pacman-contrib.git/about/"
depends=(fakeroot)
optdepends=("mlocate: pacdiff")
source=(https://git.archlinux.org/$pkgname.git/snapshot/$pkgname-$pkgver.tar.{gz,asc})
sha256sums=('2b8f614651dbe272c9b518c73e53fc194405c06fd8637159a8ac9012c84e9452'
            'SKIP')
validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5')

prepare() {
  cd pacman-contrib-$pkgver

  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-doc

  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
}
