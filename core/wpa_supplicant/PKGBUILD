# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=wpa_supplicant
pkgver=2.9
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=(x86_64)
url="https://w1.fi/wpa_supplicant/"
depends=(libnl openssl)
source=(https://w1.fi/releases/$pkgname-$pkgver.tar.gz{,.asc}
        CVE-2019-16275.patch::https://w1.fi/cgit/hostap/patch/?id=d86d66dc073bc21d3b12faf4112062ae00c1773f
        roam-properties.patch::https://w1.fi/cgit/hostap/patch/?id=23d87687c2428f3b94865580b0d33e05c03e6756
        systemd.patch
        config)
sha256sums=('fcbdee7b4a64bea8177973299c8c824419c413ec2e3a95db63dd6a5dc3541f17'
            'SKIP'
            'de592c66f621736dc4e88e00b66e518e80ca16dcba95810ff2708254e398753b'
            '1ad3b61397c4a1dbafbf89059bccdda07cfe7eaff9f23ee25bed7bdd82c2bd87'
            '7c65f52b75469d8ff8677fb2e117a54c28b160d4a5723ba79b4124284c3b0163'
            'ec5acffef25e04dbd9dd3f001424dc87051c22bc3ab1dea62085505853f9ba6a')
validpgpkeys=('EC4AA0A991A5F2464582D52D2B6EF432EFC895FA')

prepare() {
  cd $pkgname-$pkgver

  msg2 "Applying patch CVE-2019-16275.patch"
  patch -p1 -i "$srcdir"/CVE-2019-16275.patch

  msg2 "Applying patch roam-properties.patch"
  patch -p1 -i "$srcdir"/roam-properties.patch

  msg2 "Applying patch systemd.patch"
  patch -p1 -i "$srcdir"/systemd.patch
}

build() {
  cd $pkgname-$pkgver/$pkgname

  cp "$srcdir"/config .config

  make LIBDIR=/usr/lib BINDIR=/usr/bin
  make LIBDIR=/usr/lib BINDIR=/usr/bin eapol_test
}

package() {
  cd $pkgname-$pkgver/$pkgname

  make LIBDIR=/usr/lib BINDIR=/usr/bin DESTDIR="$pkgdir/" install
  install -Dm755 eapol_test "$pkgdir"/usr/bin/eapol_test

  install -dm755 "$pkgdir"/etc/wpa_supplicant/
  install -Dm644 wpa_supplicant.conf "$pkgdir"/usr/share/doc/wpa_supplicant/wpa_supplicant.conf

  install -dm755 "$pkgdir"/usr/share/dbus-1/{system.d,system-services}
  install -m644 dbus/fi.w1.wpa_supplicant1.service "$pkgdir"/usr/share/dbus-1/system-services/

  install -Dm644 dbus/dbus-wpa_supplicant.conf "$pkgdir"/usr/share/dbus-1/system.d/wpa_supplicant.conf

  install -dm755 "$pkgdir"/usr/share/man/man{5,8}/
  install -m644 doc/docbook/*.5 "$pkgdir"/usr/share/man/man5/
  install -m644 doc/docbook/*.8 "$pkgdir"/usr/share/man/man8/
  rm -f "$pkgdir"/usr/share/man/man8/wpa_{priv,gui}.8

  install -dm755 "$pkgdir"/usr/lib/systemd/system/
  install -m644 systemd/*.service "$pkgdir"/usr/lib/systemd/system/
}
