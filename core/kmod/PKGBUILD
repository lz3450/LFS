# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=kmod
pkgver=27
pkgrel=1
pkgdesc="Linux kernel module management tools and library"
arch=(x86_64)
url="https://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
depends=(openssl)
source=(https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.{xz,sign}
        depmod.script
        depmod.hook
        depmod-search.conf)
sha256sums=('c1d3fbf16ca24b95f334c1de1b46f17bbe5a10b0e81e72668bdc922ebffbbc0c'
            'SKIP'
            '31699f1b8b33f1bd61111328375c10d75c587e49c4fee9b7778e664a9354e4b0'
            'c11c2a0f66ea405493e8617689ca10818dc81dd1dddc19bdb220c8b2917119c1'
            '1a92bfeae870f61ce814577e69d2a147a9c0caf6aed1131243e4179241fcc4a8')
validpgpkeys=('EAB33C9690013C733916AC839BA2A5A630CBEA53')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-xz \
    --with-zlib \
    --with-openssl

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  
  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir"/usr/bin/$tool
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir"/depmod-search.conf "$pkgdir"/usr/lib/depmod.d/search.conf

  # hook
  install -Dm644 "$srcdir"/depmod.hook "$pkgdir"/usr/share/libalpm/hooks/60-depmod.hook
  install -Dm755 "$srcdir"/depmod.script "$pkgdir"/usr/share/libalpm/scripts/depmod
}
