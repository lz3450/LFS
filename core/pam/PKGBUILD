# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=pam
pkgver=1.3.1
pkgrel=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64')
url="http://linux-pam.org"
depends=(cracklib libtirpc)
source=(https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz{,.asc}
        other
        system-auth
        system-local-login
        system-login
        system-remote-login
        system-services)
sha256sums=('eff47a4ecd833fbf18de9686632a70ee8d0794b79aecb217ebd0ce11db4cd0db'
            'SKIP'
            '39f8e23c458d50a5d3ed09e921303e5af4f15dbed9dfadc127dd2ebe311a6ed5'
            '3eb67872e436817ec97c4f3795adba2cf1d3829ea4e107ef5747569e4eeb5746'
            '005736b9bd650ff5e5d82a7e288853776d5bb8c90185d5774c07231c1e1c64a9'
            '1e5dd7dd31a81381f4a00179844bd45f15e4ead46cc4d5a1a0f2c0701189829b'
            '005736b9bd650ff5e5d82a7e288853776d5bb8c90185d5774c07231c1e1c64a9'
            '6eb1acdd3fa9f71a7f93fbd529be57ea65bcafc6e3a98a06af4d88013fc6a567')
validpgpkeys=('8C6BFD92EE0F42EDF91A6A736D1A7F052E5924BB')

build() {
  cd Linux-PAM-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --disable-db

  make
}

check() {
  cd Linux-PAM-$pkgver

  make check
}

package() {
  cd Linux-PAM-$pkgver

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/usr/share/doc/Linux-PAM/index.html

  install -dm755 "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/other "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/system-auth "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/system-local-login "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/system-login "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/system-remote-login "$pkgdir"/etc/pam.d/
  install -m644 "$srcdir"/system-services "$pkgdir"/etc/pam.d/
}
