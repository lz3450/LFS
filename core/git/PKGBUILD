# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=git
pkgver=2.26.2
pkgrel=1
arch=(x86_64)
url="https://git-scm.com/"
depends=(curl expat openssl pcre2)
# makedepends=(libsecret)
# optdepends=("libsecret: credential helper")
source=(https://www.kernel.org/pub/software/scm/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('6d65132471df9e531807cb2746f8be317e22a343b9385bbe11c9ce7f0d2fc848'
            'SKIP')
validpgpkeys=('96E07AF25771955980DAD10020D04E5A713660A7')

# TODO: git-daemon libsecret

_make_paths=(
  prefix="/usr"
  gitexecdir="/usr/lib/git"
  perllibdir="$(/usr/bin/perl -MConfig -wle 'print $Config{installvendorlib}')"
)

_make_options=(
  CFLAGS="$CFLAGS"
  LDFLAGS="$LDFLAGS"
  INSTALL_SYMLINKS=1
  MAN_BOLD_LITERAL=1
  NO_PERL_CPAN_FALLBACKS=1
  USE_LIBPCRE2=1
)

build() {
  cd $pkgname-$pkgver

  make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    all

  # make -C contrib/credential/libsecret \
  #   "${_make_paths[@]}"
}

check() {
  cd $pkgname-$pkgver

  local jobs
  jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*') || true

  mkdir -p /dev/shm/git-test

  make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS=" -Q" \
    GIT_TEST_OPTS="--root=/dev/shm/git-test" \
    test
}

package() {
  cd $pkgname-$pkgver

  make \
    "${_make_paths[@]}" \
    "${_make_options[@]}" \
    DESTDIR="$pkgdir/" \
    install

  # libsecret credentials helper
  # install -Dm0755 contrib/credential/libsecret/git-credential-libsecret "$pkgdir"/usr/lib/git/
  
  # sysusers
  echo 'u git - "git daemon user" / /usr/bin/git-shell' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/git.conf
}
