# Maintainer: Zelun Kong <zelun.kong@outlook.com>

pkgname=dbus
pkgver=1.12.16
pkgrel=2
pkgdesc="Freedesktop.org message bus system"
arch=(x86_64)
url="https://wiki.freedesktop.org/www/Software/dbus/"
depends=(expat)
makedepends=(autoconf-archive)
optdepends=(audit)
source=(git+https://gitlab.freedesktop.org/dbus/dbus.git#tag=$pkgname-$pkgver?signed
        dbus-reload.hook)
sha256sums=('SKIP'
            '40a68351784cff90591656db57e77c9a97840ebf9e7a63eb1ccd6538e9c090f7')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059')

prepare() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/dbus-$pkgver \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --docdir=/usr/share/doc/dbus-$pkgver \
    runstatedir=/run \
    --disable-static \
    --disable-verbose-mode \
    --disable-checks \
    --disable-asserts \
    --disable-xml-docs \
    --disable-doxygen-docs \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --without-x \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/" install

  # pre-assigned uid (81)
  echo 'u dbus 81 "System Message Bus"' | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/dbus.conf

  install -Dm644 "$srcdir"/dbus-reload.hook -t "$pkgdir"/usr/share/libalpm/hooks
}
